' Gambas class file

STATIC PRIVATE $hServer AS CServer
STATIC PRIVATE $sUser AS String
STATIC PRIVATE $bIgnore AS Boolean

STATIC PUBLIC FUNCTION Run(hServer AS CServer, OPTIONAL sUser AS String) AS Boolean

  DIM hForm AS Form

  $hServer = hServer
  $sUser = sUser

  hForm = NEW FUser
  RETURN NOT hForm.ShowModal()

END

PRIVATE SUB GetInfo()

  DIM hUser AS DatabaseUser

  WITH $hServer.Handle
    .Open
    hUser = .Users[$sUser]
    txtPassword.Text = hUser.Password
    $bIgnore = TRUE
    chkAdmin.Value = hUser.Administrator
    $bIgnore = FALSE
  END WITH

FINALLY

  $hServer.Handle.Close

CATCH

  Message.Error(Error.Text)
  btnOK.Enabled = FALSE

END


PUBLIC SUB _new()

  txtServer.Text = $hServer.Name

  IF $sUser THEN

    ME.Title = ("Edit user")

    txtName.Text = $sUser
    txtName.ReadOnly = TRUE

    GetInfo

    txtPassword.SetFocus

  ELSE

    ME.Title = ("Create user")
    txtName.SetFocus

  ENDIF

  lblTitle.Text = ME.Title

END


PUBLIC SUB btnOK_Click()

  DIM sUser AS String
  DIM sPassword AS String

  IF $sUser THEN

    IF $hServer.EditUser($sUser, Trim(txtPassword.Text)) THEN RETURN

  ELSE

    sUser = Trim(txtName.Text)
    IF NOT sUser THEN
      Message.Warning(("Please type a user name."))
      RETURN
    ENDIF
    
    sPassword = Trim(txtPassword.Text)
    
    IF $hServer.CreateUser(sUser, chkAdmin.Value, sPassword) THEN RETURN
    
  ENDIF

  ME.Close(TRUE)

END

PUBLIC SUB btnCancel_Click()

  ME.Close()

END


PUBLIC SUB chkAdmin_Click()

  IF $bIgnore THEN RETURN

  IF $sUser THEN
    $bIgnore = TRUE
    chkAdmin.Value = NOT chkAdmin.value
    $bIgnore = FALSE
  ENDIF

END
