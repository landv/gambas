' Gambas module file

Property Read Name As String
Property Read Enabled As Boolean
Property Read Output As String

Private $hVC As CVersionControl

Private $bEnabled As Boolean
Private $bEnd As Boolean
Private $hEditor As TextEditor
Private $bOutput As Boolean
Private $sOutput As String
Private $dOutput As Date
Private $bIdent As Boolean

Private $bAuth As Boolean
Private $sUser As String
Private $sPassword As String

'Public (User) As String
'Public Password As String
'Public UseTerminal As Boolean

Public LANG_ENV As String[] = ["LC_ALL=C.UTF-8", "LANG=C.UTF-8", "LANGUAGE=C.UTF-8"]
Public Const DELIM_CHANGE As String = ("This line and the following will be ignored")

Public Sub Refresh()

  $bEnabled = True
  If CVersionControlSubversion.Check() Then
    $hVC = CVersionControlSubversion
  Else If CVersionControlGit.Check() Then
    $hVC = CVersionControlGit
  Else
    $hVC = CVersionControl
    $bEnabled = False
  Endif
  
  $bAuth = False
  CheckPaths
  FMain.OnVersionControlChange
  
End

Public Sub Insert(sText As String)

  If $hEditor Then
    $hEditor.Print(DConv(sText))
  Else
    If Not Settings["/QuietExternalCommands", False] Then FOutput.Insert(sText)
  Endif

End

Public Sub Shell(sCmd As String, Optional bSilent As Boolean, Optional aEnv As String[]) As String
  
  Dim sResult As String
  
  If Not bSilent Then Insert(sCmd & "\n")
  Shell sCmd With aEnv To sResult
  'Insert(sResult)
  Return sResult
  
End


Public Sub Run(aCmd As String[], Optional bIdent As Boolean, Optional bOutput As Boolean, Optional aEnv As String[]) As Integer

  Dim sPassword As String
  Dim hProcess As Process
  Dim sCmd As String
  
  $bEnd = False
  $bIdent = bIdent
  $sOutput = ""
  $bOutput = bOutput
  $dOutput = Now
  
  ' If bIdent Then
  '   'aCmd.Add("--non-interactive")
  '   If User Then
  '     aCmd.Add("--username")
  '     aCmd.Add(User)
  '     If Password Then
  '       sPassword = " --password " & Password
  '       aCmd.Add("--password")
  '       aCmd.Add(Password)
  '     Endif
  '   Endif
  ' Endif
  
  hProcess = Exec aCmd With aEnv For Input Output As "Process" 
  
  sCmd = aCmd.Join(" ")
  If sPassword Then sCmd = Replace(sCmd, sPassword, "********")

  Insert(sCmd & "\n")

  Do
    Wait
    
    If $bEnd Then Break
    
    If DateDiff($dOutput, Now, gb.Second) >= 30 Then
      Close #hProcess
      hProcess.Kill
      Insert("** " & ("command timeout") & " **\n")
    Endif
    
    Sleep 0.1
  Loop
    
  Return Process.LastValue

End

Public Sub Process_Error(({Error}) As String)
  
  Insert({Error})
  
End


Public Sub Process_Read()

  Dim sData As String
  Dim iPos As Integer
  Dim sLine As String

  sData = Read #Last, -256
  $sOutput &= Replace(sData, "\r", "")
  If $bOutput Then Insert(sData)
  'Insert(sData)
  $dOutput = Now
  
  iPos = RInStr($sOutput, "\n")
  If iPos = 0 Then
    sLine = $sOutput
  Else
    sLine = Mid$($sOutput, iPos + 1)
  Endif
  
  If $bIdent Then
    If Right(RTrim(sLine)) = ":" Then
      If InStr(sLine, "username", 1, gb.IgnoreCase) Then 
        If $bOutput Then Insert(sLine)
        If Not $bAuth Then GetAuthentication()
        If Not $sUser Then Goto CANCEL
        Print #Last, $sUser
        Insert(User & "\n")
        $sOutput = ""
      Else If InStr(sLine, "password", 1, gb.IgnoreCase) Then 
        If $bOutput Then Insert(sLine)
        If Not $bAuth Then GetAuthentication()
        Print #Last, $sPassword
        Insert("********" & "\n")
        $sOutput = ""
      Else If InStr(sLine, "passphrase", 1, gb.IgnoreCase) Then 
        If $bOutput Then Insert(sLine)
        If Not $bAuth Then GetAuthentication(True)
        Print #Last, $sPassword
        Insert("********" & "\n")
        $sOutput = ""
      Endif
    Endif
  Endif
  
  Return
  
CANCEL:

  Last.Kill

End

Public Sub Process_Kill()

  $bEnd = True

End

Public Sub AddFile(sPath As String, Optional bForce As Boolean)

  $hVC.AddFile(sPath, bForce)

  ' If $bCVS Then
  '   'NB: For CVS file must already exist
  '   Run(["cvs", "add", sPath])
  ' Endif

End

Public Sub AddDir(sPath As String)

  $hVC.AddDir(sPath)

  ' If $bCVS Then
  '   'NB: For CVS, directory must already exist
  '   Run(["cvs", "add", sPath])
  ' Endif

End

' Must be called *after* the file has been deleted

Public Sub RemoveFile(sPath As String, Optional (bForce) As Boolean)

  $hVC.RemoveFile(sPath, bForce)
  ' If $bCVS Then
  '   'NB: For CVS do 'cvs remove file' first then delete file
  '   Run(["cvs", "remove", sPath])
  ' Endif
  CheckPaths

End

' Must be called *after* the directory has been deleted
' The directory must be empty

Public Sub RemoveDir(sPath As String)

  $hVC.RemoveDir(sPath)
  ' If $bCVS Then
  '   'NB: For CVS do 'cvs remove dirname' first then delete dirname
  '   Run(["cvs", "remove", sPath])
  ' Endif
  CheckPaths

End

' Must be called *after* the file has been moved

Public Sub MoveFile(sOld As String, sNew As String)

  $hVC.MoveFile(sOld, sNew)

  ' If $bCVS Then
  '   'NB: make sure the file is added BEFORE
  '   AddFile(sNew)
  '   'NB: make sure the file deleted AFTER
  '   RemoveFile(sOld)
  ' Endif
  CheckPaths

End

Public Sub CheckPaths()

  Project.ResetFlags
  $hVC.CheckPaths()

End

Public Sub MoveDir(sOld As String, sNew As String) As Boolean

  $hVC.MoveDir(sOld, sNew)
  CheckPaths

End

Public Sub Status(Optional sPath As String) As String

  Return $hVC.Status(sPath)

End

Public Sub Commit(sChange As String) As Boolean

  Dim bRes As Boolean
  
  Inc Application.Busy
  bRes = $hVC.Commit(sChange)
  If Not bRes Then Project.Refresh
  Dec Application.Busy
  
  If bRes Then
    Message.Error(("The project could not be committed."))
  Endif
  
  Return bRes

End

Public Sub Update()

  Dim bErr As Boolean
  Dim hFile As Object

  Inc Application.Busy
  bErr = $hVC.Update()
  Project.Refresh
  Dec Application.Busy

  If bErr Then  
    Message.Error(("Unable to update project from repository."))
  Else
    Message.Info(("Project has been updated from repository successfully."))
  Endif
  
  For Each hFile In Project.Files
    If Project.IsConflict(hFile.Path) Then Try hFile.Close
  Next

End

Public Sub Revert(Optional sPath As String) As Boolean

  If sPath Then Return $hVC.Revert(sPath)

  If Message.Warning(("You are going to cancel your changes!"), ("Continue"), ("Cancel")) = 2 Then Return

  Inc Application.Busy
  
  $hVC.Revert()
  Project.Refresh
  Project.Reload
  
  Dec Application.Busy

End

Public Sub CheckoutSVN(sPath As String, sDir As String, hEditor As TextEditor)

  $hEditor = hEditor
  CVersionControlSubversion.checkout(sPath, sDir)
  $hEditor = Null

End

Public Sub InConflict(sPath As String) As Boolean

  Return $hVC.InConflict(sPath)

End

Public Sub Resolve(sPath As String, Optional sAccept As String) As Boolean

  Return $hVC.Resolve(sPath, sAccept)

End

Public Sub GetVersion(sDir As String) As String

  Return $hVC.GetVersion(sDir)

End

' Private Sub RemoveFirstLines(sStr As String, iCount As Integer) As String
' 
'   Dim iPos As Integer
' 
'   While iCount
'     iPos = InStr(sStr, "\n", iPos + 1)
'     If iPos = 0 Then Return
'     Dec iCount
'   Wend
' 
'   Return Mid$(sStr, iPos + 1)
' 
' End

Public Sub Diff(Optional sPath As String) As String

  If Not sPath Then sPath = Project.Dir
  Return $hVC.Diff(sPath)

End

Public Sub CanControlProject() As Boolean

  Return False

  ' Dim sDir As String
  ' 
  ' If $hVC <> CVersionControl Then Return
  ' 
  ' If $bSvn17 Then
  '   sDir = $sRealDir
  '   Do
  '     sDir = File.Dir(sDir)
  '     If Exist(sDir &/ ".svn") Then Return True
  '     If sDir = "/" Then Return False
  '   Loop
  ' Else
  '   If Exist($sRealDir &/ "../.svn") Then Return True
  ' Endif
  ' 
  ' If Exist($sRealDir &/ "../CVS") Or Exist($sRealDir &/ "../CVSROOT") Then Return True

End

Public Sub ControlProject()

  ' If CanControlProject() Then
  '   Project.CleanUp
  '   Try Kill Project.Dir &/ ".settings"
  '   Run(["svn", "add", "--parents", SvnPath$($sRealDir)])
  '   Project.Config.Save
  '   Refresh
  '   Project.Refresh
  ' Endif

End

Public Sub Info(bRemote As Boolean) As Collection

  Return $hVC.Info(bRemote)
  
End

Public Sub CleanUp(sPath As String)

  $hVC.CleanUp(sPath)
  
End

Private Function Enabled_Read() As Boolean

  Return $bEnabled

End

Private Function Output_Read() As String

  Return $sOutput

End

Public Sub GetBranches(ByRef sCurrent As String) As String[]
  
  Return $hVC.GetBranches(ByRef sCurrent)
  
End

Private Function Name_Read() As String

  Return $hVC.Name

End

Public Sub GetDefaultJournal() As String
  
  Return $hVC.GetDefaultJournal()
  
End

Public Sub GetDelimChange() As String

  Dim sStr As String

  sStr = String.Left("-------- " & DELIM_CHANGE & " ", 77)
  sStr &= String$(78 - String.Len(sStr), "-")
  Return sStr

End

Private Sub GetAuthentication(Optional bNoUser As Boolean) 

  Dialog.NoUser = bNoUser
  If Dialog.AskPassword() Then Return
  $sUser = Dialog.User
  $sPassword = Dialog.Password
  $bAuth = True

End
