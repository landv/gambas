' Gambas class file

Private $hModule As Object
Private $hMenu As Menu
Private $sName As String

Public Sub _new(hModule As Object, hMenuButton As MenuButton, hMenu As Menu)
  
  $hModule = hModule
  $hMenu = hMenu
  $sName = Object.Parent(GetEditor()).Name
  
  Object.Attach(hMenuButton, Me, "btnBookmark")
  
  Load
  
End

Private Sub GetEditor() As TextEditor

  Return $hModule.GetEditor()

End

Private Sub FillMenu(aBook As Integer[])
  
  Dim hMenu As Menu
  Dim iLine As Integer
  Dim sLine As String
  Dim hEditor As TextEditor
  
  $hMenu.Children.Clear
  
  ' mnuAddBookmark,2,"Define bookmark",,,T,T,,,T,,,"B","icon:/small/bookmark",""
  ' mnuClearBookmarks,2,"Clear all bookmarks",,,T,T,,,,,,"","icon:/small/delete",""
  ' mnuPreviousBookmark,2,"Previous bookmark",,,T,T,,,,,T,"PgUp","icon:/small/up",""
  ' mnuNextBookmark,2,"Next bookmark",,,T,T,,,,,T,"PgDown","icon:/small/down",""
  
  hMenu = New Menu($hMenu) As "mnuAdd"
  hMenu.Text = ("Define bookmark")
  hMenu.Picture = Picture["icon:/small/bookmark"]
  hMenu.Shortcut = "Ctrl+B"
  
  hMenu = New Menu($hMenu) As "mnuClear"
  hMenu.Text = ("Clear all bookmarks")
  hMenu.Picture = Picture["icon:/small/delete"]
  
  hMenu = New Menu($hMenu) As "mnuPrevious"
  hMenu.Text = ("Previous bookmark")
  hMenu.Picture = Picture["icon:/small/up"]
  hMenu.Shortcut = "Alt+PgUp"
  
  hMenu = New Menu($hMenu) As "mnuNext"
  hMenu.Text = ("Next bookmark")
  hMenu.Picture = Picture["icon:/small/down"]
  hMenu.Shortcut = "Alt+PgDown"
  
  If aBook Then
    hMenu = New Menu($hMenu)
    hEditor = GetEditor()
    
    For Each iLine In aBook
      Try sLine = hEditor[iLine].Text
      If Error Then Continue
      
      hMenu = New Menu($hMenu) As "mnuGoto"
      hMenu.Tag = iLine
      If Len(sLine) > 36 Then sLine = Left(sLine, 32) & "..."
      hMenu.Text = Format(iLine + 1, "######") & ": " & Replace(sLine, "&", "&&")
    Next
  Endif
  
End

Private Sub Clear()
  
  Dim iLine As Integer
  Dim hEditor As TextEditor
  
  hEditor = GetEditor()
  For Each iLine In hEditor.Bookmarks
    hEditor[iLine].Bookmark = False
  Next
  
End

Public Sub Load()
  
  Dim aBook As Integer[]
  
  aBook = Project.Config["Bookmarks/" &/ $sName]
  If aBook Then GetEditor().Bookmarks = aBook
  FillMenu(aBook)
  
End

Private Sub Save()
  
  Dim aBook As Integer[] = GetEditor().Bookmarks
  
  Project.Config["Bookmarks/" &/ $sName] = aBook
  FillMenu(aBook)
  
End

Public Sub mnuAdd_Click()

  Dim hEditor As TextEditor
  
  hEditor = GetEditor()
  hEditor.Current.Bookmark = Not hEditor.Current.Bookmark  
  Save
  
End

Public Sub mnuClear_Click()

  Clear
  Save

End

Public Sub mnuGoto_Click()
  
  GetEditor().GotoCenter(0, Last.Tag)
  
End

Public Sub btnBookmark_Click()

  mnuAdd_Click

End

Public Sub MovePrevious()
  
  Dim hEditor As TextEditor = GetEditor()
  Dim iLine As Integer = hEditor.Line
  
  While iLine > 0
    Dec iLine
    If hEditor[iLine].Bookmark Then
      hEditor.GotoCenter(0, iLine)
      Return
    Endif
  Wend
  
End

Public Sub MoveNext()
  
  Dim hEditor As TextEditor = GetEditor()
  Dim iLine As Integer = hEditor.Line
  
  While iLine < (hEditor.Count - 1)
    Inc iLine
    If hEditor[iLine].Bookmark Then
      hEditor.GotoCenter(0, iLine)
      Return
    Endif
  Wend
  
End

Public Sub mnuPrevious_Click()
  
  MovePrevious
  
End

Public Sub mnuNext_Click()

  MoveNext

End

Public Sub Update()
  
  Save
  
End
