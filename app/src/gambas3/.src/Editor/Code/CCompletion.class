' Gambas class file

Public Const GAMBAS As String = "g"

Property Read Enabled As Boolean

Private $hModule As Object
Private $bCompletion As Boolean

Public Sub _new(hModule As Object)
  
  $hModule = hModule
  
End

Private Sub GetEditor() As TextEditor
  
  Return $hModule.GetEditor()
  
End


Public Sub ReadConfig()
  
  $bCompletion = Settings["/Editor/Completion", True]
  
End

Public Sub Hide()
  
  FCompletion.HideFrom(GetEditor())
  
End

Private Sub CheckCompletion(sMode As String)
  
  If Not $bCompletion Then Return
  If GetEditor().Selected Then Return
  
  $hModule.CheckCompletion(sMode)
  
End


Public Sub OnKeyPress() As Boolean

  Dim hEditor As TextEditor = GetEditor()
  Dim bStop As Boolean
  Dim iCol As Integer
  
  If hEditor.ReadOnly Then Return
  
  iCol = hEditor.Column

  If FCompletion.VisibleFrom(hEditor) Then
    bStop = FCompletion.ManageKey()
  Endif

  If bStop Then 
    Stop Event
    Return True
  Endif

  If Key.Code = Key.Space Then

    CheckCompletion(" ")
  
  Else If Key.Code = Key.Backspace Then

    CheckCompletion("<")
    If iCol <> hEditor.Column Then
      Stop Event
      Return True
    Endif

  Else If Key.Text = "." Then

    CheckCompletion(".")
    
  ' Else If Key.Text = "_" Then
  '   
  '   CheckCompletion("_")
    
  Else If Key.Text = "/" Then
    
    CheckCompletion("/")

  Else If Key.Text And If $hModule.IsWordChar(Key.Text) Then
    
    CheckCompletion("A")
    
  Endif  
  
End

Private Function Enabled_Read() As Boolean

  Return $bCompletion

End

Public Sub GetWord(sLine As String, iPos As Integer) As String

  Dim sCar As String
  Dim iPos2 As Integer

  iPos2 = iPos

  Do
    If iPos <= 0 Then Break
    sCar = String.Mid$(sLine, iPos, 1)
    If Not $hModule.IsWordChar(sCar) Then Break
    Dec iPos
  Loop

  Inc iPos

  Do
    If iPos2 > String.Len(sLine) Then Break
    sCar = String.Mid$(sLine, iPos, 1)
    If Not $hModule.IsWordChar(sCar) Then Break
    Inc iPos2
  Loop

  Return String.Mid$(sLine, iPos, iPos2 - iPos)

Catch

End
