' Gambas module file

Private $cOpen As New Collection
Private $cClose As New Collection

Private $iLine As Integer
Private $iLevel As Integer
Private $bLastNewLine As Boolean
Private $bNeedNewLine As Boolean
Private $iLastDim As Integer
Private $iLastSelect As Integer
Private $bLastWrap As Boolean

Private $bIndentDim As Boolean
Private $bRemoveSpace As Boolean
Private $bKeepVoidLine As Boolean

Public Sub _init()
  
  Dim sStr As String
  
  For Each sStr In ["DO", "FOR", "ELSE", "SELECT", "CASE", "DEFAULT", "WHILE", "REPEAT", "WITH", "CATCH", "FINALLY"]
    $cOpen[sStr] = True
  Next
  For Each sStr In ["LOOP", "NEXT", "ENDIF", "END IF", "ELSE", "CASE", "DEFAULT", "WEND", "UNTIL", "END WITH", "CATCH", "FINALLY"]
    $cClose[sStr] = True
  Next
  ' IF and END SELECT are special
  
End

Private Sub RemoveLine(aPos As Integer[], iLine As Integer)

  Dim I As Integer
  
  While I < aPos.Count
    If aPos[I] = iLine Then
      aPos.Remove(I)
    Else
      If aPos[I] > iLine Then Dec aPos[I]
      Inc I
    Endif
  Wend
  
End

Private Sub InsertLine(aPos As Integer[], iLine As Integer, Optional iCount As Integer = 1)

  Dim I As Integer
  
  While I < aPos.Count
    If aPos[I] >= iLine Then aPos[I] += iCount
    Inc I
  Wend
  
End


Public Sub Run(hEditor As Editor, Optional iStart As Integer, Optional iEnd As Integer = -1)
  
  Dim sLine As String
  Dim aLine As String[]
  Dim iLine As Integer
  Dim iLevel As Integer
  Dim aBookmark As Integer[]
  Dim bBookmark As Boolean
  Dim aBreakpoint As Integer[]
  Dim bBreakpoint As Boolean
  Dim I As Integer
  
  aBreakpoint = hEditor.Breakpoints
  aBookmark = hEditor.Bookmarks
    
  $bIndentDim = Settings["/FormatCode/IndentLocal", 1]
  $bRemoveSpace = Settings["/FormatCode/RemoveSpaces", 0]
  $bKeepVoidLine = Settings["/FormatCode/KeepVoidLines", 0]
  
  If iEnd < 0 Then
    iEnd = hEditor.Lines.Count - 1
  Endif
  
  $iLevel = iLevel
  $bLastNewLine = True
  $iLastDim = -1
  $iLastSelect = -1
  $bLastWrap = False
  
  hEditor.Begin
  
  iLine = 0
  While iLine <= iEnd
    
    $iLine = iLine
    sLine = hEditor.Lines[iLine].Text
    sLine = IndentLine(sLine)
    
    If iLine < iStart Then
      Inc iLine
      Continue
    Endif
    
    If $bRemoveSpace Then sLine = RTrim(sLine)
    aLine = Split(sLine, "\n")
    
    If aLine.Count <= 1 Then
      hEditor.Lines[iLine].Text = sLine
    Else
      hEditor.Lines[iLine].Text = aLine[0]
      For I = 1 To aLine.Max
        hEditor.Insert(aLine[I] & "\n", iLine + I, 0)
      Next
      
      If aBookmark.Count Then
        InsertLine(aBookmark, iLine + 1, aLine.Max)
        bBookmark = True
      Endif
      If aBreakpoint.Count Then
        InsertLine(aBreakpoint, iLine + 1, aLine.Max)
        bBreakpoint = True
      Endif
      
      iLine += aLine.Max
    Endif
    
    Inc iLine
  Wend
  
  If Not $bKeepVoidLine Then
    
    iLine = Max(1, iStart)
    While iLine <= iEnd
      
      If Not Trim(hEditor.Lines[iLine].Text) And If Not Trim$(hEditor.Lines[iLine - 1].Text) Then
        
        hEditor.Remove(iLine - 1, 0, iLine, 0)
        Dec iEnd
        
        If aBookmark.Count Then
          RemoveLine(aBookmark, iLine)
          bBookmark = True
        Endif
        If aBreakpoint.Count Then
          RemoveLine(aBreakpoint, iLine)
          bBookmark = True
        Endif
        
      Else
        
        Inc iLine
        
      Endif
      
    Wend
    
  Endif
  
  If bBookmark Then hEditor.Bookmarks = aBookmark
  If bBreakpoint Then hEditor.Breakpoints = aBreakpoint
    
  hEditor.End
  
End

Private Sub IsStruct() As Boolean
  
  Dim aSym As String[] = Highlight.Symbols
  Dim iInd As Integer
  
  If aSym.Count >= 3 Then
    If aSym[iInd] = "PUBLIC" Then Inc iInd
    Return aSym[iInd] = "STRUCT"
  Endif
  
End

Private Sub IsEndStruct() As Boolean
  
  Dim aSym As String[] = Highlight.Symbols
  
  If aSym.Count = 2 And If aSym[0] = "END" And If aSym[1] = "STRUCT" Then Return True
  
End

Private Sub IndentLine(sLine As String) As String
  
  Dim aSym As String[]
  Dim sAdd As String
  Dim iNextLevel As Integer
  Dim sSym As String
  Dim iPos As Integer
  Dim iType As Integer
  Dim bLastWrap As Boolean
  
  sLine = LTrim(sLine)
  
  If sLine Then
    
    If Left(sLine) = "'" Then
      Return Space$(Project.TabSize * $iLevel) & sLine
    Endif
    
    aSym = Highlight.Analyze(Highlight.Purge(Trim(sLine)))
    While aSym.Count
      iType = Highlight.Types[aSym.Max]
      If iType <> Highlight.Comment And If iType <> Highlight.Help Then Break
      aSym.Remove(aSym.Max)
    Wend
    
    If aSym.Count Then
      
      bLastWrap = $bLastWrap
      $bLastWrap = False
      If iType = Highlight.Operator And If InStr(":]);.!", aSym[aSym.Max]) = 0 And If aSym[0] <> "PRINT" Then $bLastWrap = True 
      
      If $bNeedNewLine Then
        If Not $bLastNewLine Then sAdd = "\n"
        $bNeedNewLine = False
      Endif
      
      If aSym.Count = 2 And If aSym[1] = ":" Then
        iNextLevel = $iLevel
        Dec $iLevel
      Else If FEditor.IsCurrentProc() Then
        $iLevel = 0
        iNextLevel = 1
        $bNeedNewLine = True
        If Not $bLastNewLine Then sAdd = "\n"
      Else If FEditor.IsCurrentEndProc() Then
        $iLevel = 0
        iNextLevel = 0
        $bNeedNewLine = True
        If Not $bLastNewLine Then sAdd = "\n"
      Else If IsStruct() Then
        $iLevel = 0
        iNextLevel = 1
        If Not $bLastNewLine Then sAdd = "\n"
      Else If IsEndStruct() Then
        $iLevel = 0
        iNextLevel = 0
        $bNeedNewLine = True
      Else
        sSym = aSym[0]
        If sSym = "END" And If aSym.Count >= 2 Then sSym &= " " & aSym[1]
        
        If sSym = "DIM" Then
          
          $iLastDim = $iLine
          If Not $bIndentDim Then $iLevel = 0
          iNextLevel = 1
        
        Else
        
          If $iLastDim = ($iLine - 1) Then
            If Not $bLastNewLine Then sAdd = "\n"
          Endif
        
          If sSym = "END SELECT" Then
            $iLevel -= 2
            iNextLevel = $iLevel
          Else If Right(aSym[0]) = ":" Then
            iNextLevel = $iLevel
            $iLevel = 0
          Else If sSym = "IF" Then
            iNextLevel = $iLevel
            iPos = aSym.Find("THEN")
            If iPos < 0 Or If iPos = aSym.Max Then Inc iNextLevel
          Else If sSym = "SELECT" Then
            $iLastSelect = $iLine
            iNextLevel = $iLevel + 2
          Else
            If $cClose.Exist(sSym) Then Dec $iLevel
            iNextLevel = $iLevel
            If $cOpen.Exist(sSym) Then Inc iNextLevel
            If bLastWrap Then Inc $iLevel
          Endif
          
        Endif
        
      Endif
      
      $bLastNewLine = False 
      
      $iLevel = Max(0, $iLevel)
      iNextLevel = Max(0, iNextLevel)
      
      sLine = sAdd & Space$(Project.TabSize * $iLevel) & sLine
      $iLevel = iNextLevel
      
      Return sLine
      
    Endif
    
  Endif
  
  $bLastNewLine = True
  sLine = Space$(Project.TabSize * $iLevel)
  Return sLine
  
End
