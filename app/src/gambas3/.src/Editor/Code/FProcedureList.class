' Gambas class file

Private $aProc As String[]
Private $iCurrent As Integer
Private $iInit As Integer
Private $sClass As String
Private $sFind As String
Private $dFind As Float
Private $hModule As FEditor

Static Public Sub Popup(hModule As FEditor, sClass As String, aProc As String[], iCurrent As Integer, X As Integer, Y As Integer, W As Integer, H As Integer) As Integer
  
  Dim iRet As Integer
  
  FProcedureList.Init(hModule, sClass, aProc, iCurrent, W, H)
  iRet = FProcedureList.ShowPopup(X, Y) - 1
  FProcedureList.Exit
  Return iRet
  
End

Public Sub Init(hModule As FEditor, sClass As String, aProc As String[], iCurrent As Integer, W As Integer, H As Integer)
  
  $hModule = hModule
  $sClass = sClass
  ' Copy it, as it can disappear during the Editor_Change() method
  $aProc = aProc.Copy()
  $iInit = iCurrent
  $iCurrent = iCurrent
  
  gvwProc.Rows.Count = aProc.Count
  gvwProc.Rows.UnselectAll
  gvwProc.Columns.Count = 1
  Project.SetNormalFont(gvwProc)
  gvwProc.Rows.H = gvwProc.Font.Height + 4

  Me.Resize(W, Min(H, gvwProc.Rows.Count * gvwProc.Rows.Height + 2))
  Try gvwProc[$iCurrent, 0].EnsureVisible
  
  $sFind = ""
  $dFind = Timer
  
End

Public Sub Exit()
  
  $hModule = Null
  
End


Public Sub gvwProc_Data(Row As Integer, (Column) As Integer)
  
  Dim hSymbol As CSymbolInfo
  Dim sName As String
  
  'Dim hColor As Color
  
  Try sName = $aProc[Row]
  If Not sName Then Return
  
  gvwProc.Data.Text = sName

  If $sClass Then
    
    'cSymbol = CComponent.GetClassSymbols($sClass)
    Try hSymbol = $hModule.Scan[sName]

    If hSymbol Then
      If hSymbol.Kind = "M" Then gvwProc.Data.Font.Bold = True
      If hSymbol.NotPublic Then 
        'hColor = Color(Color.Foreground)
        'hColor.Value = 255 - (255 - hColor.Value) * 0.6
        gvwProc.Data.Foreground = Color.Merge(Color.Foreground, Color.Background, 0.4)
      Endif
      'gvwProc.Data.Picture = Picture[hSymbol.GetIcon()]
    Else
      gvwProc.Data.Foreground = Color.Merge(Color.Foreground, Color.Background, 0.4)
    Endif
  
  Endif
    
  If Left(sName) = "_" Then 
    'gvwProc.Data.Picture = Picture["img/16/point.png"]
    'gvwProc.Data.Foreground = Color.Lighter(Color.Foreground)
    If FCompletion.GetSpecialMethods().Exist(sName, gb.IgnoreCase) Then 
      gvwProc.Data.Background = Color.Background
    Endif
  Else If InStr(sName, "_") Then
    'gvwProc.Data.Picture = Picture["img/16/event.png"]
    gvwProc.Data.Font.Italic = True
  Else If sName == "main" Then
    gvwProc.Data.Background = Color.Background
  Endif
  
  If Row = $iCurrent Then 
    gvwProc.Data.Background = Color.SelectedBackground
    gvwProc.Data.Foreground = Color.SelectedForeground
  Else If Row = $iInit Then
    gvwProc.Data.Background = Color.LightBackground
  Endif
  
End

Public Sub gvwProc_MouseUp()
  
  Me.Close($iCurrent + 1)
  
End

Private Sub SetCurrent(iCurrent As Integer)

  Dim iOld As Integer = $iCurrent
  
  If iCurrent = iOld Then Return
  
  $iCurrent = iCurrent
  If iOld >= 0 Then gvwProc.Rows[iOld].Refresh
  If iCurrent >= 0 Then 
    gvwProc.Rows[iCurrent].Refresh
    gvwProc[iCurrent, 0].EnsureVisible
  Endif

End

Public Sub gvwProc_MouseMove()
  
  SetCurrent(gvwProc.RowAt(Mouse.Y))
  
End

Public Sub gvwProc_MouseWheel()
  
  gvwProc_MouseMove
  
End

Private Sub FindProcedure()
  
  Dim iRow, iStart As Integer
  
  iStart = $iCurrent
  iRow = iStart
  
  Do
    If LCase(gvwProc[iRow, 0].Text) Begins $sFind Then
      SetCurrent(iRow) 'Try gvwProc.Rows[iRow].Selected = True
      Break
    Endif
    Inc iRow
    If iRow >= gvwProc.Rows.Count Then
      iRow = 0
    Endif
    If iRow = iStart Then Break
  Loop
  
End

Public Sub gvwProc_KeyPress()
  
  If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.Space Then
    gvwProc_MouseUp
    Stop Event
  Else If Key.Code = Key.F6 Or If Key.Code = Key.Escape Then
    Me.Close
    Stop Event
  Else If Key.Text Then
    If (Timer - $dFind) >= 1 Then $sFind = ""
    $sFind &= String.LCase(Key.Text)
    $dFind = Timer
    FindProcedure
  Else If Key.Code = Key.Up Then
    If $iCurrent > 0 Then SetCurrent($iCurrent - 1)
    Stop Event
  Else If Key.Code = Key.Down Then
    If $iCurrent < (gvwProc.Rows.Count - 1) Then SetCurrent($iCurrent + 1)
    Stop Event
  Else If Key.Code = Key.Home Then
    SetCurrent(0)
    Stop Event
  Else If Key.Code = Key.End Then
    SetCurrent(gvwProc.Rows.Count - 1)
    Stop Event
  Endif
  
End

Public Sub Form_Activate()
  
  gvwProc.SetFocus
  'Debug gvwProc.ScrollY
  
End

' Public Sub gvwProc_Scroll()
' 
'   Dim iRow As Integer = gvwProc.RowAt(Mouse.ScreenY - gvwProc.ScreenY)
'   
'   Try gvwProc.Rows[iRow].Selected = True
'   Debug gvwProc.ScrollY
' 
' End
