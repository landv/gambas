' Gambas class file

Private $hProcess As Process
Private $dDate As Date
Private $bWorking As Boolean

Public Sub Run(hProcess As Process) 

  $hProcess = hProcess
  $dDate = Now
  Me.ShowModal()

End

Public Sub btnOK_Click()

  Dim sDir As String
  Dim sTempProject As String
  Dim sTempInfo As String
  Dim sCore As String
  Dim sTemp As String
  Dim sCmd As String
  'Dim hProcess As Process
  Dim aSendCC As String[]

  If txtDesc.Visible And If txtDesc.Foreground <> Color.Default Then
    txtDesc.SetFocus
    Return
  Endif
    
  If $bWorking Then Return
  $bWorking = True
  
  If chkSend.Value Then
    
    Inc Application.Busy
    
    sDir = Temp$()
    Try Mkdir sDir
    
    If Not chkNoProject.Value Then

      sTempProject = sDir &/ Project.GetDefaultArchiveBaseName() & ".tar.bz2"
      Project.MakeSourcePackageTo(sTempProject) ', True)
      
    Endif
    
    Copy Project.Dir &/ ".project" To sDir &/ "project.conf"
    
    sTempInfo = sDir &/ "info.txt"
    File.Save(sTempInfo, FSystemInfo.GetInfo().Join("\n"))
    
    sCore = Project.Dir &/ "core"    
    If Not Exist(sCore) Then sCore &= "." & $hProcess.Id
    If Exist(sCore) And If System.Exist("gdb") Then
      Shell "gdb gbx3 " & Shell$(sCore) & " -batch -ex \"bt full\" >" & Shell$(sDir &/ "gdb.txt") & " 2>&1" Wait
      ' hProcess = Shell "gdb gbx3 " & Shell$(sCore) & " -batch -ex \"bt full\" >" & Shell$(sDir &/ "gdb.txt") & " 2>&1"
      ' While hProcess.State = Process.Running
      '   Wait 0.1
      ' Wend
    Endif
    
    sTemp = File.SetName(Temp$(), Project.GetDefaultArchiveBaseName()) & "-crash-" & Format($dDate, "yymmdd-hhnnss") & ".tar.bz2"
    Try Kill sTemp
    
    sCmd &= "cd " & Shell$(sDir) & " && tar cfvhj " & Shell$(sTemp) & " *" & " >/dev/null 2>&1"
    Shell sCmd Wait
    ' hProcess = Shell sCmd 
    ' While hProcess.State = Process.Running
    '   Wait 0.1
    ' Wend
    
    If Not Exist(sTemp) Then Error.Raise(Subst$(("The '&1' command has failed."), "tar"))
    
    If Stat(sTemp).Size <= 256000 Then aSendCC = ["gambas-user@lists.sourceforge.net"]
    
    Desktop.SendMail(["gambas@users.sourceforge.net"], aSendCC,, "[CRASH REPORT] " & If(Project.Title, Project.Title, Project.Name), "Hello,\n\nDamned, my program crashed Gambas that way :\n\n" & Trim(txtDesc.Text) & "\n\n", sTemp)
    
    Dec Application.Busy
    
  Endif

  $bWorking = False
  Me.Close
  
Catch
  
  Application.Busy = 0
  Message.Error(("Unable to send bug report.") & "\n\n" & Error.Text)
  $bWorking = False

End

Public Sub Form_Open()

  lblMessage.Text = "<h2>" & ("The program has stopped unexpectedly!") & "</h2>" & Html(Debug.GetSignal($hProcess.Value)) & " (" & $hProcess.Value & ")" ' & Subst(("Please send a bug report to the following mail address: &1"), "gambas-user@users.sourceforge.net.")
  chkSend_Click

End

Public Sub Form_Close()

  If $bWorking Then Stop Event

End

Public Sub btnCancel_Click()

  If $bWorking Then Return
  Me.Close

End

' Public Sub Form_Arrange()
' 
'   spnWait.Move(spnWait.Parent.W - spnWait.Width - Desktop.Scale, 0)
' 
' End

Public Sub chkSend_Click()

  txtDesc.Visible = chkSend.Value
  chkNoProject.Enabled = chkSend.Value
  Me.H = Desktop.Scale * If(txtDesc.Visible, 50, 30)
  txtDesc_LostFocus

End

Public Sub txtDesc_LostFocus()
  
  If txtDesc.Length = 0 Then
    txtDesc.Text = ("Please describe how to reproduce the crash here.")
    txtDesc.Foreground = Color.LightForeground
    txtDesc.Font.Italic = True
  Endif
  
End

Public Sub txtDesc_GotFocus()
  
  If txtDesc.Foreground <> Color.Default Then
    txtDesc.Text = ""
    txtDesc.Foreground = Color.Default
    txtDesc.Font.Italic = False
  Endif
  
End
