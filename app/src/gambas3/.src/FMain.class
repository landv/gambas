' Gambas class file

Public NoMessage As Integer

Private $sKey As String
Private $bCtrl As Boolean
Private $bLock As Boolean
Private $sKeyDrag As String
Private $sDeleteKey As String
Private $bNeedProperty As Boolean

Private $bUpdateRecentMenu As Boolean
Private $bInTreeView As Boolean
Private $bInRename As Boolean
Private $sKeyInfo As String
Private $bNoSelect As String
Private $bHasJustCollapsed As Boolean
Private $bExpandOnMouseUp As Boolean

Public Sub _new()
  
  Me.Resize(800, 600)
  
  Project.ProjectTree = tvwProject
  Project.Workspace = wrkProject
  
  tvwProject.Columns.Count = 2
  tvwProject.Columns[0].Width = Desktop.Width 
  
  Design.Init
  
  FDebugInfo.Load(panDebug)
  FToolbox.Load(panTool)
  FProperty.Load(tabProperty)
  tabProperty.Index = 1
  FFormStack.Load(tabProperty)
  tabProperty.Index = 0
  
  FSearch.Load
  
  FOption.Load
  FHelpBrowser.Load
  
  'tvwProject.Columns.Count = 1
  
End

Public Sub Form_Open()
  
  'DEBUG
  
  Settings.Read(Me)
  Settings.Read(panProject, "panProject")
  Settings.Read(panProperty, "panProperty")
  Settings.Read(panTool, "panTool")
  'Settings.Read(panStack, "panStack")
  Settings.Read(panDebug, "panDebug")
  
  'ShowTool(FALSE)
  
  ReadConfig
  
  FDebugInfo.UpdateView
  
  tvwProject_Select
  'tabProperty_Click
  'btnShowMenu_Click
  
  mnuOpenExample.Tag = "."
  
  Try Project.ActiveForm.SetFocus
  'tvwProject_LostFocus
  
End

Public Sub Form_Close()
  
  Dim bRet As Boolean
  
  If Project.Close() Then
    Stop Event
  Else
    Settings.Write(Me)
    Settings.Write(panProject, "panProject") 
    Settings.Write(panProperty, "panProperty") 
    Settings.Write(panTool, "panTool") 
    'Settings.Write(panStack, "panStack") 
    Settings.Write(panDebug, "panDebug") 
    
    ' We must close them now, otherwise the Close event is raised when the embedded forms
    ' are destroyed, and then the Settings.Save() method gets no TopLevel form anymore for generating the
    ' settings key !
    FProperty.Close
    FDebugInfo.Close
    
    Design.Exit
    Project.Exit
  Endif
  
End

Private Sub OpenProject()
  
  Dim sPath As String
  
  sPath = FOpenProject.Run(True)
  If Not sPath Then Return
  
  Project.Open(sPath, FOpenProject.InAnotherWindow)
  UpdateRecentMenu
  
End

Public Sub tvwProject_Activate()
  
  Dim sKey As String
  Dim hModule As CModule
  
  sKey = tvwProject.Key
  
  If sKey Then
    tvwProject[sKey].Expanded = True
    If CanEdit(sKey) Then
      If $bCtrl Then
        If File.Ext(sKey) <> "module" Then
          sKey = File.SetExt(sKey, "class")
        Endif
      Endif
      Project.OpenFile(sKey)
    Endif
  Endif
  
End

Public Sub mnuEditFile_Click()
  
  tvwProject_Activate()
  
End

Public Sub mnuEditClass_Click()
  
  $bCtrl = True
  tvwProject_Activate()
  
End

Public Sub mnuQuit_Click()
  
  Me.Close
  
End

Public Sub mnuSaveFile_Click()
  
  Dim sPath As String
  Dim hForm As Object
  
  sPath = GetCurrent()
  If Not sPath Then Return
  
  hForm = Project.Files[sPath]
  If Not hForm Then Return
  
  hForm.Save
  
End

Public Sub mnuView_Show()
  
  'mnuViewTool.Checked = Project.ToolForm.Visible
  'mnuViewProperty.Checked = Project.PropertyForm.Visible
  'mnuViewMessage.Checked = Project.MessageForm.Visible
  
End

Public Sub mnuAbout_Click()
  
  FAbout.Run
  
End

Public Sub mnuDeleteFile_Click()
  
  Dim sPath As String
  Dim sName As String
  
  sPath = GetCurrent()
  If Not sPath Then Return
  
  If IsDir(sPath) Then
    
    If Stat(sPath).Type = gb.Link Then
      If Message.Delete(File.Name(sPath) & "\n\n" & ("Do you really want to delete this link ?"), ("Delete"), ("Cancel")) <> 1 Then Return
      Project.DeleteFile(sPath)
    Else
      If Message.Delete(File.Name(sPath) & "\n\n" & ("Do you really want to delete this directory ?"), ("Delete"), ("Cancel")) <> 1 Then Return
      Project.DeleteDir(sPath)
    Endif
    
  Else
    
    If Project.IsSourcePath(sPath) Or Project.IsConnectionPath(sPath) Then
      sName = File.BaseName(sPath)
    Else
      sName = File.Name(sPath)
    Endif
    
    If Message.Delete(sName & "\n\n" & ("Do you really want to delete this file ?"), ("Delete"), ("Cancel")) <> 1 Then Return
    Project.DeleteFile(sPath)
    
  Endif
  
Catch
  
  FMain.ShowError(("Cannot delete file or directory") & "\n\n" & Error.Text)
  
End

Public Sub tvwProject_Menu()
  
  'mnuPopup.Enabled = TRUE
  GetCurrent()
  
  If $sKey And If Project.IsLocked($sKey) Then Return
  
  If $sKey = Project.KEY_CONNECTION Or If Project.IsConnectionPath($sKey) Then
    mnuDatabase.Popup
  Else
    mnuPopup.Popup
  Endif
  'mnuPopup.Enabled = FALSE
  
End

Private Function GetCurrent() As String
  
  $sKey = ""
  If Not tvwProject.Key Then Return
  If Not tvwProject.Current.Selected Then Return
  $sKey = tvwProject.Key
  If Left$($sKey) = "$" Then Return
  If IsProject($sKey) Or If Project.IsLocked($sKey) Then Return
  Return $sKey
  
End

Private Function GetCurrentDir(Optional sKey As String) As String
  
  If sKey Then 
    $sKey = sKey
  Else
    GetCurrent
  Endif
  If Not $sKey Then Return 
  If Project.IsLocked($sKey) Then Return
  If $sKey = Project.KEY_MISC Then Return Project.Dir
  If $sKey = Project.KEY_PROJECT Then Return Project.Dir &/ ".hidden"
  If Project.SourceDir <> Project.Dir
    If $sKey = Project.KEY_SOURCE Then Return Project.SourceDir
  Endif
  If Left$($sKey) = "$" Then Return
  
  If IsDir($sKey) Then
    Return $sKey
  Else
    Return File.Dir($sKey)
  Endif
  
End

Public Sub mnuPopup_Show()
  
  Dim sCurrent As String
  Dim bCurrent As Boolean
  Dim bFile As Boolean
  Dim bCanStoreFile As Boolean
  Dim hMenu As Menu
  Dim sParent As String
  
  sCurrent = GetCurrent()
  bCurrent = sCurrent
  
  UpdateMenu
  
  If sCurrent And If Project.IsReadOnly(sCurrent) Then
    mnuNew.Visible = False
    If Not IsDir(sCurrent) Or If Stat(sCurrent).Type <> gb.Link Then
      mnuCut.Visible = False
      mnuCopy.Visible = False
      mnuPaste.Visible = False
    Endif
    mnuRenameFile.Visible = False
  Endif
  
  If $sKey = Project.KEY_SOURCE Or If sCurrent Begins Project.SourceDir &/ "/" Then
    
    mnuRenameFile.Visible = mnuRenameFile.Visible And bCurrent And Not Project.Running
    mnuDeleteFile.Visible = mnuDeleteFile.Visible And CanDelete(sCurrent) And Not Project.Running
    
    mnuEditFile.Visible = mnuEditFile.Visible And CanEdit(sCurrent)
    mnuEditClass.Visible = mnuEditFile.Visible And Project.IsFormPath(sCurrent)
    
    mnuStartup.Visible = mnuEditFile.Visible 
    If mnuStartup.Visible Then
      mnuStartup.Checked = (Project.Startup = File.BaseName(sCurrent))
      mnuStartup.Enabled = Not Project.Running
    Endif
    
    If mnuCut.Visible Then
      
      mnuCut.Visible = bCurrent
      mnuCopy.Hide
      mnuPaste.Visible = True
      
      sParent = tvwProject[$sKey].ParentKey
      
      mnuPaste.Enabled = False
      mnuCut.Enabled = False
      
      If Not Project.Running Then
        
        If Clipboard.Format = MMime.CLASS Then
          If IsDir(sParent) Or If Not Project.IsSourcePath(sParent) Then mnuPaste.Enabled = True
        Endif
        
        If IsDir(sParent) Or If Not Project.IsSourcePath(sParent) Then mnuCut.Enabled = True
        
      Endif
      
    Endif
    
    mnuEditWith.Visible = False
    mnuCopyPath.Visible = mnuCopy.Visible
    
    If mnuNew.Visible Then
      
      mnuAddDirectory.Visible = Project.Dir <> Project.SourceDir
      mnuAddProjectLink.Visible = False 'mnuAddDirectory.Visible
      
      For Each hMenu In mnuNew.Children
        If Not hMenu.Tag Then Continue
        If Not CModule.All.Exist(hMenu.Tag) Then 
          hMenu.Hide
        Else
          hMenu.Visible = CModule[hMenu.Tag].Used
        Endif
      Next
      mnuNewClass.Show
      mnuNewModule.Show
      
    Endif
    
  Else
    
    mnuRenameFile.Visible = mnuRenameFile.Visible And bCurrent
    mnuDeleteFile.Visible = mnuDeleteFile.Visible And CanDelete(sCurrent)
    
    mnuEditFile.Visible = mnuEditFile.Visible And CanEdit(sCurrent)
    mnuEditClass.Visible = False
    
    mnuStartup.Visible = False
    
    bCanStoreFile = $sKey = Project.KEY_MISC Or $sKey = Project.KEY_PROJECT
    bFile = bCurrent
    
    If mnuCut.Visible Then
      mnuCut.Visible = bFile And Not IsDir($sKey)
      mnuCopy.Visible = bFile And Not IsDir($sKey)
      mnuPaste.Visible = bFile Or bCanStoreFile
      mnuPaste.Enabled = Clipboard.Format = MMime.FILE
    Endif
    
    mnuEditWith.Visible = Project.IsImagePath(sCurrent) And mnuEditFile.Visible
    mnuCopyPath.Visible = mnuCopy.Visible
    
    If mnuNew.Visible Then 
      
      mnuAddDirectory.Visible = True
      
      For Each hMenu In mnuNew.Children
        If Not hMenu.Tag Then Continue
        If CModule.All.Exist(hMenu.Tag) Then 
          hMenu.Hide
        Else
          hMenu.Visible = bFile Or bCanStoreFile
        Endif
      Next
      mnuNewClass.Hide
      mnuNewModule.Hide
      
    Endif
    
  Endif
  
  mnuAddVersion.Visible = Project.IsAdded($sKey)
  
End

Public Sub mnuDatabase_Show()
  
  Dim sCurrent As String = GetCurrent()
  Dim bVisible As Boolean
  
  bVisible = $sKey <> Project.KEY_CONNECTION
  
  mnuEditConnection.Visible = bVisible
  mnuKillConnection.Visible = bVisible
  mnuInfoConnection.Visible = bVisible
  If bVisible Then 
    mnuAddVersionConnection.Visible = Project.IsAdded($sKey)
  Else
    mnuAddVersionConnection.Visible = False
  Endif
  
End

Private Function FormatFile(sPath As String) As String
  
  Dim sText As String
  
  sText = File.Name(sPath)
  
  If Left$(sPath, Len(User.Home)) = User.Home Then
    sPath = "~" &/ Mid$(sPath, Len(User.Home) + 1)
  Endif
  
  Return sText & "  (" & File.Dir(sPath) & ")"
  
End

Public Sub UpdateRecentMenu()
  
  Dim hMenu As Menu
  Dim sPath As String
  Dim aList As CRecentFile[]
  Dim bSort As Boolean
  Dim hRecentFile As CRecentFile
  Dim sTitle As String
  Dim sLastTitle As String
  Dim hMenuTitle As Menu
  Dim cMenu As New Collection
  Dim cParent As New Collection
  Dim aMenu As New String[]
  Dim sDir As String
  Dim vVal As Variant
  Dim iVal As Integer
  Dim bIgnored As Boolean
  
  Inc Application.Busy
  
  bSort = Settings["/FWelcome/SortRecent", False]
  
  mnuOpenRecent.Children.Clear
  mnuOpenRecent.Enabled = CRecentFile.All.Count > 0
  
  If mnuOpenRecent.Enabled Then
    
    bSort = Settings["/FWelcome/SortRecent", False]
    
    aList = CRecentFile.Get(bSort)
    
    If bSort Then
      
      For Each hRecentFile In aList
        
        sPath = hRecentFile.Path
        Repeat
          If cMenu.Exist(sPath) Then Break
          cMenu[sPath] = True
          sPath = File.Dir(sPath)
          iVal = 0
          Try iVal = cParent[sPath]
          Inc iVal
          cParent[sPath] = iVal
        Until sPath = "/"
        
      Next
      
      For Each iVal In cParent
        If iVal >= 2 Then aMenu.Add(cParent.Key)
      Next
      aMenu.Sort
      Try aMenu.Remove(0)
      
      For Each hRecentFile In aList
        aMenu.Add(hRecentFile.Path)
      Next
      
      cMenu.Clear
      
      For Each sPath In aMenu
        
        sDir = sPath
        
        Do
          sDir = File.Dir(sDir)
          If sDir = "/" Then Break
          hMenuTitle = cMenu[sDir]
          If hMenuTitle Then Break
        Loop
        If Not hMenuTitle Then hMenuTitle = mnuOpenRecent
        
        If Exist(sPath &/ ".project") Then
          hMenu = New Menu(hMenuTitle) As "mnuOpenRecentFile"
          hMenu.Picture = Project.GetIcon(sPath, 16)
        Else
          hMenu = New Menu(hMenuTitle)
        Endif
        If hMenuTitle <> mnuOpenRecent Then 
          sTitle = Mid$(Replace(sPath, hMenuTitle.Tag, ""), 2)
        Else
          sTitle = Replace(sPath, System.User.Home, "~")
        Endif
        hMenu.Text = sTitle
        hMenu.Tag = sPath
        cMenu[sPath] = hMenu
        
      Next
      
    Else
      
      For Each hRecentFile In aList
        
        sPath = hRecentFile.Path
        
        sTitle = hRecentFile.GetSlot()
        
        If sTitle <> sLastTitle Then
          If sTitle = ("Today") Then
            hMenuTitle = mnuOpenRecent
          Else
            hMenuTitle = New Menu(mnuOpenRecent)
            hMenuTitle.Text = sTitle
          Endif
          sLastTitle = sTitle
        Endif
        
        hMenu = New Menu(hMenuTitle) As "mnuOpenRecentFile"
        hMenu.Tag = sPath
        hMenu.Text = FormatFile(sPath)
        hMenu.Picture = Project.GetIcon(hMenu.Tag, 16)
        
      Next
      
    Endif
    
    hMenu = New Menu(mnuOpenRecent)
    
    hMenu = New Menu(mnuOpenRecent) As "mnuSortRecent"
    hMenu.Text = ("&Sort by path")
    hMenu.Toggle = True
    Object.Lock(hMenu)
    hMenu.Checked = bSort
    Object.Unlock(hMenu)
    hMenu = New Menu(mnuOpenRecent) As "mnuClearRecent"
    hMenu.Text = ("&Clear history")
    hMenu.Picture = Picture["icon:/small/trash"]
    
  Endif
  
  Dec Application.Busy
  
End

Public Sub mnuSortRecent_Click()
  
  Settings["/FWelcome/SortRecent"] = Not Settings["/FWelcome/SortRecent", False]
  UpdateRecentMenu
  
End

' PUBLIC SUB UpdateRunWithMenu()
'   
'   DIM hMenu AS Menu
'   DIM iInd AS Integer
'   DIM sArg AS String
' 
'   mnuRunWith.Children.Clear
'   'mnuOpenRecent.Enabled = Project.Recent.Count > 0
' 
'   'IF Project.Recent.Count = 0 THEN RETURN
' 
'   mnuRunWithNoArg = NEW Menu(mnuRunWith) AS "mnuRunWithArg"
'   mnuRunWithNoArg.Text = ("(&None)")
'   mnuRunWithNoArg.Tag = 0
'   
'   hMenu = NEW Menu(mnuRunWith)
' 
'   FOR EACH sArg IN Project.Arguments
'     hMenu = NEW Menu(mnuRunWith) AS "mnuRunWithArg"
'     sArg = Replace(sArg, "\n", " ")
'     sArg = Replace(sArg, "&", "&&")
'     INC iInd
'     hMenu.Tag = iInd
'     IF Len(sArg) > 64 THEN sArg = Left(sArg, 64) & "..."
'     hMenu.Text = sArg
'   NEXT  
'   
'   hMenu = NEW Menu(mnuRunWith)
' 
'   hMenu = NEW Menu(mnuRunWith) AS "mnuEditArgument"
'   hMenu.Text = ("&Edit arguments...")
'   hMenu.Picture = Picture["icon:/small/edit"]
' 
'   SetArgument
'   
' END

Private Sub UpdateExampleMenu(Optional hParent As Menu)
  
  Dim hMenu As Menu
  Dim aList As String[]
  Dim iInd As Integer
  Dim sPath As String
  Dim sParent As String
  Dim sDir As String
  
  If Not hParent Then hParent = mnuOpenExample
  
  If Not hParent.Tag Then Return
  
  Inc Application.Busy 
  
  hParent.Children.Clear
  
  aList = Project.GetExamples(hParent.Tag)
  hParent.Tag = ""
  
  If aList Then 
    
    For Each sPath In aList
      
      ' IF InStr(sPath, "/") THEN
      '   sDir = File.Dir(sPath)
      '   IF sDir <> sParent THEN
      '     sParent = sDir
      '     hParent = NEW Menu(mnuOpenExample)
      '     hParent.Text = Project.ExampleTitle[sParent]
      '     IF NOT hParent.Text THEN hParent.Text = sParent
      '   ENDIF
      ' ELSE
      '   hParent = mnuOpenExample
      '   sParent = ""
      ' ENDIF
      
      If Exist(Project.EXAMPLES_DIR &/ sPath &/ ".project") Then
        hMenu = New Menu(hParent) As "mnuOpenRecentFile"
        hMenu.Tag = Project.EXAMPLES_DIR &/ sPath
        hMenu.Text = File.Name(sPath) 'FormatFile(sPath)
        hMenu.Picture = Project.GetIcon(hMenu.Tag, 16)
      Else 
        hMenu = New Menu(hParent) As "mnuOpenExample"
        hMenu.Tag = sPath
        hMenu.Text = Project.ExampleTitle[File.Name(sPath)]
        hMenu = New Menu(hMenu)
      Endif
      
    Next
    
  Endif
  
  Dec Application.Busy
  
End

Public Sub mnuOpenRecentFile_Click()
  
  Project.Open(Last.Tag)
  UpdateRecentMenu
  
End

Public Sub mnuClearRecent_Click()
  
  CRecentFile.Clear
  UpdateRecentMenu
  
End

Private Sub NewProject()
  
  Dim sPath As String
  
  sPath = FCreateProject.Run(True)
  If Not sPath Then Return
  
  Project.Open(sPath, FCreateProject.InAnotherWindow)
  UpdateRecentMenu
  
End

Public Sub mnuTip_Click()
  
  FTips.Run
  
End

' PUBLIC SUB mnuAddIcon_Click()
' 
'   IF FNewIcon.Run(GetCurrentDir()) THEN RETURN
'   Project.InsertFile(FNewIcon.Name, FNewIcon.DestDir, FNewIcon.Template)
' 
' END
' 
' 
' PUBLIC SUB mnuAddText_Click()
' 
'   IF FNewText.Run(GetCurrentDir()) THEN RETURN
'   Project.InsertFile(FNewText.Name, FNewText.DestDir, FNewText.Template)
' 
' END

Private Sub Rename(sKey As String)
  
  $bInRename = True
  tvwProject[sKey].Rename
  
End

Public Sub mnuAddDirectory_Click()
  
  Dim sPath As String
  Dim iInd As Integer = 1
  
  sPath = GetCurrentDir() &/ ("New folder")
  While Exist(sPath)
    Inc iInd
    sPath = GetCurrentDir() &/ ("New folder") & " (" & iInd & ")"
  Wend
  
  If Not Project.InsertDirectory(sPath, True) Then Return
  $sDeleteKey = sPath
  
  Rename(sPath)  
  'IF FNewDirectory.Run(GetCurrentDir()) THEN RETURN
  'Project.InsertDirectory(FNewDirectory.ParentDir &/ FNewDirectory.Name)
  
End

Private Function CanEdit(sPath As String) As Boolean
  
  If Not sPath Then Return
  If Left$(sPath) = "$" Then Return
  Return Not IsDir(sPath)
  
End

Private Function CanDelete(sPath As String) As Boolean
  
  If Not sPath Then Return
  If Left$(sPath) = "$" Then Return
  If IsDir(sPath) And If Stat(sPath).Type = gb.Link Then Return True
  If Project.IsReadOnly(sPath) Then Return False
  If tvwProject[sPath].Count = 0 Then Return True
  
End

Public Sub UpdateTranslate()
  
  Action["translate"].Visible = Project.Localize And Not Project.ReadOnly
  Action["translate"].Enabled = Not Project.Running
  'Action["component"].Visible = Project.CreateComponent 'AND NOT (Project.ReadOnly OR Project.Running)
  
End

Public Sub UpdateConsoleAction(bDebug As Boolean)
  
  'DIM bVal AS Boolean
  
  'HideDebug(Not bDebug)
  
  'bVal = Action["console"].Value
  panDebug.Action = If(FOutput.Parent, "console", "")
  FOutput.Action = If(FOutput.Parent, "", "console")
  
  If FOutput.Parent Then 
    Action["console"].Value = Not panDebug.Hidden
  Else 
    Action["console"].Value = FOutput.Visible
  Endif
  
  'Action["console"].Value = bVal  
  
End

Private Sub Translate()
  
  If Project.Running Then Return
  If Project.Compile() Then Return
  
  If Project.CheckProgram("msgmerge") Or If Project.CheckProgram("msgfmt") Then
    Message.Warning(("The GNU translation tools are not installed on your system.\n\nPlease install them to be able to do the translation."))
    Return
  Endif
  
  FTranslate.Run
  
End

Public Sub mnuDatabaseManager_Click()
  
  'Project.RunTool("gambas" & System.Version & "-database-manager")
  Project.RunTool("gambas-database-manager")
  
End

Public Sub mnuStartup_Click()
  
  If mnuStartup.Checked Then Return
  mnuStartup.Checked = True
  Project.DefineStartup(GetCurrent())
  
End

Private Function IsProject(sKey As String) As Boolean
  
  Return sKey = Project.Dir
  
End

Public Sub mnuCopy_Click()
  
  Dim sPath As String
  Dim sType As String
  
  sPath = GetCurrent()
  If Project.IsSourcePath(sPath) Then
    sType = MMime.CLASS
  Else
    sType = MMime.FILE
  Endif
  Clipboard.Copy("C" & sPath, sType)
  
  mnuPopup_Show
  
End

Public Sub mnuCopyPath_Click()
  
  Dim sPath As String
  
  sPath = GetCurrent()
  If Left(sPath, Len(Project.Dir)) = Project.Dir Then
    sPath = Mid$(sPath, Len(Project.Dir) + 2)
    'IF Left(sPath) = "/" THEN sPath = Mid$(sPath, 2)
  Endif
  
  Clipboard.Copy(sPath)
  
End

Public Sub mnuCut_Click()
  
  Dim sPath As String
  Dim sType As String
  
  sPath = GetCurrent()
  If Project.IsSourcePath(sPath) Then
    sType = MMime.CLASS
  Else
    sType = MMime.FILE
  Endif
  Clipboard.Copy("X" & sPath, sType)
  
  mnuPopup_Show
  
End

Public Sub mnuPaste_Click()
  
  Dim sFile As String
  Dim sAction As String
  Dim sSrc As String
  Dim sDst As String
  Dim sType As String
  Dim bSource As Boolean
  
  sDst = GetCurrentDir()
  bSource = Project.IsSourcePath(sDst)
  If bSource Then
    sType = MMime.CLASS
  Else
    sType = MMime.FILE
  Endif
  
  sFile = Clipboard.Paste(sType)
  If Not sFile Then Return
  
  sAction = Left$(sFile)
  sSrc = Mid$(sFile, 2)
  sDst &/= File.Name(sFile)
  
  Select Case sAction
      
    Case "C"
      Project.CopyFile(sSrc, sDst)
      Rename(tvwProject.Key)
      
    Case "X"
      If sSrc = sDst Then Return
      Project.MoveFile(sSrc, sDst, sType = MMime.CLASS)
      Clipboard.Clear
      
  End Select
  
End

Public Sub tvwProject_Select()
  
  Balloon.Hide(tvwProject)
  GetCurrent()
  RefreshMenu
  
  If tvwProject.Current.Children Then 
    ' Print Mouse.X ;; Mouse.Y ' generates crashes on random places, strange...
    'If Not tvwProject.Find(Mouse.X, Mouse.Y) Then
      $bExpandOnMouseUp = True
    'Endif
  Endif
  
End

Public Sub tvwProject_MouseUp()
  
  If $bExpandOnMouseUp And If Not $bHasJustCollapsed Then
    Try tvwProject.Current.Expanded = True
  Endif
  $bExpandOnMouseUp = False
  $bHasJustCollapsed = False
  
End

' PUBLIC SUB tvwProject_Expand()
' 
'   IF tvwProject.Item.Picture = Picture["img/16/close.png"] THEN
'     tvwProject.Item.Picture = Picture["img/16/open.png"]
'   ENDIF
' 
' END

' PUBLIC SUB tvwProject_Collapse()
' 
'   IF tvwProject.Item.Picture = Picture["img/16/open.png"] THEN
'     tvwProject.Item.Picture = Picture["img/16/close.png"]
'   ENDIF
' 
' END

Public Sub mnuCloseAll_Click()
  
  Project.CloseAllWindows
  
End

Public Sub ReadConfig()
  
  Dim bShow As Boolean
  
  '   DIM hFont AS Font
  '
  '   IF Settings["/UseSmallFont", FALSE] THEN
  '     hFont = Font["8"]
  '   ELSE
  '     hFont = Font["10"]
  '   ENDIF
  '
  '   tvwProject.Font = hFont
  
  tvwProject.Font.Grade = - Settings["/GlobalFont", 0]
  lblMessage.Font = tvwProject.Font
  tabProperty.TextFont = Font[CStr(- Settings["/TabStripFont", 0])]
  wrkProject.Font = tabProperty.TextFont
  'wrkProject.Font = tvwProject.Font
  'tabProperty.Font = tvwProject.Font
  panMessage.Height = lblMessage.Font.Height + lblMessage.Padding * 2 + 2
  '$bCanShowBalloon = Settings["/ShowBalloon", TRUE]
  'IF NOT $bCanShowBalloon THEN HideBalloon
  bShow = Settings["/ShowMessages", True]
  panMessage.Visible = bShow
  mnuStatusBar.Value = bShow
  
  Me.Menus.Visible = Settings["/ShowMenus", True]
  'btnShowMenu.Visible = Not Me.Menus.Visible
  
  MTheme.InitControl(tvwProject)
  
  'panTabProperty.H = tabProperty.ClientY - 2
  
End

' PUBLIC SUB mnuViewDebug_Click()
' 
'   HideOrShow(FDebugInfo)
' 
' END

Private Sub UpdateProperty()
  
  Action["property"].Visible = (Not Project.ReadOnly) And $bNeedProperty
  Action["property"].Enabled = Not Project.Running
  
End

Public Sub UpdateMenu()
  
  Dim bVisible As Boolean
  Dim bEnabled As Boolean
  Dim sAction As String
  
  bVisible = Not Project.ReadOnly
  bEnabled = Not Project.Running
  
  UpdateTranslate
  
  For Each sAction In ["exec", "install", "compile", "compile-all"]
    Action[sAction].Visible = bVisible
    Action[sAction].Enabled = bEnabled
  Next
  
  UpdateProperty
  
  Action["version-control"].Visible = VersionControl.CanControlProject()
  
  mnuStartup.Visible = bVisible
  mnuRenameFile.Visible = bVisible
  mnuCut.Visible = bVisible
  mnuCopy.Visible = bVisible
  mnuPaste.Visible = bVisible
  mnuDeleteFile.Visible = bVisible
  mnuClean.Visible = bVisible
  mnuNew.Visible = bVisible
  mnuEditFile.Visible = bVisible
  
  mnuClearDebugWindows.Enabled = Project.Running
  
  Action["component-property"].Visible = Project.CreateComponent
  
End

Public Sub OnProjectChange()
  
  UpdateMenu
  btnSearchAdd.Visible = VersionControl.Enabled()
  'UpdateTranslate
  'RefreshInfo
  'IF $bHidden THEN HideAll
  
End

Public Sub OnProjectDebug()
  
  UpdateMenu
  
End

Public Sub tvwProject_MouseDown()
  
  $bCtrl = Mouse.Control
  
End 

' PUBLIC SUB Form_Show()
' 
'   Project.Activate(NULL)  
' 
' END

Public Sub ShowTool(bShow As Boolean)
  
  $bNeedProperty = bShow
  UpdateProperty
  
  ' If bShow <> panProperty.Hidden Then Return
  ' 
  ' If Not bShow Then $bTool = Not panTool.Hidden
  ' 
  ' Action["property"].Value = bShow
  
End

Public Sub ShowDebug(bShow As Boolean)
  
  panDebug.Hidden = Not bShow
  'FDebugInfo.UpdateView
  
End

Public Sub HideDebug(bHide As Boolean)
  
  panDebug.Visible = Not bHide
  
End

' PUBLIC SUB mnuHideAll_Click()
' 
'   HideAll  
' 
' END
' 
' PRIVATE SUB HideAll()
'   
'   IF FOption.Visible THEN RETURN
'   
'   IF $bHidden THEN
'     
'     panProject.Hidden = $aHiddenState["panProject"]
'     panProperty.Hidden = $aHiddenState["panProperty"]
'     panTool.Hidden = $aHiddenState["panTool"]
'     
'   ELSE
'     
'     $aHiddenState.Clear
'     $aHiddenState["panProject"] = panProject.Hidden
'     $aHiddenState["panProperty"] = panProperty.Hidden
'     $aHiddenState["panTool"] = panTool.Hidden
'     
'     panProject.Hidden = TRUE
'     panProperty.Hidden = TRUE
'     panTool.Hidden = TRUE
'     
'   ENDIF
'   
'   $bHidden = NOT $bHidden
'   
' END

Public Sub wrkProject_Activate()
  
  Project.Activate(wrkProject.ActiveWindow)
  
End

Public Sub ActivateCurrentWindow()
  
  Try wrkProject.ActiveWindow.SetFocus
  wrkProject_Activate ' If it already has the focus
  
End

Public Sub Action_Activate(Key As String) As Boolean
  
  Dim hEditor As FEditor
  
  'DEBUG Key
  
  Select Key
      
    Case "project"
      panProject.Hidden = Not Action[Key].Value
    Case "start"
      Project.Run(False)
    Case "pause"
      Design.Pause
    Case "stop"
      Design.Stop
    Case "forward"
      Project.Run(False, 2)
    Case "step"
      Project.Run(False, 1)
    Case "return"
      Project.Run(False, 3)
      
    Case "new-project"
      NewProject
    Case "open-project"
      OpenProject
    Case "save-project"
      Project.Save
    Case "save-project-as"
      Project.SaveAs
    Case "refresh-project"
      Project.Refresh
    Case "project-property"
      FPropertyProject.Run
    Case "component-property"
      FPropertyComponent.Run
    Case "translate"
      Translate
    Case "compile"
      Project.Compile()
    Case "compile-all"
      Project.Compile(True)
    Case "archive"
      Project.MakePackage
    Case "exec"
      Project.MakeExecutable
    Case "install"
      Project.MakeInstall
    Case "shortcut"
      Action.Configure
    Case "version-control"
      VersionControl.ControlProject
      UpdateMenu
      
    Case "find-next"
      FSearch.FindNext
    Case "find-previous"
      FSearch.FindPrevious
      'Case "find-project"
      'cmbFind_Activate
      
    Case "console"
      If Action["console"].Value Then 
        Try FOutput.SetFocus
      Else
        Try Project.ActiveForm.SetFocus
      Endif
      'FOutput.Toggle(Action["console"].Value)
      
      ' Case "break"
      '   'Debug Action["break"].Value
      '   Try hEditor = Project.ActiveForm
      '   If Not Error Then Design.SetBreakpoint(hEditor.Name, hEditor.Editor.Line, Action["break"].Value)
      ' Case "watch"
      '   Try hEditor = Project.ActiveForm
      '   If Not Error Then hEditor.WatchExpression
    Case "until"
      Try hEditor = Project.ActiveForm
      If Not Error Then hEditor.RunUntil
    Case "info"
      FInfo.Run(tvwProject.Key)
      
    Case "menu-file"
      mnuFile.Popup(btnMnuFile.ScreenX, btnMnuFile.ScreenY + btnMnuFile.H)
    Case "menu-project"
      mnuProject.Popup(btnMnuProject.ScreenX, btnMnuProject.ScreenY + btnMnuProject.H)
    Case "menu-debug"
      mnuDebug.Popup(btnMnuDebug.ScreenX, btnMnuDebug.ScreenY + btnMnuDebug.H)
    Case "menu-tool"
      mnuTool.Popup(btnMnuTool.ScreenX, btnMnuTool.ScreenY + btnMnuTool.H)
      
  End Select 
  
End

Private Sub RefreshMenu()
  
  GetCurrent
  
  If $sKey = Project.KEY_CONNECTION Or If Project.IsConnectionPath($sKey) Then
    If tvwProject.HasFocus Then Project.SetCurrentPopup(mnuDatabase)
    mnuDatabase_Show
  Else
    If tvwProject.HasFocus Then Project.SetCurrentPopup(mnuPopup)
    mnuPopup_Show
  Endif
  
End

Public Sub tvwProject_GotFocus()
  
  RefreshMenu
  
End

Public Sub wrkProject_WindowMenu()
  
  'DEBUG
  
End

Public Sub tvwProject_Rename()
  
  Dim sPath As String
  Dim sNew As String
  
  $bInRename = False
  sPath = GetCurrent()
  If Not sPath Then Return
  
  'DEBUG Before;; tvwProject.Item.Text
  If $sDeleteKey Then
    sNew = File.Dir($sDeleteKey) &/ tvwProject.Item.Text
    Project.DeleteDir(sPath, True)
    Project.InsertDirectory(sNew)
    $sDeleteKey = ""
  Else
    Project.RenameFile(sPath, tvwProject.Item.Text)
  Endif
  
End

Public Sub mnuRenameFile_Click()
  
  Rename(tvwProject.Key)
  
End

Private Sub IsDataKey(sKey As String) As Boolean
  
  Dim sPrevious As String
  
  tvwProject.MoveTo(sKey)
  Do
    tvwProject.MoveParent
    If Not tvwProject.Available Then Break
    sPrevious = sKey
    sKey = tvwProject.Item.Key
  Loop 
  
  Return sPrevious = Project.KEY_MISC Or sPrevious = Project.KEY_PROJECT
  
End

Public Sub tvwProject_DragMove()
  
  Dim sKey As String
  
  If Not tvwProject.Find(Drag.X, Drag.Y) Then 
    $sKey = tvwProject.Item.Key
    If Project.IsLocked($sKey) Then Goto STOP_EVENT
    If Drag.Format = MMime.DATA Then 
      If Not IsDataKey($sKey) Then Goto STOP_EVENT
    Else If Drag.Format = MMime.CLASS Then
      If $sKey <> Project.KEY_SOURCE Then
        If Not Project.IsSourcePath($sKey) Then Goto STOP_EVENT
      Endif
    Else If Drag.Format = MMime.URI Then
      If Not IsDataKey($sKey) Then Goto STOP_EVENT
    Else
      Goto STOP_EVENT
    Endif
    With tvwProject[$sKey] 
      If .Children Then 
        If Not timOpen.Enabled Or $sKey <> $sKeyDrag Then 
          $sKeyDrag = $sKey
          timOpen.Enabled = True
        Endif
      Else  
        timOpen.Enabled = False
      Endif
      Drag.Show(tvwProject, .X, .Y, Min(tvwProject.ClientW - .X, tvwProject.Font.TextWidth(.Text) + 8 + .Picture.Width), .H)
      'Drag.Show(tvwProject)
    End With
    Return
  Endif
  
  STOP_EVENT:
  
  Stop Event
  
End

Private Sub AddDataFiles(aFile As String[], bLink As Boolean)
  
  Dim sPath As String
  Dim sDir As String
  Dim sName As String
  
  Inc Application.Busy
  
  sDir = GetCurrentDir($sKey)
  
  For Each sPath In aFile
    
    If Not (sPath Begins "file://") Then Continue
    
    sPath = MMime.UnEscapeURI(Mid$(sPath, 8))
    If IsDir(sPath) Then Continue
    
    sName = Project.GetUniqueName(sDir, File.Name(sPath))
    Project.InsertFile(sName, sDir, sPath, True, bLink)
    
  Next
  
  Project.SelectKey(sDir &/ sName)
  
  Dec Application.Busy
  
Catch
  
  Application.Busy = 0
  ShowError(("Unable to drop file into the project.") & "\n\n" & Error.Text, $sKey)
  
End

Public Sub tvwProject_Drop()
  
  Dim aFile As String[]
  Dim sSrc As String = Drag.Data
  
  'Debug Drag.Format;; Drag.Data
  
  If Drag.Format = MMime.URI Then
    
    aFile = Split(Replace(sSrc, "\r", ""), "\n")
    If Drag.Action = Drag.Copy Or If Drag.Action = Drag.Move Then
      AddDataFiles(aFile, False)
    Else If Drag.Action = Drag.Link Then
      AddDataFiles(aFile, True)
    Endif
    
  Else
    
    If Drag.Action = Drag.Move Then
      Project.MoveFile(sSrc, GetCurrentDir($sKey) &/ File.Name(sSrc), Drag.Format = MMime.CLASS)
    Else If Drag.Action = Drag.Copy Then
      If Drag.Format <> MMime.CLASS Then
        Project.CopyFile(sSrc, GetCurrentDir($sKey) &/ File.Name(sSrc))
      Endif
    Else If Drag.Action = Drag.Link Then
      If Drag.Format <> MMime.CLASS Then
        Project.LinkFile(sSrc, GetCurrentDir($sKey) &/ File.Name(sSrc))
      Endif
    Endif
    
  Endif
  
End

Public Sub tvwProject_MouseDrag()
  
  Dim sKey As String
  
  If Not Mouse.Left Then Return
  
  If tvwProject.Find(Mouse.StartX, Mouse.StartY) Then Return
  sKey = tvwProject.Item.Key
  If tvwProject.Key <> sKey Then Return
  'If Project.IsSourcePath(sKey) Then Return
  If Left(sKey) = "$" Then Return
  
  mnuPopup_Show
  If Not mnuCut.Visible Or If Not mnuCut.Enabled Then Return
  
  Drag.Icon = tvwProject.Item.Picture
  tvwProject[sKey].Selected = True
  Drag(tvwProject, sKey, If(Project.IsSourcePath(sKey), MMime.CLASS, MMime.DATA))
  
End

Public Sub timOpen_Timer()
  
  If Drag.Pending Then 
    Try tvwProject[$sKeyDrag].Expanded = True
  Endif
  timOpen.Enabled = False
  
End

Public Sub mnuNewFile_Click()
  
  FCreateFile.Run(GetCurrent(), GetCurrentDir(), Last.Tag)
  
End

Public Sub mnuAddProjectLink_Click()
  
  Dim sPath As String 
  
  sPath = FOpenProject.Run() 
  If Not sPath Then Return
  
  Project.InsertFile(File.Name(sPath), GetCurrentDir(), sPath &/ ".src", True, True)
  
End

Public Sub tvwProject_Cancel()
  
  Dim sPath As String
  
  $bInRename = False
  sPath = GetCurrent()
  If Not sPath Then Return
  
  If sPath = $sDeleteKey Then Project.DeleteDir(sPath, True)
  $sDeleteKey = ""
  
End

Public Sub tvwProject_Expand()
  
  Dim sKey As String = tvwProject.Item.Key
  
  If Not sKey Then Return
  If Left(sKey) = "$" Then Return
  Project.FillDir(sKey)  
  
End

Public Sub tvwProject_Collapse()
  
  Dim sKey As String = tvwProject.Item.Key
  
  $bHasJustCollapsed = True
  If Not sKey Then Return
  If File.Dir(sKey) = Project.Dir And If Not IsDir(sKey) Then tvwProject[sKey].Expanded = True
  
End

Public Sub mnuClean_Click()
  
  If Message.Warning(("All backup or generated files will be removed from the project directory."), ("Clean"), ("Cancel")) = 2 Then Return 
  
  Project.CleanUp
  
End

Public Sub mnuClearBreakpoints_Click()
  
  Design.ClearBreakpoints
  
End

Public Sub panInfo_Show()
  
  'RefreshInfo  
  
End

Public Sub mnuOpenRecent_Show()
  
  If $bUpdateRecentMenu Then Return
  UpdateRecentMenu
  $bUpdateRecentMenu = True
  
End

Public Sub OnUpdateRecent()
  
  $bUpdateRecentMenu = False
  
End

Public Sub mnuOpenExample_Show()
  
  UpdateExampleMenu(Last)
  
End

' Public Sub tabProperty_Click()
' 
'   With tabProperty
' 
'   Select Case .Index
'   
'     Case 0
'       FFormStack.Hide
'       'FProperty.Move(.X, .Y + .ClientY - 2, .W, .ClientH + 2)
'       FProperty.Show
'     
'     Case 1
'       FProperty.Hide
'       'FFormStack.Move(.X, .Y + .ClientY - 2, .W, .ClientH + 2)
'       FFormStack.Show
'   
'   End Select 
'   
'   End With
'   
' End

Public Sub panProperty_Arrange()
  
  'tabProperty_Click  
  
End

Public Sub tvwProject_Leave()
  
  $bInTreeView = False
  
End

Public Sub tvwProject_Enter()
  
  $bInTreeView = True  
  
End

Public Sub HidePanels()
  
  FSearch.Hide 'Action["find"].Value = False
  Action["console"].Value = False
  Balloon.Hide
  
End

Public Sub Form_KeyPress()
  
  If Key.Code = Key.Escape Then 
    If FSearch.InBrowse Then
      FSearch.Cancel
      Return
    Endif
    HidePanels
    FProperty.HideProperty
    Try Project.ActiveForm.SetFocus
  Else If Key.Code = Key["M"] And If Key.Control And If Key.Alt Then
    If Me.Menus.Visible Then
      mnuHideMenu_Click
    Else
      btnShowMenu_Click
    Endif
  Endif
  
End

Public Sub ActivatePropertyTab()
  
  tabProperty.Index = 0
  
End

Public Sub SetMessage(sMsg As String)
  
  lblMessage.Text = sMsg
  timMessage.Stop
  timMessage.Start
  'Wait
  
End

Public Sub timMessage_Timer()
  
  lblMessage.Text = ""
  timMessage.Stop
  
End

Public Sub mnuBrowse_Click()
  
  Desktop.Open(Project.Dir)
  
End

Public Sub mnuEditWith_Show()
  
  Dim sApp As String
  Dim hMenu As Menu
  
  mnuEditWith.Children.Clear
  For Each sApp In Project.ImageEditors
    hMenu = New Menu(mnuEditWith) As "mnuEditWithProg"
    hMenu.Caption = sApp
  Next
  
End

Public Sub mnuEditWithProg_Click()
  
  Dim sKey As String = tvwProject.Key
  
  If sKey Then
    If CanEdit(sKey) Then
      Project.OpenImage(sKey, Last.Text)
    Endif
  Endif
  
End

Public Sub mnuClearDebugWindows_Click()
  
  FDebugExpr.Clear
  
End

Public Sub btnClose_Click()
  
  panError.Hide
  
End

Public Sub mnuTerminal_Click()
  
  If Desktop.Type = "KDE4" Then
    Exec ["konsole", "--workdir", Project.Dir, "--notransparency"]
  Else
    Desktop.OpenTerminal(Project.Dir)  
  Endif
  
End

Public Sub mnuHideMenu_Click()
  
  If Me.Menus.Visible Then
    Me.Menus.Hide
    sepMenu.Hide
    'btnShowMenu.Show
    Settings["/ShowMenus"] = Me.Menus.Visible
    Message.Optional(Subst(("To show the menu bar again, hit the following keys:<p><b>&1</b>"), mnuHideMenu.Shortcut), ("OK"), "HideMenu", "menu", Settings["/AlwaysDisplayOptionalMessages"])
  Else
    btnShowMenu_Click
  Endif
  
End

Public Sub btnShowMenu_Click()
  
  Me.Menus.Show
  sepMenu.Show
  'btnShowMenu.Hide
  Settings["/ShowMenus"] = Me.Menus.Visible
  
End

Public Sub tvwProject_Drag()
  
End

Public Sub mnuStatusBar_Click()
  
  Settings["/ShowMessages"] = mnuStatusBar.Value
  ReadConfig
  
End

Public Sub mnuNewConnection_Click()
  
  FNewConnection.Run()
  
End

Public Sub mnuInfoConnection_Click()
  
  FNewConnection.Run($sKey)
  
End

Public Sub mnuEditConnection_Click()
  
  tvwProject_Activate
  
End

Public Sub ShowMessage(sMsg As String, sIcon As String, Optional sKey As String)
  
  Dim X, Y As Integer
  
  If NoMessage Then Return
  
  If Not sKey Then sKey = Project.Dir
  
  'Try tvwProject[sKey].Selected = True
  'Wait
  Try tvwProject[sKey].EnsureVisible
  Y = 8
  Try Y = tvwProject[sKey].Y + tvwProject[sKey].H \ 2
  
  X = tvwProject.W \ 2
  
  If FMain.Visible Then
    Balloon(sMsg, tvwProject, Picture["icon:/32/" & sIcon], X, Y)
    ' lblError.Text = Replace$(sMsg, "\n", "<br>")
    ' imgIcon.Picture = Picture["icon:/32/" & sIcon]
    ' panError.Height = lblError.Height
    ' panError.Show
  Else
    sMsg = Replace(sMsg, "\n", "<br>")
    If sIcon = "error" Then
      Message.Error(sMsg)
    Else If sIcon = "warning" Then
      Message.Warning(sMsg)
    Else
      Message(sMsg)
    Endif
  Endif
  
End

Public Sub ShowError(sMsg As String, Optional sKey As String)
  
  ShowMessage(sMsg, "error", sKey)
  
End

Public Sub ShowWarning(sMsg As String, Optional sKey As String)
  
  ShowMessage(sMsg, "warning", sKey)
  
End

Public Sub ShowInfo(sMsg As String, Optional sKey As String)
  
  ShowMessage(sMsg, "info", sKey)
  
End

Public Sub mnuAddVersion_Click()
  
  If Not Project.IsAdded($sKey, True) Then Return
  
  If Message.Question(("Do you really want to add this file to the repository?"), ("Add"), ("Cancel")) = 2 Then Return
  
  VersionControl.AddFile($sKey)
  If Project.IsFormPath($sKey) Then VersionControl.AddFile(File.SetExt($sKey, "class"))
  Project.Refresh
  
End

Public Sub panErrorFrame_Arrange()
  
  btnClose.Move(panErrorFrame.Width - btnClose.Width, 0)
  
End

' Public Sub cmbFind_Change()
' 
'   FSearch.UpdateSearchString(cmbFind.Text)
' 
' End
' 
' Public Sub UpdateSearchString(sText As String)
'   
'   If sText <> cmbFind.Text Then cmbFind.Text = sText
'   
' End

Public Sub mnuFind_Click()
  
  FSearch.WakeUp(False)
  
End

Public Sub mnuReplace_Click()
  
  FSearch.WakeUp(True)
  
End

Public Sub panSearchProject_Arrange()
  
  dwgSearchProject.Move(0, 0, panSearchProject.W, panSearchProject.H)
  dwgSearchProject.Lower
  
End

Public Sub panSearchProject_Enter()
  
  dwgSearchProject.Refresh
  
End

Public Sub panSearchProject_Leave()
  
  dwgSearchProject.Refresh
  
End

Public Sub dwgSearchProject_Draw()
  
  Dim iFlag As Integer
  
  If txtSearchProject.HasFocus Then iFlag += Draw.Focus
  If dwgSearchProject.Hovered Then iFlag += Draw.Hover
  If Not panSearchProject.Enabled Then iFlag += Draw.Disabled
  
  Draw.Style.Box(0, 0, dwgSearchProject.W, dwgSearchProject.H, iFlag)
  
End

Public Sub txtSearchProject_GotFocus()
  
  dwgSearchProject.Refresh
  
End

Public Sub txtSearchProject_LostFocus()
  
  dwgSearchProject.Refresh
  
End

Public Sub btnClearSearchProject_Click()
  
  txtSearchProject.Clear
  Project.ProjectFilter = ""
  timSearchProject_Timer
  
End

Public Sub txtSearchProject_Change()
  
  Project.ProjectFilter = Trim(txtSearchProject.Text)
  timSearchProject.Start
  
End

Public Sub timSearchProject_Timer()
  
  Project.Refresh
  timSearchProject.Stop
  
End

' Public Sub tlbMain_Configure()
'   
'   panSepToolbar.H = tlbMain.H
'   btnShowMenu.H = tlbMain.H
'   
' End

Public Sub txtSearch_Click()
  
  'Project.StoreCombo(cmbFind)
  FSearch.FindProject(txtSearch.Text)
  
End

Public Sub txtSearch_Activate()
  
  txtSearch_Click
  
End

Public Sub btnSearchAdd_Click()
  
  txtSearchProject.Clear
  Project.ProjectFilter = Project.FILTER_ADDED
  timSearchProject_Timer
  
End

' Public Sub tabProperty_Click()
' 
'   If tabProperty.Index = 1 Then FFormStack.SetFocus
' 
' End
