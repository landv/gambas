' Gambas class file

Private $hProjectItem As ProjectBox
Private $sLast As String
Private $bShowButton As Boolean

Public Sub _new()
  
  $hProjectItem = New ProjectBox(panProjectItem)
  $hProjectItem.Highlight = False
  panProject.Reparent(dchProject.FileView.Parent)
  panProject.Hide
  
End

Public Sub Form_Open()
  
  $sLast = ""
  dchProject.Bookmarks = [["Name": ("Installed softwares"), "Path": Desktop.DataDir &/ "gambas3/src", "Icon": "icon:/small/download"]]
  dchProject.ShowFile = True
  dchProject_Change
  'tabProject_Click

End


Public Sub GetSettings() As Variant[]
  
  Return [dchProject.Settings, splProject.Settings]
  
End

Public Sub SetSettings(Value As Variant[])
  
  Try dchProject.Settings = Value[0]
  Try splProject.Settings = Value[1]
  
End

Public Sub dchProject_Icon(Path As String)

  If Exist(Path &/ ".project") Then 
    dchProject.Icon = Project.GetIcon(Path)    
  Endif  

End

Private Sub GetProjectPath(sPath As String) As String
  
  Dim hFile As File
  Dim sHeader As String
  
  Do
    
    If Exist(sPath &/ ".project") Then 
      
      hFile = Open sPath &/ ".project"
      sHeader = Read #hFile, -256
      hFile.Close
      
      If sHeader Begins Project.PROJECT_MAGIC Or If sHeader Begins Project.PROJECT_MAGIC_2 Then
        Return sPath
      Endif
      
    Endif
    
    sPath = File.Dir(sPath)
    If sPath = "/" Then Break
  Loop
  
Catch
  
End

Private Sub ShowButton()

  If Not $bShowButton Or If Not $sLast Then
    panOpen.Hide
  Else
    panOpen.Show
  Endif

End

Public Sub dchProject_Change()

  Dim sPath As String
  Dim hProjectTree As CProjectTree
  
  sPath = GetProjectPath(dchProject.Value)
  If sPath = $sLast Then Return
  
  If Not sPath Then
    
    $sLast = ""
    panProject.Hide
    dchProject.FileView.Show

  Else

    $sLast = sPath
    
    $hProjectItem.Path = sPath
    hProjectTree = New CProjectTree(tvwProject, sPath)
    
    panProject.Show
    dchProject.FileView.Hide
    
  Endif
  
  ShowButton
  
End

Private Sub GetParent() As ProjectChooser

  Return Me.Parent

End

Public Sub dchProject_Activate()

  GetParent()._RaiseActivate

End

Public Sub GetPath() As String
  
  Return GetProjectPath(dchProject.Value)
  
End

Public Sub SetPath(sPath As String)
  
  dchProject.Value = sPath
  
End

Public Sub GetBorder() As Boolean
  
  Return dchProject.Border
  
End

Public Sub SetBorder(Value As Boolean)

  dchProject.Border = Value

End

Public Sub GetShowButton() As Boolean
  
  Return $bShowButton
  
End

Public Sub SetShowButton(Value As Boolean)
  
  $bShowButton = Value
  ShowButton
  
End

Public Sub GetShowAnother() As Boolean
  
  Return btnOpenAnother.Visible
  
End

Public Sub SetShowAnother(Value As Boolean)

  btnOpenAnother.Visible = Value  
  
End

Public Sub btnOpen_Click()

  GetParent()._RaiseOpen($sLast, False)

End

Public Sub btnOpenAnother_Click()

  GetParent()._RaiseOpen($sLast, True)
  

End

Public Sub btnCancel_Click()

  GetParent()._RaiseCancel()

End
