' Gambas class file

Export

'Event LineChange

Private $iStartLine As Integer
Private $iStartColumn As Integer
Private $iEndLine As Integer
Private $iEndColumn As Integer

Private $hLabel As TextLabel
Private $hLabelTimer As Timer
Private $hObserver As Observer
Private $iLastLine As Integer = -1

Private $bLabelBottom As Boolean

Static Private $sFindPrefix As String
Static Private $aFind As Integer[]

Public Sub _new()
  
  $hObserver = New Observer(Me) As "Editor"
  
End

Public Sub ReadConfig(Optional bPreview As Boolean)

  Project.SetEditorFont(Me)

  MTheme.InitEditor(Me)

  Me.ShowPosition = True
  Me.ShowLimit = Settings["/Editor/ProcedureLimit", True]
  Me.ShowModified = Settings["/Editor/ShowChange", True]
  Me.ShowLineNumber = Settings["/Editor/ShowLineNumbers", False]
  Me.ShowCurrent = Settings["/Editor/ShowCurrent", True]
  Me.ShowExpand = Settings["/Editor/ShowExpand", True]
  Me.ShowSpaces = Settings["/Editor/ShowSpaces", True]
  Me.ShowIndent = Settings["/Editor/ShowIndent", True]
  If bPreview Then
    Me.ShowPreview = Settings["/Editor/ShowPreview", False]
  Else
    Me.ShowPreview = False
  Endif
  'Me.Flags[Editor.NoFolding] = Not Settings["/Editor/ShowFolding", True]
  Me.ShowBraces = True
  Me.CloseBraces = Settings["/Editor/CloseBraces", 1]
  Me.CloseStrings = Settings["/Editor/CloseStrings", 1]
  
  'Me.KeywordsUseUpperCase = Settings["/Editor/KeywordsUseUpperCase"]
  
End

Public Sub GetPosition() As Integer
  
  Dim iLine, iColumn, iInd, iPos As Integer
  
  iLine = Me.Line
  iColumn = Me.Column
  If Me.Selected Then 
    If Me.SelectionLine = Me.Line Then
      iColumn = Min(iColumn, Me.SelectionColumn)
    Else If Me.SelectionLine < Me.Line Then
      iLine = Me.SelectionLine
      iColumn = Me.SelectionColumn
    Endif
  Endif
      
  For iInd = 0 To iLine - 1
    iPos += Len(Me[iInd].Text) + 1
  Next
  iPos += Len(String.Left(Me[iLine].Text, iColumn))
  
  Return iPos
  
End

Fast Private Sub CalcPos(iPos As Integer) As Integer[]
  
  Dim iLine As Integer
  Dim iLen As Integer
  
  While iLine < Me.Count
  
    iLen = Len(Me[iLine].Text)
    If iPos <= iLen Then Break
    iPos -= iLen + 1
    Inc iLine
    
  Wend
  
  Return [iLine, iPos]

End


Public Sub SetPosition(hFindResult As CFindResult, bJustCheck As Boolean) As Boolean
  
  Dim iLine, iColumn As Integer
  Dim sLine As String
  Dim iPos As Integer
  Dim aPos As Integer[]
  
  ' If hFindResult.Line Or If hFindResult.Column Then
  ' 
  '   iLine = hFindResult.Line
  '   iColumn = hFindResult.Column
  '   sLine = Me.Lines[iLine].Text
  ' 
  ' Else
  
  aPos = CalcPos(hFindResult.Pos)
  iLine = aPos[0]
  iPos = aPos[1]
  
  sLine = Me[iLine].Text
  iColumn = String.Len(Left$(sLine, iPos))
    
  If Me.Highlight Then
    If InStr(String.Mid$(Me[iLine].Purge(Not FSearch.IgnoreComments, Not FSearch.IgnoreStrings, Chr$(1)), iColumn + 1, hFindResult.Length), Chr$(1)) Then Return True
  Endif
  
  hFindResult.Line = iLine
  hFindResult.Column = iColumn
  
  If bJustCheck Then 
    'If iColumn >= 40 And If Me[iLine].Length > 80 Then
    '  sLine = "â€¦" & Html(String.Mid$(sLine, iColumn - 31, 32)) & "<u>" & Html(String.Mid$(sLine, iColumn + 1, hFindResult.Length)) & "</u>" & Html(String.Mid$(sLine, iColumn + hFindResult.Length + 1, 160))
    'Else
    '  sLine = Html(String.Left(sLine, iColumn)) & "<u>" & Html(String.Mid$(sLine, iColumn + 1, hFindResult.Length)) & "</u>" & Html(String.Mid$(sLine, iColumn + hFindResult.Length + 1, 160))
    'Endif
    'sLine = Replace(sLine, " ", "&nbsp;")
    hFindResult.Text = sLine

  Else
    ' TODO Me[iLine].Expanded = True
    Me.GotoCenter(iColumn, iLine)
    Me.Goto(iColumn + hFindResult.Length, iLine, True)
  Endif
  
End

Public Sub Replace(sReplace As String) As Boolean
  
  Dim iLine, iColumn As Integer
  
  If Me.ReadOnly Then Return True
  If Not Me.Selected Then Return True
  
  iLine = Me.SelectionLine
  iColumn = Me.SelectionColumn
  Me.Begin
  Me.Insert(sReplace)
  Me.Goto(iColumn, iLine)
  Me.Goto(iColumn + String.Len(sReplace), iLine, True)
  Me.End  
  
End

Public Sub GetSelection() As String
  
  If Me.Selected And If Me.SelectionLine = Me.Line Then
    Return Me.SelectedText
  Endif
  
End

Public Sub StoreSelection(Optional bNoFullLines As Boolean)

  With Me
    
    $iStartLine = .SelectionLine
    $iStartColumn = .SelectionColumn
    $iEndLine = .Line
    $iEndColumn = .Column
    
    If Not bNoFullLines Then 
      If $iEndLine >= $iStartLine Then
        If $iEndColumn > 0 Or Not .Selected Then 
          Inc $iEndLine
          $iEndColumn = 0
          $iStartColumn = 0
          RecallSelection
        Endif
      Else
        If $iStartColumn > 0 Or Not .Selected Then 
          Inc $iStartLine
          $iStartColumn = 0
          $iEndColumn = 0
          RecallSelection
        Endif
      Endif
    Endif
    
  End With
  

End

Public Sub RecallSelection()

  Me.Goto($iStartColumn, $iStartLine)
  Me.Goto($iEndColumn, $iEndLine, True)

End

Public Sub LabelTimer_Timer()

  If $hLabel.Visible Then
    HideMessageLabel
    $hLabelTimer.Stop
  Endif

End

Public Sub Label_MouseDown()

  LabelTimer_Timer

End

Private Sub MoveMessageLabel()

  Dim X As Integer
  Dim Y As Integer
  
  If Not $hLabel Then Return
  If Not $hLabel.Visible Then Return
  
  If $bLabelBottom Then
    X = 0
    Y = Container(Me.Proxy).ClientH - $hLabel.H
  Else
    X = Me.ToPosX()
    Y = Me.ToPosY() + Me.LineHeight
  Endif

  X = Me.ScreenX + X - $hLabel.Parent.ScreenX
  Y = Me.ScreenY + Y - $hLabel.Parent.ScreenY
  
  If (Y + $hLabel.H) > $hLabel.Parent.ClientH Then Y -= $hLabel.H + Me.LineHeight
  $hLabel.Move(X, Y)
  
End


Public Sub ShowMessageLabel(sStr As String, iStyle As Integer, Optional bBottom As Boolean)

  Dim hParent As Container

  If Not $hLabel Then
    
    hParent = Me.Parent
    If hParent Is HSplit Or If hParent Is VSplit Then hParent = hParent.Parent
    
    $hLabel = New TextLabel(hParent) As "Label"
    $hLabel.Ignore = True
    $hLabel.AutoResize = True
    $hLabel.Border = If(bBottom, Border.None, Border.Plain)
    $hLabel.Padding = 2
    $hLabel.Wrap = False
    $hLabelTimer = New Timer As "LabelTimer"
    $hLabelTimer.Delay = 10000
    
  Endif
  
  $hLabel.Foreground = Me.Styles[Highlight.Normal].Color
  'If IsMissing(iStyle) Then iStyle = Highlight.Highlight
  $hLabel.Background = Color.Merge(Me.Styles[iStyle].Color, Me.Styles[Highlight.Background].Color, 0.3)
  $hLabel.Text = "&nbsp;&nbsp;" & Replace(Replace(sStr, "<p>", " "), "<br>", " ") & "&nbsp;&nbsp;"

  $bLabelBottom = bBottom
  
  $hLabel.Show
  $hLabel.Raise
  MoveMessageLabel
  
  $hLabelTimer.Restart
  Me.SetFocus

End

Public Sub Editor_GotFocus()
  
  $iLastLine = -1
  $aFind = Null
  $sFindPrefix = ""
  
End

Public Sub Editor_Cursor()
  
  HideMessageLabel
  
  If Me.Line <> $iLastLine Then
    $iLastLine = Me.Line
    '$aFind = Null
    '$sFindPrefix = ""
    'Raise LineChange
  Endif
  
End


Public Sub Editor_Scroll()
  
  MoveMessageLabel
  'Try $hLabel.Hide
  
End

Public Sub HideMessageLabel()
  
  If Not $hLabel Or If Not $hLabel.Visible Then Return
  $hLabel.Hide
  
End

Public Sub IsSameFindStrings(sPrefix As String) As String
  
  Return sPrefix = $sFindPrefix
  
End


Public Sub FindStrings(sPrefix As String) As Integer[]
  
  Dim iLine As Integer
  
  If sPrefix = $sFindPrefix Then Return $aFind
  
  $sFindPrefix = sPrefix
  $aFind = New Integer[]
  
  iLine = -1
  Do
  
    iLine = Me.FindNextString(sPrefix, iLine + 1)
    If iLine < 0 Then Break 
    'If iLine = Me.Line Then Continue
    
    $aFind.Add(iLine)
  
  Loop 
  
  Return $aFind
  
End

Public Sub Editor_Arrange()
  
  MoveMessageLabel
  
End

Static Public Sub GetModeFromPath(sPath As String) As String

  Dim sMode As String
  Dim sExt As String
  
  sExt = File.Ext(sPath)
  
  Select Case sExt
    Case "css"
      sMode = "css"
    Case "webpage"
      sMode = "webpage"
    Case "js"
      sMode = "javascript"
    Case "c"
      sMode = "c"
    Case "cpp", "h"
      sMode = "c++"
    Case "gbs"
      sMode = "gambas"
    Case "html", "htm", "xml", "kml"
      sMode = "html"
    Case "module", "class"
      If Project.IsSourcePath(sPath) Then sMode = "gambas"
      If Project.IsHiddenPath(sPath) Then sMode = "gambas"
  End Select
  
  Return sMode
  
End
