' Gambas class file

Public Index As Integer
Public Foreground As Integer
Public Background As Integer
Public Bold As Boolean
Public Italic As Boolean
Public Underline As Boolean

Static Public Sub _get((Style) As String) As CStyle
  
  Dim hStyle As New CStyle(Style)
  Return hStyle
  
End


Public Sub _new(Optional (Style) As String)
  
  Reset
  Foreground = Color.Black
  If Style Then FromString(Style)
  
End


Public Sub Reset()

  Foreground = Color.Default
  Background = Color.Default
  Bold = False
  Italic = False
  Underline = False
  
End


Public Sub FromString(sStyle As String)
  
  Dim sStr As String
  Dim iColor As Integer
  
  Reset
  
  For Each sStr In Split(sStyle)
    If Left(sStr) = "#" Then 
      iColor = Val("&H" & Mid$(sStr, 2) & "&") 
      If Foreground = Color.Default Then 
        Foreground = iColor
      Else 
        Background = iColor
      Endif
    Else If sStr = "Bold" Then 
      Bold = True
    Else If sStr = "Italic" Then 
      Italic = True
    Else If sStr = "Underline" Then
      Underline = True
    Endif
  Next
  
End

Public Sub ToString() As String

  Dim sStr As String
  
  sStr = "#" & Hex$(Foreground, 6)
  If Background <> Color.Default Then sStr &= ",#" & Hex$(Background, 6)
  If Bold Then sStr &= ",Bold"
  If Italic Then sStr &= ",Italic"
  If Underline Then sStr &= ",Underline"
  
  Return sStr
  
End

