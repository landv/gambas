' Gambas class file

Property Read {Error} As Boolean

Static Private $aLang As String[] = ["en", "fr", "de", "es", "it", "nl", "pl", "pt", "pt_BR", "mk", "sq", "ca", "hu", "tr", "ar", "fa", "vi", "ko", "ja", "ru", "zh", "zh_TW", "eo"]

Private $bError As Boolean
Private $bPrint As Boolean
Private mnuLang As Menu
Private $bFirstTime As Boolean

Public Sub _new()
  
  Dim sLang As String
  Dim hMenu As Menu
  Dim aLangName As New String[]
  Dim aLang As String[]
  
  Me.Utility = Settings["/UseUtilityWindows", 1]
  
  mnuLang = New Menu(Me) As "mnuLang"
  
  hMenu = New Menu(mnuLang) As "mnuSelectLanguage"
  hMenu.Text = ("Default language")
  
  For Each sLang In $aLang
    aLangName.Add(Language.ToName(sLang) & ":" & sLang)
  Next
  aLangName.Sort(gb.Language)
  
  For Each sLang In aLangName
    aLang = Split(sLang, ":")
    hMenu = New Menu(mnuLang) As "mnuSelectLanguage"
    hMenu.Text = aLang[0]
    hMenu.Tag = aLang[1]
  Next
  
  btnHome.Menu = "mnuLang"
  
  movWaiting.Resize(64, 64)
  
  '$hHistory = New CHistory As "History"
  
End

Public Sub GetBrowser() As WebView
  
  Return webHelp
  
End


Public Sub ReadConfig()
  
  Project.SetSmallFont(panToolbar)
  
End


Public Sub Form_Open()
  
  Dim sLang As String
  
  Settings.Read(Me)
  Settings.Read(splHelp)
  btnShowPanel.Value = Settings["/FHelpBrowser/ShowPanel", False]
  btnShowPanel_Click
  
  ReadConfig
  
  sLang = MHelp.GetLanguage(True)
  If sLang Then
    btnHome.Text = Language.ToName(sLang)
  Else
    btnHome.Text = ("Default language")
  Endif
  
  'If Not webHelp.Url Then 
  '  webHelp.Url = MHelp.GetURL() & "?help&" & MHelp.GetLanguage() & "&v3"
  '  webHelp.TextZoom = 0.8
  'Endif
  
  MHelp.LoadProxyConfig
  
  If Not $bFirstTime Then
    $bFirstTime = True
    OnProjectChange
  Endif
  
End

Public Sub Form_Close()
  
  Settings.Write(Me)
  Settings.Write(splHelp)
  Settings["/FHelpBrowser/ShowPanel"] = btnShowPanel.Value
  
End

Private Function GetHomeURL() As String

  'Debug MHelp.GetURL() & "?help&" & MHelp.GetLanguage() & "&v3"
  Return MHelp.GetURL() & "?l=" & MHelp.GetLanguage()

End

Public Sub GotoURL(sUrl As String)
  
  webHelp.Url = sUrl
  '$hHistory.Add(sUrl)
  
End

Public Sub GotoHome()
  
  GotoURL(GetHomeURL())
  webHelp.TextZoom = 0.8
  
End


Public Sub btnHome_Click()
  
  GotoHome()
  
End

Public Sub btnBack_Click()
  
  'GotoURL($hHistory.Backward())
  webHelp.Back
  
End

Public Sub btnForward_Click()
  
  'GotoURL($hHistory.Forward())
  webHelp.Forward
  
End

Public Sub btnStop_Click()
  
  webHelp.Stop
  
End

Public Sub btnReload_Click()
  
  webHelp.Reload
  
End

Public Sub btnZoomIn_Click()
  
  webHelp.TextZoom = Round(webHelp.TextZoom * 1.25, -2)
  
End

Public Sub btnZoomOut_Click()
  
  webHelp.TextZoom = Round(webHelp.TextZoom / 1.25, -2)
  
End

Public Sub btnZoomNormal_Click()
  
  webHelp.TextZoom = 0.8
  
End

Public Sub Form_Resize()
  
  Dim bVisible As Boolean = Me.Height > Desktop.Scale * 30 And Me.Width > Desktop.Scale * 30

  panToolbar.Visible = bVisible
  sepToolbar.Visible = bVisible
  
End

Public Sub btnWebSite_Click()
  
  GotoURL("http://gambas.sourceforge.net")
  webHelp.TextZoom = 0.8
  
End

Private Function Error_Read() As Boolean
  
  Return $bError
  
End

' Public Sub btnModify_Click()
'   
'   Dim sUrl As String
'   
'   sUrl = webHelp.Url
'   If Not (sUrl Begins Project.WIKI_URL) Then Return
'   
'   'sUrl = Replace(sUrl, "http://gambasdoc.org/help", "http://gambasdoc.org/edit")
'   
'   Project.OpenWebPage(sUrl)
'   
' End

Public Sub btnShowPanel_Click()
  
  tvwClasses.Visible = btnShowPanel.Value
  If btnShowPanel.Value Then FillTree
  'If splHelp.Layout[0] = 0 Then splHelp.Layout = [1, 2]

End

''Fill the TreeView with Project components and classes

Private Sub FillTree()

  Dim hClass As CClassInfo
  Dim sComponent As String
  Dim sClass As String
  
  tvwClasses.Clear
  
  tvwClasses.Add("$", Project.Name, Project.GetIcon(Project.Dir, 16))
  For Each sClass In Project.GetClasses()
    
    tvwClasses.Add("$" &/ sClass, sClass,, "$")
    
  Next
  tvwClasses["$"].Expanded = True
  
  For Each hClass In CComponent.Classes
    
    If hClass.Name = "." Then Continue
    If InStr(hClass.Name, "*") Then Continue
    
    If hClass.Component = Null Then 
      sComponent = "gb"
    Else 
      sComponent = hClass.Component
    Endif
    sClass = hClass.Name
    If sClass = "Hunk" Then Stop
    
    If Not tvwClasses.Exist(sComponent) Then 
      tvwClasses.Add(sComponent, sComponent, Picture["img/16/component.png"])
    Endif
    
    Try tvwClasses.Add(sComponent &/ sClass, sClass,, sComponent)
    
  Next
  
End

Public Sub tvwClasses_Select()
  
  Dim aKey As String[]
  Dim sKey As String

  sKey = tvwClasses.Current.Key
  If Not sKey Or If sKey = "$" Then Return
  
  aKey = Split(sKey, "/")
  If aKey.Count = 2 Then
    MHelp.InitWebViewWith(webHelp, MHelp.TYPE_CLASS, aKey[0] &/ aKey[1])
  Else If aKey.Count = 1 Then
    MHelp.InitWebViewWith(webHelp, MHelp.TYPE_COMPONENT, aKey[0])
  Endif
  
End

Public Sub btnPrint_Click()
  
  Printer1.OutputFile = "~/print.pdf"
  $bPrint = False
  If Printer1.Configure() Then Return
  webHelp.Current.Print(Printer1)
  
End

Public Sub Form_KeyPress()
  
  If Key.Code = Key.Escape Then Me.Close
  
End

Private Sub SetLang(Optional sLang As String)
  
  Dim sUrl As String
  
  MHelp.SetLanguage(sLang)
  sLang = MHelp.GetLanguage()
  
  sUrl = webHelp.Url
  If sUrl Begins Project.WIKI_URL Then
    sUrl = URL.SetQuery(sUrl, "l", sLang)
    GotoURL(sUrl)
  Else If sUrl Begins "file:///" &/ MHelp.GetOfflineDir() Then
    GotoURL(Project.WIKI_ROOT &/ Wiki.Path & "?l=" & sLang)
  Endif
  
End

Public Sub mnuSelectLanguage_Click()
  
  SetLang(Last.Tag)
  btnHome.Text = Last.Text
  
End

Public Sub OnProjectChange()
  
  MHelp.Clear
  If Me.Closed Then Return
  
  If btnShowPanel.Value Then FillTree
  
  If webHelp.Url <> GetHomeURL() Then
    btnHome_Click
  Endif
  
End
