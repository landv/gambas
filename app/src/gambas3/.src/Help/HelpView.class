' Gambas class file

Export

Public Const _Properties As String = "*,NoHeader"
Public Const _DrawWith As String = "WebView"

Inherits WebView

Property NoHeader As Boolean

Private $hObs As Observer
Private $hWatcher As Watcher
Private $hSpinner As Spinner
Private $sLastUrl As String
Private $bNoHeader As Boolean
Private $sNextUrl As String

Public Sub _new()

  $hObs = New Observer(Me) As "WebView"
  $hWatcher = New Watcher(Me) As "WebView"
  
  $hSpinner = New Spinner(Me.Window)
  $hSpinner.Hide
  $hSpinner.Ignore = True
  $hSpinner.ResizeScaled(8, 8)
  
End

Private Sub ShowWaiting(bShow As Boolean)

  If $hSpinner.Visible = bShow Then Return

  If bShow Then
    $hSpinner.Show
    $hSpinner.Raise
    $hSpinner.Start
  Else
    $hSpinner.Stop
    $hSpinner.Hide
  Endif


End

' Public Sub WebView_Error()
'   
'   Dim sLastUrl As String
'   
'   ShowWaiting(False)
'   
'   If Not $sLastUrl Then 
'     sLastUrl = Me.Url
'   Else
'     sLastUrl = $sLastUrl
'     $sLastUrl = ""
'   Endif
'   
'   If sLastUrl Begins "/" Then sLastUrl = Project.WIKI_ROOT &/ sLastUrl
'   
'   If sLastUrl Begins "gambas://" Then 
'     
'     Me.Stop
'     MHelp.ManageSpecialLink(Me, sLastUrl)
'     Return
'     
'   Else If sLastUrl Begins "wiki://" Then
'     
'     MHelp.MakeOffline(Me, sLastUrl, $bNoHeader)
'     Return
'     
'   Endif
'   
' End

Public Sub WebView_Move()
  
  Dim hWin As Window
  
  If Not $hSpinner Then Return
  
  With Me
    hWin = $hSpinner.Window
    $hSpinner.Move(.ScreenX - hWin.ScreenX - hWin.ClientX + (.W - $hSpinner.W) \ 2, .ScreenY - hWin.ScreenY - hWin.ClientY + (.H - $hSpinner.H) \ 2)
  End With
  
End

Public Sub WebView_Resize()

  WebView_Move
  
End

Public Sub WebView_Load()
  
  ShowWaiting(False)
  
End

Public Sub WebView_Link(Url As String)
  
  $sLastUrl = Url
  
End

Public Sub WebView_NewWindow((Modal) As Boolean)

  Project.OpenWebPage($sLastUrl)

End

Public Sub TimerUrl_Timer()
  
  'Debug $sNextUrl
  
  If $sNextUrl Begins "gambas://" Then 
    
    MHelp.ManageSpecialLink(Me, $sNextUrl)
    
  Else If $sNextUrl Begins "wiki://" Then
    
    MHelp.MakeOffline(Me, $sNextUrl, $bNoHeader)
    
  Endif
  
  $sNextUrl = ""
  
End

Public Sub WebView_Progress()

  Dim sLastUrl As String
  Dim hTimer As Timer
  
  'Debug Me.Progress;; Me.Url
  
  ShowWaiting(Me.Progress < 1)

  If Me.Progress = 0 Then

    sLastUrl = $sLastUrl
    If $sLastUrl Then
      sLastUrl = $sLastUrl
      $sLastUrl = ""
    Else
      sLastUrl = Me.Url
    Endif
    
    If sLastUrl Begins "/" Then sLastUrl = Project.WIKI_ROOT &/ sLastUrl
    
    If sLastUrl Begins "gambas://" Or If sLastUrl Begins "wiki://" Then 
      
      $sNextUrl = sLastUrl
      'Me.Stop
      hTimer = New Timer As "TimerUrl"
      hTimer.Trigger
     
    Endif
  
  Endif

End

Private Function NoHeader_Read() As Boolean

  Return $bNoHeader

End

Private Sub NoHeader_Write(Value As Boolean)

  $bNoHeader = Value

End

Public Sub Stop()
  
  Super.Stop
  ShowWaiting(False)
  
End
