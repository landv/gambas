' Gambas class file

Static Private $sPath As String
Static Private $bAnother As Boolean

Static Public InAnotherWindow As Boolean

Private $iSort As Integer
Private $bControl As Boolean
Private $hRecent As CProjectList
Private $hExample As CProjectList

Public Sub Run(Optional bAnother As Boolean) As String

  $bAnother = bAnother
  $sPath = ""
  
  Inc Application.Busy
  If Me.ShowModal() Then 
    'Debug
    Return $sPath
  Endif
  'Debug

End

Public Sub _new()
  
  $hRecent = New CProjectList(lstRecent, txtFilter, CProjectList.TYPE_RECENT, Arrange.Row) As "ProjectItem"
  $hExample = New CProjectList(lstExample, txtFilterExample, CProjectList.TYPE_EXAMPLE, Arrange.Row) As "ProjectItem"
  
  tabProject[2].Visible = False
  
End


Public Sub btnOpen_Click()

  If tabProject.Text = ("Open") Then
     $sPath = pchProject.Path
  Else If tabProject.Text = ("Recent") Then
    If Not $sPath Then $sPath = $hRecent.GetFirst()
  Else If tabProject.Text = ("Examples") Then
    If Not $sPath Then $sPath = $hExample.GetFirst()
  Endif
  
  If Not $sPath Then Return
  
  Me.Close(True)

End

Public Sub Form_Open()

  Dim hCtrl As RadioButton

  '$sLast = ""
  Settings.Read(Me)
  Settings.Read(pchProject)
  ' Settings.Read(dchProject)
  ' Settings.Read(splProject)
  ' dchProject.ShowFile = True
  ' 'dchProject.Value = Settings["/FOpenProject/Path"]
  ' dchProject_Change

  InAnotherWindow = False
  $hRecent.AllowOpenExtern = $bAnother
  $hExample.AllowOpenExtern = $bAnother
  
  $iSort = Settings["/FOpenProject/SortRecent", CRecentProject.SORT_BY_DATE]
  
  For Each hCtrl In [radSortByDate, radSortByName, radSortByPath]
    If hCtrl.Tag = $iSort Then hCtrl.Value = True
  Next
  
  pchProject.ShowAnother = $bAnother
  
  If $bAnother Then tabProject.Index = 1
  tabProject_Click
  
  Dec Application.Busy

End

Public Sub Form_Close()

  Settings["/FOpenProject/SortRecent"] = $iSort
  Settings.Write(Me)  
  ' Settings.Write(splProject)
  ' Settings.Write(dchProject)
  Settings.Write(pchProject)
  
End

Public Sub tabProject_Click()

  If tabProject.Text = ("Recent") Then 
    $hRecent.Fill
    $hRecent.ApplyFilter(txtFilter.Text)
    txtFilter.SetFocus
  Else If tabProject.Text = ("Examples") Then 
    $hExample.Fill
    $hExample.ApplyFilter(txtFilterExample.Text)
    txtFilterExample.SetFocus
  Endif  

End

Public Sub OnUpdateRecent()
  
  $hRecent.Clear
  If tabProject.Text = ("Recent") Then $hRecent.Fill
  
End

Public Sub btnOpenAnother_Click()

  InAnotherWindow = True
  btnOpen_Click

End

Public Sub radSort_Click()

  $iSort = Last.Tag
  
  If tabProject.Text = ("Recent") Then
    $hRecent.SetSort(Last.Tag)
    $hRecent.ApplyFilter(txtFilter.Text)
    txtFilter.SetFocus
  Endif

End

Public Sub ProjectItem_Click(sPath As String, bAnother As Boolean)
  
  InAnotherWindow = $bControl Or bAnother
  $sPath = sPath
  btnOpen_Click
  
End

Public Sub pchProject_Activate()

  btnOpen_Click

End

Public Sub txtFilter_KeyPress()

  If Key.Control Then
    If Key.Code = Key.Enter Or If Key.Code = Key.Return Then
      btnOpen_Click
    Endif
  Else If Key.Normal Then
    If Key.Code = Key.Esc Then
      If txtFilter.Length Then
        txtFilter.Clear
      Else
        Me.Close
      Endif
    Endif
  Endif

End

Public Sub txtFilterExample_KeyPress()

  If Key.Control Then
    If Key.Code = Key.Enter Or If Key.Code = Key.Return Then
      btnOpen_Click
    Endif
  Endif

End

Public Sub pchProject_Cancel()

  Me.Close

End

Public Sub pchProject_Open((Path) As String, Extern As Boolean)

  InAnotherWindow = Extern
  btnOpen_Click

End
