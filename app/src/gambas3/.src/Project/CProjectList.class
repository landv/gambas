' Gambas class file

Public Enum TYPE_RECENT, TYPE_EXAMPLE, TYPE_SOFTWARE, TYPE_TEMPLATE

Event Click(Path As String, Another As Boolean)
Event DblClick(Path As String)
Event Refresh
Event Fill

Property AllowOpenExtern As Boolean
Property Current As ProjectBox

Private $hList As ScrollView
Private $hFilter As ButtonBox
Private $iSort As Integer
Private $iType As Integer
Private $hObserver As Observer
Private $iArrange As Integer
Private $hObsList As Observer
Private $dRecent As Date
Private $sLastURL As String
Private $hHelp As WebView
Private $hCurrent As ProjectBox
Private $iCount As Integer
Private $bNoFilter As Boolean

Public Sub _new(hList As ScrollView, hFilter As ButtonBox, iType As Integer, Optional iArrange As Integer = Arrange.Row)
  
  $hList = hList
  $hFilter = hFilter
  $iType = iType
  $iArrange = iArrange
  
  $bNoFilter = Not $hFilter.Enabled
  
  $hObserver = New Observer($hFilter) As "Filter"
  $hObsList = New Observer(hList) As "ScrollView"
  
End

Public Sub SetSort(iSort As Integer)
  
  $iSort = iSort
  Fill(True)
  
End

Public Sub SetArrangement(iArrange As Integer)
  
  If $iArrange = iArrange Then Return
  
  $iArrange = iArrange
  Clear
  Fill
  
End

Private Sub GetExamples() As String[]

  Dim sFile As String
  Dim aList As New String[]
  Dim sDir As String
  Dim hStat As Stat

  sDir = Desktop.DataDir &/ "gambas3/src/example"
  If Not IsDir(sDir) Then Return aList

  For Each sFile In Dir(sDir)
    If Left(sFile) = "." Then Continue
    hStat = Stat(sDir &/ sFile)
    If hStat.Link Then
      aList.Add(Project.GetAbsoluteLink(sDir &/ sFile, hStat.Link))
    Else
      aList.Add(sDir &/ sFile)
    Endif
  Next

  aList.Sort(gb.Natural)

  Return aList

End


Private Sub FillWithExample()

  Dim sExample As String
  ' Dim sDir As String
  ' Dim sLastDir As String
  ' Dim hDrawingArea As DrawingArea
  Dim hProjectItem As ProjectBox
  ' Dim sGroup As String
  Dim aExamples As String[]
  
  Inc Application.Busy
  $hList.Arrangement = Arrange.None
  
  aExamples = GetExamples()
  $iCount = aExamples.Count
    
  If aExamples.Count = 0 Then
    
    If Not $hHelp Then
      Clear
      $hHelp = New WebView($hList) As "ExampleWebView"
      $hHelp.MoveScaled(0, 0, 4, 4)
      $hHelp.Foreground = Color.LightForeground
      $hHelp.HTML = "<html><body><style type=\"text/css\">BODY { font-family:sans-serif;font-style:italic;color:#" & Hex$(Color.LightForeground) & "; }</style>" & 
        Subst(("Examples are stored on the Gambas farm server. Click &1 to access the farm server and download them..."), "<a href=\"farm://\">" & ("here") & "</a>") &
        "</body></html>"
    Endif
    $hList.Arrangement = Arrange.Fill
    $hFilter.Enabled = False
    
  Else
  
    Clear

    For Each sExample In aExamples
    
      ' sDir = File.Dir(sExample)
      ' If sDir <> sLastDir Then
      '   If $iArrange = Arrange.Row And If $hList.Children.Count Then
      '     hPanel = New Panel($hList)
      '     hPanel.Resize(1, 1)
      '     hPanel.Expand = True
      '   Endif
      '   hDrawingArea = New DrawingArea($hList) As "dwgTitle"
      '   With hDrawingArea
      '     .Enabled = False
      '     .Tag = Project.ExampleTitle[sDir]
      '     sGroup = .Tag
      '     '.Font = Font["Bold"]
      '     .H = .Font.Height
      '     If $iArrange = Arrange.Row Then .Expand = True
      '   End With
      '   'bHighlight = False
      '   sLastDir = sDir
      ' Endif
      
      hProjectItem = New ProjectBox($hList) As "ProjectItem"
      If $iArrange = Arrange.Row Then
        hProjectItem.ResizeScaled(32, 16)
      Else
        hProjectItem.ResizeScaled(1, 9)
      Endif
      hProjectItem.ShowAuthors = True
      hProjectItem.Path = sExample
      'hProjectItem.Group = sGroup
    
    Next
  
    ApplyFilter($hFilter.Text)
    $hList.Arrangement = $iArrange
    $hFilter.Enabled = True
  
  Endif
  
  Dec Application.Busy

End

Private Sub GetSoftwares() As String[]

  Dim sFile As String
  Dim aList As New String[]
  Dim sDir As String
  Dim hStat As Stat
  Dim sVendor As String

  sDir = Desktop.DataDir &/ "gambas3/src/"
  If Not IsDir(sDir) Then Return aList

  For Each sVendor In Dir(sDir)
    For Each sFile In Dir(sDir &/ sVendor)
      If Left(sFile) = "." Then Continue
      hStat = Stat(sDir &/ sVendor &/ sFile)
      If hStat.Link Then Continue
      aList.Add(sDir &/ sVendor &/ sFile)
    Next
  Next

  aList.Sort(gb.Natural)

  Return aList

End

Public Sub IsVoid() As Boolean
  
  Return $iCount = 0
  
End


Private Sub FillWithSoftware()

  Dim hProjectItem As ProjectBox
  Dim sGroup As String
  Dim aSoft As String[]
  Dim sSoft As String
  Dim sDir As String
  Dim sLastDir As String
  Dim hPanel As Panel
  Dim hDrawingArea As DrawingArea
  Dim sTitle As String
  
  Inc Application.Busy
  $hList.Arrangement = Arrange.None
  
  aSoft = GetSoftwares()
  $iCount = aSoft.Count
  
  If aSoft.Count = 0 Then
    
  Else
  
    Clear

    For Each sSoft In aSoft
    
      sDir = File.Dir(sSoft)
      If sDir <> sLastDir Then
        If $iArrange = Arrange.Row And If $hList.Children.Count Then
          hPanel = New Panel($hList)
          hPanel.Resize(1, 1)
          hPanel.Expand = True
        Endif
        hDrawingArea = New DrawingArea($hList) As "dwgTitle"
        With hDrawingArea
          .Enabled = False
          sTitle = File.Name(sDir)
          If sTitle = ".extension" Then
            sTitle = ("IDE extensions")
          Else
            sTitle = String.UCaseFirst(sTitle)
          Endif
          .Tag = sTitle
          sGroup = .Tag
          '.Font = Font["Bold"]
          .H = .Font.Height
          If $iArrange = Arrange.Row Then .Expand = True
        End With
        'bHighlight = False
        sLastDir = sDir
      Endif
      
      hProjectItem = New ProjectBox($hList) As "ProjectItem"
      If $iArrange = Arrange.Row Then
        hProjectItem.ResizeScaled(32, 16)
      Else
        hProjectItem.ResizeScaled(1, 9)
      Endif
      hProjectItem.ShowAuthors = True
      hProjectItem.ShowRemove = True
      hProjectItem.Path = sSoft
      hProjectItem.Group = sGroup
      
    Next
  
    ApplyFilter($hFilter.Text)
    $hList.Arrangement = $iArrange
    $hFilter.Enabled = True
  
  Endif
  
  Dec Application.Busy

End

Private Sub GetTemplates() As String[]

  Dim sFile As String
  Dim aList As New String[]
  Dim sDir As String

  sDir = System.Path &/ "share/" & "gambas" & System.Version &/ "template"
  If IsDir(sDir) Then
    For Each sFile In Dir(sDir).Sort(gb.Natural)
      If Left(sFile) = "." Then Continue
      If Not Exist(sDir &/ sFile &/ ".project") Then Continue
      aList.Add(sDir &/ sFile)
    Next
  Endif

  sDir = Desktop.DataDir &/ "gambas3/template/"
  If IsDir(sDir) Then 
    For Each sFile In Dir(sDir).Sort(gb.Natural)
      If Left(sFile) = "." Then Continue
      If Not Exist(sDir &/ sFile &/ ".project") Then Continue
      aList.Add(sDir &/ sFile)
    Next
  Endif

  Return aList

End

Private Sub FillWithTemplate()

  Dim hProjectItem As ProjectBox
  Dim sGroup As String
  Dim aSoft As String[]
  Dim sSoft As String
  
  Inc Application.Busy
  $hList.Arrangement = Arrange.None
  
  aSoft = GetTemplates()
  $iCount = aSoft.Count
  
  If aSoft.Count Then
    
    Clear

    For Each sSoft In aSoft
    
      hProjectItem = New ProjectBox($hList) As "ProjectItem"
      hProjectItem.ResizeScaled(40, 16)
      hProjectItem.Path = sSoft
      hProjectItem.Group = sGroup
      hProjectItem.Template = True
    
    Next
  
    ApplyFilter($hFilter.Text)
    $hList.Arrangement = $iArrange
    $hFilter.Enabled = True
  
  Endif
  
  Dec Application.Busy

End

Public Sub Clear()
  
  $hList.Children.Clear
  $hHelp = Null
  $hCurrent = Null
  
End


Private Sub FillWithRecent(bClear As Boolean)
  
  Dim hProjectItem As ProjectBox
  'Dim hFirst As ProjectBox
  Dim aRecent As CRecentProject[]
  Dim hRecent As CRecentProject
  'Dim bHighlight As Boolean
  Dim sTitle As String
  Dim sLastTitle As String
  Dim hDrawingArea As DrawingArea
  Dim hPanel As Panel
  
  If Not bClear Then
    If $hList.Children.Count And If Not CRecentProject.IsOutOfDate($dRecent) Then Return
  Endif

  Inc Application.Busy

  'panRecent.Hide
  'lstRecent.Lock
  $hList.Arrangement = Arrange.None
  Clear()

  aRecent = CRecentProject.Get($iSort)
  $iCount = aRecent.Count

  For Each hRecent In aRecent
    
    sTitle = hRecent.GetTitle($iSort)
    
    If sTitle <> sLastTitle Then
      If $iArrange = Arrange.Row Then
        If $hList.Children.Count Then
          hPanel = New Panel($hList)
          hPanel.Resize(1, 1)
          hPanel.Expand = True
        Endif
      Endif
      hDrawingArea = New DrawingArea($hList) As "dwgTitle"
      With hDrawingArea
        .Enabled = False
        .Tag = sTitle 
        '.Font = Font["Bold"]
        .H = .Font.Height
        If $iArrange = Arrange.Row Then .Expand = True
      End With
      sLastTitle = sTitle
    Endif
    
    hProjectItem = New ProjectBox($hList) As "ProjectItem"
    If $iArrange = Arrange.Row Then
      hProjectItem.ResizeScaled(32, 16)
    Else
      hProjectItem.ResizeScaled(1, 9)
    Endif
    hProjectItem.ShowAuthors = True
    hProjectItem.ShowRemove = True
    hProjectItem.Path = hRecent.Path
    
  Next
  
  ApplyFilter($hFilter.Text)
  
  'If hFirst Then hFirst.Selected = True
  
  ' Workaround a scrollview bug that does not show the scrollbars.
  'lstRecent.Unlock 
  'panRecent.Show
  
  $dRecent = CRecentProject.LastLoad
  
  $hList.Arrangement = $iArrange
  $hList.ScrollY = 0
  
  $hFilter.Enabled = aRecent.Count
  
  Dec Application.Busy
  
End

Public Sub Fill(Optional bClear As Boolean)
  
  Select Case $iType
    Case TYPE_RECENT
      FillWithRecent(bClear)
    Case TYPE_EXAMPLE
      FillWithExample
    Case TYPE_SOFTWARE
      FillWithSoftware
    Case TYPE_TEMPLATE
      FillWithTemplate
  End Select
  
  Raise Fill
  
End

Public Sub ApplyFilter(sFilter As String)

  Dim hCtrl, hLast As Control
  Dim hProjectItem As ProjectBox
  Dim bShowEnd As Boolean

  $hList.Arrangement = Arrange.None
  
  If $bNoFilter Or If Not sFilter Then
    sFilter = "*"
  Else
    sFilter = "*" & String.LCase(sFilter) & "*"
  Endif
  
  For Each hCtrl In $hList.Children
    
    If hCtrl Is ProjectBox Then
      hProjectItem = hCtrl
      hProjectItem.Visible = hProjectItem.Match(sFilter)
      ' If hProjectItem.Visible Then 
      '   If Not hFirst Then hFirst = hProjectItem
      ' Endif
    Endif
    
  Next
  
  For Each hCtrl In $hList.Children
    
    If hCtrl Is DrawingArea Then
      If hLast Then 
        hLast.Hide
      Endif
      hLast = hCtrl
      bShowEnd = False
    Else If hCtrl Is ProjectBox Then
      If hCtrl.Visible Then
        If hLast Then 
          hLast.Show
        Endif
        hLast = Null
        bShowEnd = True
      Endif
    Else
      hCtrl.Visible = bShowEnd
    Endif
    
  Next
  
  If hLast Then hLast.Hide
  
  $hList.Arrangement = Arrange.Row
  
  'hFirst.Selected = True
  'hList.Unlock
  
End


Public Sub dwgTitle_Draw()
  
  Dim hDrawingArea As DrawingArea = Last  
  
  Paint.Background = Color.LightForeground
  Paint.DrawText(hDrawingArea.Tag, 1, 0, Paint.W, Paint.H, Align.BottomNormal)
  
End

Public Sub ProjectItem_Click()

  Me.Current = Last
  Raise Click($hCurrent.Path, False)
  
End

Public Sub ProjectItem_DblClick()

  Me.Current = Last
  Raise DblClick($hCurrent.Path)
  
End

Public Sub ProjectItem_ButtonClick()
  
  Raise Click(Last.Path, True)
  
End

Public Sub ProjectItem_RemoveClick()
  
  Dim hItem As ProjectBox = Last
  Dim sErr As String
  
  If $hCurrent = hItem Then $hCurrent = Null
  
  If $iType = TYPE_RECENT Then 
    
    CRecentProject.Remove(hItem.Path)
    hItem.Delete
    
  Else If $iType = TYPE_SOFTWARE Then
    
    If Message.Question(("Do you really want to uninstall this software?"), ("Uninstall"), ("Cancel")) = 1 Then
      sErr = CSoftware.UninstallFromPath(hItem.Path)
      If sErr Then
        Message.Error(("Unable to uninstall software:") & "\n\n" & sErr)
      Else
        Clear
        Fill
        Message(("The software has been successfully uninstalled."))
        Raise Refresh
      Endif
    Endif
    
  Endif
  
End

' Public Sub ProjectItem_preparePopUp(item As ProjectBox)
' 
'   $hCurrent = item
' 
' End
' 
Public Sub GetFirst() As String

  Dim hChild As Control
  Dim hProjectItem As ProjectBox

  For Each hChild In $hList.Children
    Try hProjectItem = hChild
    If Not Error And If hProjectItem.Visible Then Return hProjectItem.Path
  Next

End


Public Sub Filter_Click()

  $hFilter.Text = ""

End

Public Sub Filter_Change()

  ApplyFilter($hFilter.Text)

End

Public Sub Filter_Activate()
  
  Dim sFirst As String = GetFirst()
  If sFirst Then Raise Click(sFirst, False)
  
End


Public Sub ScrollView_GotFocus()
  
  $hFilter.SetFocus
  
End

' Public Sub ScrollView_Arrange()
'   
'   Debug $hList.Name;; $hList.Children.Count;; 
'   If $hList.Children.Count Then
'     Error Str($hList.Children[0].Visible);;
'   Endif
'   Error
'   
' End


Private Function AllowOpenExtern_Read() As Boolean

  Return ProjectBox.ShowButton

End

Private Sub AllowOpenExtern_Write(Value As Boolean)

  ProjectBox.ShowButton = Value

End

Public Sub ExampleWebView_Link(sURL As String)
  
  $sLastURL = sURL
  
End

Public Sub ExampleWebView_Error()
  
  If $sLastURL = "farm://" Then 
    FSoftwareFarm.Run
    Fill
  Endif
  
End

Public Sub ExampleWebView_Menu()
  
  Stop Event
  
End

Private Function Current_Read() As ProjectBox

  Return $hCurrent

End

Private Sub Current_Write(Value As ProjectBox)

  If $hCurrent = Value Then Return
  
  If IsNull(Value) Then
    $hCurrent = Value
    Return
  Endif 
  
  If $hCurrent Then $hCurrent.Selected = False
  $hCurrent = Value
  If $hCurrent Then $hCurrent.Selected = True

End
