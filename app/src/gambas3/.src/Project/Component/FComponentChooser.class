' Gambas class file

Public Struct ComponentChooserItem
  Key As String
  Checked As Boolean
  Dependencies As String[]
End Struct

Static Private $aPict As Picture[]
Static Private $hPictLocked As Picture

Private $aComp As String[]
Private $cComp As Collection

Private $bShowDeprecated As Boolean
Private $aHidden As String[]
Private $iTooltipLastRow As Integer
Private $bShowCheckedOnly As Boolean
Private $aLibraries As New String[]

' Private Sub GetParent() As ComponentChooser
' 
'   Return Me.Parent
' 
' End

Public Sub _new()

  Dim hBorder As Border
  
  hBorder = Border("radius:5;width:2.5;margin:8;padding:6")
  hBorder.Color = Color.Gradient(Color.LightForeground, Color.TextBackground)
  gvwComponent.Rows.Border = hBorder
  
  gvwComponent.Columns.Count = 4
  gvwComponent.Columns[0].Width = 0
  'gvwComponent.Columns[0].Expand = False
  'gvwComponent.Columns[1].Expand = False
  gvwComponent.Columns[2].Expand = True
  gvwComponent.Columns[3].Width = 40
  'gvwComponent.Columns[3].Expand = False

  ScrollArea(gvwComponent.Proxy).Shadow = True

  RefreshList

End

Private Sub RefreshList()
  
  Dim aComp As New String[]
  Dim hComp As CComponent
  Dim sComp As String
  Dim sFilter As String
  
  sFilter = txtFilter.Text
  
  For Each hComp In CComponent.All
    
    sComp = hComp.Key
    If hComp.Hidden Then Continue
    If hComp.Key = CComponent.PROJECT_NAME Then Continue
    If hComp.Key = Project.Name Then Continue 
    If $aHidden And If $aHidden.Exist(sComp) Then Continue
    If sFilter And If Not hComp.Match(sFilter) Then Continue
    
    If $cComp Then
      If Not IsChecked(sComp) Then
        If $bShowCheckedOnly Then Continue
        If hComp.State = CComponent.DEPRECATED And If Not $bShowDeprecated Then Continue
      Endif
    Else
      If hComp.State = CComponent.DEPRECATED And If Not $bShowDeprecated Then Continue
    Endif
    
    aComp.Add(sComp)
  Next
  
  aComp.Sort
  
  SetComponents(aComp)
  
End


Public Sub txtFilter_Filter()

  RefreshList

End

Public Sub btnForward_Click()

  webAboutComponent.Forward

End

Public Sub webAboutComponent_Click((Frame) As WebFrame)

  btnForward.Enabled = webAboutComponent.History.Index < webAboutComponent.History.Max

End

Public Sub btnStop_Click()
  
  webAboutComponent.Stop
  
End

Public Sub btnReload_Click()
  
  webAboutComponent.Reload
  
End

Public Sub btnZoomIn_Click()
  
  webAboutComponent.TextZoom = Round(webAboutComponent.TextZoom * 1.25, -2)
  
End

Public Sub btnZoomOut_Click()
  
  webAboutComponent.TextZoom = Round(webAboutComponent.TextZoom / 1.25, -2)
  
End

Public Sub btnZoomNormal_Click()
  
  webAboutComponent.TextZoom = 0.8
  
End


Public Sub btnBack_Click()

  If webAboutComponent.History.Index Then
    webAboutComponent.Back
  Else
    webAboutComponent.Stop
    wizComponent.MovePrevious
  Endif
  
End


Private Sub SetComponents(Value As String[])

  If Not Value Then Value = New String[]
  $aComp = Value
  
  gvwComponent.UnSelectAll
  gvwComponent.Rows.Count = $aComp.Count
  gvwComponent.Columns[0].W = -1
  gvwComponent_Arrange

End

Public Sub gvwComponent_Data(Row As Integer, Column As Integer)
  
  Dim sComp As String = $aComp[Row]
  Dim hComp As CComponent = CComponent.All[sComp]
  
  With gvwComponent.Data
  
    Select Case Column
      
      Case 0
        If $cComp Then
          If sComp = "gb" Then
            .Picture = $hPictLocked
          Else
            .Picture = $aPict[GetState(sComp)]
          Endif
        Endif
      
      Case 1
        .Text = hComp.Key
        .Font.Bold = True
        If hComp.State = CComponent.UNSTABLE Then 
          .Font.Italic = True
        Else If hComp.State = CComponent.DEPRECATED Then
          .Foreground = Color.LightForeground
          '.Font.Strikeout = True
        Endif
        
      Case 2
        .WordWrap = True
        .RichText = hComp.Name
        If hComp.State = CComponent.UNSTABLE Then 
          .Font.Italic = True
        Else If hComp.State = CComponent.DEPRECATED Then
          .Foreground = Color.LightForeground
          '.Font.Strikeout = True
        Endif
        
      Case 3
        Select Case hComp.State
          Case CComponent.STABLE_NOT_FINISHED
            .Picture = Picture["img/16/unfinished.png"]
          Case CComponent.UNSTABLE
            .Picture = Picture["img/16/experimental.png"]
          Case CComponent.STABLE
            .Picture = Picture["img/16/finished.png"]
          Case CComponent.DEPRECATED
            .Picture = Picture["img/16/deprecated.png"]
        End Select
      
    End Select
    
    .Alignment = Align.TopNormal
    '.Background = If(Even(Row), Color.Gradient(Color.LightForeground, Color.TextBackground, 0.9), Color.TextBackground)
    .Background = Color.Gradient(Color.LightForeground, Color.TextBackground, 0.96)
    
  End With
  
End

Public Function GetCurrent() As String

  Try Return $aComp[gvwComponent.Row]

End

Public Sub SetCurrent(Value As String)

  gvwComponent.Row = $aComp.Find(Value)

End

Public Sub gvwComponent_Arrange()

  Dim I As Integer
  
  gvwComponent.Columns[1].Width = -1

  For I = 0 To $aComp.Max
    gvwComponent.Rows[I].H = -1
    'Debug I;; gvwComponent.Rows[I].H
  Next

End

Public Sub gvwComponent_Activate()
  
  MHelp.InitWebViewWith(webAboutComponent, MHelp.TYPE_COMPONENT, GetCurrent(),, "v&nh&l=" & MHelp.GetLanguage())
  wizComponent.MoveNext
  
End

Public Sub HasBorder() As Boolean
  
  Return wizComponent.Border
  
End

Public Sub SetBorder(Value As Boolean)
  
  wizComponent.Border = Value
  
End


Public Sub GetShowDeprecated() As Boolean
  
  Return $bShowDeprecated
  
End

Public Sub SetShowDeprecated(Value As Boolean)
  
  $bShowDeprecated = Value
  RefreshList
  
End

Public Sub GetHiddenComponents() As String[]
  
  Return $aHidden
  
End

Public Sub SetHiddenComponents(Value As String[])
  
  $aHidden = Value
  RefreshList
  
End

Public Sub gvwComponent_MouseMove()

  Dim iRow As Integer
  Dim hComp As CComponent
  Dim sText As String
  
  If gvwComponent.ColumnAt(Mouse.X) = 3 Then
    
    iRow = gvwComponent.RowAt(Mouse.Y)
    
    Try hComp = CComponent.All[$aComp[iRow]]
    If hComp Then
      Select Case hComp.State
        Case CComponent.STABLE_NOT_FINISHED
          sText = ("Not finished but stable")
        Case CComponent.UNSTABLE
          sText = ("Experimental")
        Case CComponent.STABLE
          sText = ("Stable")
        Case CComponent.DEPRECATED
          sText = ("Deprecated")
      End Select
    Endif
    
    If $iTooltipLastRow <> iRow Then
      gvwComponent.Tooltip = sText
      $iTooltipLastRow = iRow
    Endif
    
  Else
    
    gvwComponent.Tooltip = ""
    $iTooltipLastRow = -1
    
  Endif

End

Public Sub IsCheckable() As Boolean
  
  Return $cComp
  
End

Public Sub SetCheckable(Value As Boolean)
  
  gvwComponent.Columns[0].W = If(Value, 40, 0)

  If Value Then
    
    If Not $aPict Then
      $aPict = New Picture[3]
      $aPict[ComponentChooser.Unchecked] = Picture["img/16/unchecked.png"]
      $aPict[ComponentChooser.Checked] = Picture["img/16/checked.png"]
      $aPict[ComponentChooser.GrayChecked] = Picture["img/16/checked-gray.png"]
      $hPictLocked = Picture["img/16/checked-lock.png"]
    Endif
    
    $cComp = New Collection
    
  Else
    
    $cComp = Null
    
  Endif
  
End


Private Sub GetState(sComp As String) As Integer
  
  Dim hComp As ComponentChooserItem 
  
  If sComp = "gb" Then Return ComponentChooser.Checked
  
  hComp = $cComp[sComp]
  
  If hComp Then 
    If hComp.Checked Then Return ComponentChooser.Checked
    If hComp.Dependencies.Count Then Return ComponentChooser.GrayChecked
  Endif
  
End

Public Sub IsChecked(sComp As String) As Boolean
  
  Return GetState(sComp)
  
End


Public Sub gvwComponent_Leave()

  gvwComponent.Tooltip = ""

End

Public Sub gvwComponent_Enter()

  $iTooltipLastRow = -1

End

Public Sub SetFocus()
  
  txtFilter.SetFocus
  
End

Public Sub gvwComponent_Click()
  
  Dim sComp As String
  'Dim sDep As String
  
  If gvwComponent.Column <> 0 Then Return
  
  sComp = $aComp[gvwComponent.Row]
  If sComp = "gb" Then Return
  
  Select Case GetState(sComp)
    Case ComponentChooser.Checked
      UncheckComponent(sComp)
    Case Else
      CheckComponent(sComp)
  End Select
  
  ' For Each sDep In GetRequire(hCompItem.Key)
  '   If hCompItem.Value Then
  '     $cComp[sDep].Check(hCompItem.Key)
  '   Else
  '     $cComp[sDep].Uncheck(hCompItem.Key)
  '   Endif
  ' Next
  
End

Private Sub GetRequire(sComp As String) As String[]
  
  Dim hComp As CComponent = CComponent.All[sComp]
  
  Return hComp.Require
  
End

Public Sub CheckComponent(sComp As String, Optional sParent As String)
  
  Dim hComp As ComponentChooserItem
  Dim sDep As String
  
  If sComp = "gb" Then Return
  
  hComp = $cComp[sComp]
  If Not hComp Then
    hComp = New ComponentChooserItem
    hComp.Key = sComp
    hComp.Dependencies = New String[]
    $cComp[sComp] = hComp    
  Endif
  
  If sParent Then
    hComp.Dependencies.Add(sParent)
  Else
    hComp.Checked = True
  Endif
  
  For Each sDep In GetRequire(sComp)
    CheckComponent(sDep, sComp)
  Next
  
  gvwComponent.Refresh

End

Private Sub GetDependencies(hComp As ComponentChooserItem, Optional bLib As Boolean) As String[]
  
  Dim aDep As New String[]
  Dim sComp As String
  
  For Each sComp In hComp.Dependencies
    
    If Not sComp Then Continue
    If Left(sComp) = "/" Then
      aDep.Add(File.Name(sComp))
    Else If Not bLib Then
      aDep.Add(sComp)
    Endif
    
  Next
  
  Return aDep
  
End

Public Sub UncheckComponent(sComp As String, Optional sParent As String) As Boolean
  
  Dim hComp As ComponentChooserItem
  Dim sMsg As String
  Dim aDep As String[]
  Dim sDep As String
  
  If sComp = "gb" Then Return
  
  hComp = $cComp[sComp]
  If Not hComp Then Return
  
  If sParent Then
    If hComp.Dependencies.Exist(sParent) Then
      hComp.Dependencies.Remove(hComp.Dependencies.Find(sParent))
      Return
    Endif
  Endif
  
  aDep = GetDependencies(hComp, True)
  
  If aDep.Count Then
    If aDep.Count = 1 Then
      sMsg = Subst(("This component is required by the following library:<p>&1"), "<b>" & aDep[0] & "</b>")
    Else
      sMsg = Subst(("This component is required by the following libraries:<p>&1"), "<b>" & aDep.Join("</b>, <b>") & "</b>")
    Endif
    Message.Warning(sMsg)
    Return True
  Endif
  
  aDep = GetDependencies(hComp)
  If aDep.Count Then
  
    sMsg = Subst(("This component is required by:<p>&1"), "<b>" & aDep.Join("</b>, <b>") & "</b>")
    If Message.Warning(sMsg, ("Keep"), ("Remove all")) = 2 Then
      
      For Each sDep In aDep 
        UncheckComponent(sDep)
      Next
      
    Endif
    
    aDep = GetDependencies(hComp)
    
  Endif
  
  If aDep.Count = 0 Then
    $cComp.Remove(sComp)
  Else
    hComp.Checked = False
  Endif
  
  For Each sDep In GetRequire(sComp)
    hComp = $cComp[sDep]
    hComp.Dependencies.Remove(hComp.Dependencies.Find(sComp))
  Next
  
  gvwComponent.Refresh

End



Public Sub SetCheckedComponents(aComp As String[])
  
  Dim sComp As String
  
  $cComp.Clear
  $aLibraries.Clear

  CheckComponent("gb")  
  For Each sComp In aComp
    CheckComponent(sComp)
  Next
  RefreshList
  
End

Public Sub GetCheckedComponents() As String[]

  Dim hComp As ComponentChooserItem
  Dim aComp As New String[]
  
  For Each hComp In $cComp
    If hComp.Key <> "gb" Then aComp.Add(hComp.Key)
  Next
  
  Return aComp.Sort()
  
End

Public Sub CheckSelection() As Boolean
  
  Dim aComp As New String[]
  Dim hComp As CComponent
  Dim sComp As String
  Dim aExclude As String[]
  Dim aNeed As String[]
  Dim sImpl As String
  Dim sExcl As String
  Dim sNeed As String
  Dim bFound As Boolean

  aComp = GetCheckedComponents()
  
  For Each sComp In aComp
  
    aNeed = New String[]
    aExclude = New String[]

    For Each sImpl In CComponent.All[sComp].Implement
    
      For Each hComp In CComponent.All
      
        If hComp.Key = sComp Then Continue
        If hComp.Implement.Find(sImpl) >= 0 Then
          If Not aComp.Exist(hComp.Key) Then Continue
          If aExclude.Find(hComp.Key) < 0 Then aExclude.Add(hComp.Key)
        Endif
      
      Next
    
    Next

    For Each sExcl In CComponent.All[sComp].Exclude
      If Not aComp.Exist(sExcl) Then Continue
      If Not aExclude.Exist(sExcl) Then aExclude.Add(sExcl)
    Next
    
    ' IF aExclude.Count = 1 THEN
    '   Message.Warning(Subst(("The &2 component is incompatible with the &1 component."), "<b>" & sComp & "</b>", "<b>" & aExclude[0] & "</b>"))
    '   RETURN TRUE
    ' ELSE IF aExclude.Count > 1 THEN
    '   Message.Warning(Subst(("These components are incompatible with the &1 component:\n&2"), "<b>" & sComp & "</b>", "<b>" & aExclude.Join("</b>, <b>") & "</b>"))
    '   RETURN TRUE
    ' ENDIF

    If aExclude.Count Then
      Message.Warning(Subst(("These components are incompatible:<br>&1."), "<b>" & sComp & "</b>, <b>" & aExclude.Join("</b>, <b>") & "</b>"))
      Return True
    Endif

    For Each sNeed In CComponent.All[sComp].Need
      bFound = False
      For Each hComp In CComponent.All
        If hComp.Implement.Exist(sNeed) Then
          If Not aNeed.Exist(hComp.Key) Then aNeed.Add(hComp.Key)
          If aComp.Exist(hComp.Key) Then bFound = True
        Endif
      Next
      If Not bFound Then
        Message.Warning(Subst(("The &1 component needs one of the following components:<br>&2."), "<b>" & sComp & "</b>", "<b>" & aNeed.Join("</b>, <b>") & "</b>"))
        Return True
      Endif 
    Next
      
    For Each sNeed In CComponent.All[sComp].Require
      If Not aComp.Exist(sNeed) Then 
        Message.Warning(Subst(("The &1 component needs the &2 component."), "<b>" & sComp & "</b>", "<b>" & sNeed & "</b>"))
        Return True
      Endif
    Next
  
  Next  
  
End

Public Sub GetCheckedOnly() As Boolean
  
  Return $bShowCheckedOnly
  
End

Public Sub SetCheckedOnly(Value As Boolean)
  
  $bShowCheckedOnly = Value  
  RefreshList
  
End

Public Sub HideBrowser()
  
  wizComponent.Index = 0
  
End

Public Sub SetLibraries(Value As String[])

  Dim sLib As String
  Dim hLibInfo As CLibraryInfo
  Dim sDep As String
  
  If Not Value Then Value = New String[]
  If Value.Join("\n") = $aLibraries.Join("\n") Then Return
  
  For Each sLib In $aLibraries
    hLibInfo = CLibraryInfo[sLib]
    For Each sDep In hLibInfo.Require
      UncheckComponent(sDep, sLib)
    Next
  Next

  $aLibraries = Value.Copy()
  
  For Each sLib In $aLibraries
    hLibInfo = CLibraryInfo[sLib]
    For Each sDep In hLibInfo.Require
      CheckComponent(sDep, sLib)
    Next
  Next

End

Public Sub GetLibraries() As String[]
  
  Return $aLibraries
  
End

