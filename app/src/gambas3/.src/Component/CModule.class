' Gambas class file

Static Public All As Collection
Static Public Ext As String[]

Public (Key) As String
Public Name As String
Public NamePlural As String
Public NameFilter As String
Public (Component) As String
Public Used As Boolean
'Public HasFormEditor As Boolean

Static Public Sub _init()
  
  Dim sModule As String
  Dim hModule As CModule
  
  All = New Collection(gb.IgnoreCase)
  
  For Each sModule In ["module", "class"]
    Picture["img/16" &/ sModule & ".png"] = Project.MakeModuleIcon(sModule, 16).Picture
    Picture["img/32" &/ sModule & ".png"] = Project.MakeModuleIcon(sModule, 32).Picture
  Next
  
  Ext = ["module", "class"]
  
  hModule = New CModule("form", ("Form"), ("Forms"), ("Gambas forms"))
  hModule = New CModule("webpage", ("WebPage"), ("WebPages"), ("Gambas web pages"))
  hModule = New CModule("report", ("Report"), ("Reports"), ("Gambas reports"))
  
End

Static Public Sub Exit()
  
  All = Null
  
End


Static Public Sub _get(sKey As String) As CModule
  
  Return All[sKey]
  
End

Public Sub _new(sKey As String, sName As String, sNamePlural As String, sNameFilter As String)
  
  Key = sKey
  Name = sName
  NamePlural = sNamePlural
  NameFilter = sNameFilter
  'HasFormEditor = bHasFormEditor
  All[sKey] = Me
  
  Picture["img/16" &/ sKey & ".png"] = Project.MakeModuleIcon(sKey, 16).Picture
  Picture["img/32" &/ sKey & ".png"] = Project.MakeModuleIcon(sKey, 32).Picture
  
  Ext.Add(sKey)

End

Static Public Sub Reset()
  
  Dim hModule As CModule
  
  For Each hModule In All
    hModule.Component = ""
    hModule.Used = False
  Next
  
End


Static Public Sub Activate(sFamily As String)
  
  Dim hModule As CModule = All[sFamily]
  
  If hModule Then hModule.Used = True
  
End

Static Public Sub FromTreeKey(sKey As String) As CModule
  
  Dim hModule As CModule
  
  For Each hModule In All
    If hModule.TreeKey = sKey Then Return hModule
  Next
  
End

Static Public Sub FromExt(sExt As String) As CModule
  
  Dim hModule As CModule
  
  For Each hModule In All
    If hModule.Key = LCase(sExt) Then Return hModule
  Next
  
End


