' Gambas class file

PUBLIC Name AS String
PUBLIC Title AS String
PUBLIC Version AS String
PUBLIC Description AS String
PUBLIC Authors AS String[]
PUBLIC Icon AS Picture

PRIVATE SUB GetProjectPath(sPath AS String) AS String
  
  DO
    IF Exist(sPath &/ ".project") THEN RETURN sPath
    sPath = File.Dir(sPath)
    IF sPath = "/" THEN BREAK
  LOOP
  
CATCH
  
END

PUBLIC SUB _new(sPath AS String)
  
  DIM hFile AS File
  DIM sIcon AS String
  DIM sLine AS String
  DIM iPos AS Integer
  DIM sKey AS String
  DIM sVal AS String
  
  sPath = GetProjectPath(sPath)
  IF NOT sPath THEN RETURN
  
  Name = File.Name(sPath)
    
  hFile = OPEN sPath &/ ".project"
  
  WHILE NOT Eof(hFile)
    LINE INPUT #hFile, sLine
    sLine = Trim(sLine)
    IF NOT sLine THEN CONTINUE 
    IF Left(sLine) = "#" THEN CONTINUE

    iPos = InStr(sLine, "=")
    IF iPos = 0 THEN CONTINUE

    sKey = Lower$(Trim(Left$(sLine, iPos - 1)))
    sVal = Trim(Mid$(sLine, iPos + 1))

    SELECT CASE sKey
    
      CASE "title"
        Title = sVal

      CASE "version"
        Version = sVal
        
      CASE "description"
        Description = Replace(sVal, "\\n", "\n")

      CASE "authors"
        Authors = Split(Replace(sVal, "\\n", "\n"), "\n")

      CASE "icon"
        sIcon = sVal

    END SELECT
  
  WEND
  
  CLOSE #hFile

  IF sIcon AND Exist(sPath &/ sIcon) THEN
    Icon = Picture[sPath &/ sIcon].Image.Stretch(58, 58).Picture
  ELSE 
    Icon = Picture["img/32/gambas.png"].Image.Stretch(58, 58).Picture
  ENDIF
  
END
