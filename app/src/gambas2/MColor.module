' Gambas module file

PRIVATE CONST DEFAULT_THEME AS String = "sapphire"

PRIVATE $aColor AS String[] = ["Background", "Normal", "Keyword", "Subr", "Operator", "Symbol", "Number", "String", "Comment", "Breakpoint", "Current", "Datatype", "Selection", "Highlight", "Line", "Error"]
PRIVATE $cDefault AS Collection

PUBLIC SUB _init()
  
  $cDefault = ReadTheme(DEFAULT_THEME)

END


PRIVATE FUNCTION ToString(iColor AS Integer) AS String
  
  RETURN "&" & Hex$(iColor, 6) & "&"
  
END



PUBLIC FUNCTION ReadColor(hSettings AS Settings, sKey AS String) AS Collection
  
  DIM cColor AS NEW Collection
  DIM sColor AS String
  DIM iColor AS Integer
  DIM iInd AS Integer
  
  FOR EACH sColor IN $aColor
    iColor = Val(hSettings[sKey &/ sColor, "-1"])
    IF iColor = -1 THEN iColor = $cDefault[sColor]
    cColor[sColor] = iColor
    cColor[iInd] = iColor
    INC iInd
  NEXT
  
  RETURN cColor
  
END

PUBLIC SUB WriteColor(hSettings AS Settings, sKey AS String, cColor AS Collection)
  
  DIM sColor AS String
  DIM iColor AS Integer
  DIM iInd AS Integer
  
  FOR EACH sColor IN $aColor
    hSettings[sKey &/ sColor] = ToString(cColor[sColor])
  NEXT
  
END


PUBLIC FUNCTION ReadTheme(sPath AS String) AS Collection
  
  DIM hSettings AS Settings
  DIM sTemp AS String
  
  IF Left(sPath) <> "/" THEN
    sPath = "theme" &/ sPath
    sTemp = Temp$()
    COPY sPath TO sTemp
    sPath = sTemp
  ENDIF
  
  hSettings = NEW Settings(sPath)
  IF sTemp THEN KILL sTemp
  
  RETURN ReadColor(hSettings, "Theme")
  
END

PUBLIC SUB WriteTheme(sPath AS String, cColor AS Collection)
  
  DIM hSettings AS Settings
  DIM hCtrl AS Control

  hSettings = NEW Settings(sPath)
  WriteColor(hSettings, "Theme", cColor)  
  
END

PUBLIC SUB Copy(cColor AS Collection) AS Collection
  
  DIM cNew AS NEW Collection
  DIM sColor AS String
  
  FOR EACH sColor IN $aColor
    cNew[sColor] = cColor[sColor]
  NEXT
  
  RETURN cNew
  
END

PUBLIC SUB InitEditor(hEditor AS Editor)

  DIM iInd AS Integer  
  DIM iColor AS Integer
  
  FOR iInd = 0 TO $aColor.Max

    hEditor.Styles[iInd].Color = Val(Settings["Highlight" &/ $aColor[iInd], ToString($cDefault[$aColor[iInd]])])

  NEXT
  
END
