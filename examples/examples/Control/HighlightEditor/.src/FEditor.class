' Gambas class file

PRIVATE hEditor AS Editor
PRIVATE $bIgnore AS Boolean
PRIVATE $bModified AS Boolean

PUBLIC SUB Form_Open()

  $bIgnore = TRUE
  Editor1.Text = File.Load("download.html")
  VSplit1.Layout = "1,0"

  Editor1.Flags[Editor.HighlightBraces] = TRUE
  Editor1.Styles[Highlight.Operator].Color = Color.DarkGreen
  Editor1.Styles[Highlight.Operator].Bold = TRUE
  Editor1.Styles[Highlight.String].Color = Color.DarkRed
  Editor1.Flags[Editor.ShowLineNumbers] = TRUE

  Editor2.Flags[Editor.HighlightBraces] = TRUE
  Editor2.Styles[Highlight.Operator].Color = Color.DarkGreen
  Editor2.Styles[Highlight.Operator].Bold = TRUE
  Editor2.Styles[Highlight.String].Color = Color.DarkRed

  mnuImmediately_Click

END

PUBLIC SUB Editor1_Highlight()

  DIM iState AS Integer
  DIM iNextState AS Integer
  DIM iInd AS Integer
  DIM J AS Integer
  DIM sText AS String
  DIM sCar AS String
  DIM iPos AS Integer
  DIM bMarkup AS Boolean

  iState = Highlight.State
  sText = Highlight.Text

  'PRINT "Highlight:";; iState;; sText

  FOR iInd = 1 TO String.Len(sText)

    iNextState = iState
    sCar = String.Mid$(sText, iInd, 1)

    IF bMarkup THEN

      IF sCar = ">" THEN
        bMarkup = FALSE
        iState = Highlight.Keyword
        iNextState = Highlight.Normal
      ELSE IF sCar = " " THEN
        iNextState = Highlight.Operator
      ELSE IF sCar = "=" THEN
        iNextState = Highlight.String
      ENDIF

    ELSE

      SELECT CASE iState
        CASE Highlight.Normal
          IF sCar = "<" THEN
            IF String.Mid$(sText, iInd, 4) = "<!--" THEN
              iState = Highlight.Comment
              iNextState = Highlight.Comment
            ELSE
              iState = Highlight.Keyword
              iNextState = Highlight.Keyword
              bMarkup = TRUE
            ENDIF
          ELSE IF sCar = "&" THEN
            iPos = String.InStr(sText, ";", iInd)
            IF iPos = 0 OR iPos = iInd + 1 THEN
              iState = Highlight.Error
            ELSE
              FOR J = iInd + 1 TO iPos - 1
                sCar = String.Mid$(sText, J, 1)
                IF IsLetter(sCar) THEN CONTINUE
                IF IsDigit(sCar) THEN CONTINUE
                IF InStr("_#", sCar) THEN CONTINUE
                BREAK
              NEXT
              IF J = iPos THEN
                Highlight.Add(Highlight.Number, iPos - iInd + 1)
                iInd = iPos
                CONTINUE
              ELSE
                iState = Highlight.Error
              ENDIF
            ENDIF
          ENDIF
        CASE Highlight.Comment
          IF sCar = ">" AND IF iInd > 2 AND IF String.Mid$(sText, iInd - 2, 2) = "--" THEN
            iNextState = Highlight.Normal
          ENDIF
      END SELECT

    ENDIF

    Highlight.Add(iState)
    iState = iNextState

  NEXT

  IF iNextState <> Highlight.Comment THEN
    iNextState = Highlight.Normal
  ENDIF

  Highlight.State = iNextState
  Highlight.ShowLimit = FALSE

END

PUBLIC SUB Editor1_Menu()

  hEditor = Editor1
  mnuImmediately.Checked = hEditor.Flags[Editor.HighlightCurrent]
  mnuPopup.Popup

END

PUBLIC SUB Editor2_Menu()

  hEditor = Editor2
  mnuImmediately.Checked = hEditor.Flags[Editor.HighlightCurrent]
  mnuPopup.Popup

END

PUBLIC SUB mnuHighlight_Click()

  INC mnuHighlight.Checked
  IF mnuHighlight.Checked THEN
    hEditor.Highlight = Highlight.Custom
  ELSE
    hEditor.Highlight = Highlight.None
  ENDIF

END

PUBLIC SUB mnuImmediately_Click()

  INC mnuImmediately.Checked
  Editor1.Flags[Editor.HighlightCurrent] = mnuImmediately.Checked
  Editor2.Flags[Editor.HighlightCurrent] = mnuImmediately.Checked

END


PUBLIC SUB mnuQuit_Click()

  ME.Close

END



PUBLIC SUB Editor1_Change()

  IF $bIgnore THEN
    $bIgnore = FALSE
    RETURN
  ENDIF
  IF NOT $bModified THEN
    $bModified = TRUE
    ME.Title &= " [modified]"
  ENDIF

END

PUBLIC SUB VSplit1_Resize()

  IF Editor2.H < 8 THEN
    IF Editor2.View THEN Editor2.View = Editor2
  ELSE
    IF Editor2.View = Editor2 THEN Editor2.View = Editor1
  ENDIF

END
