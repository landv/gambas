' Gambas class file

STATIC $aPict AS NEW String[]
STATIC $hApp AS Object

STATIC PUBLIC SUB Main()

  DIM hForm AS Form
  
  $hApp = Application["kdesktop"]
  
  hForm = NEW FScript
  hForm.Show
  
END


PUBLIC SUB btnPath_Click()

  Dialog.Title = "Choose a wallpaper directory"
  IF Dialog.SelectDirectory() THEN RETURN
  txtPath.Text = Dialog.Path

END

PUBLIC SUB btnQuit_Click()

  ME.Close

END

PUBLIC SUB txtInterval_Change()

  timChange.Delay = txtInterval.Value * 1000

END

PUBLIC SUB txtPath_Change()

  DIM sDir AS String
  DIM sPath AS String
  DIM sExt AS String
  
  sPath = txtPath.Text

  IF Exist(sPath) THEN
  
    $aPict.Clear
  
    FOR EACH sDir IN Dir(sPath)

      sExt = LCase(File.Ext(sDir))
      IF sExt = "jpg" OR sExt = "png" OR sExt = "bmp" THEN
        $aPict.Add(sPath &/ sDir)
      ENDIF

    NEXT

  ENDIF

  timChange.Enabled = $aPict.Count

END

PUBLIC SUB timChange_Timer()

  DIM iInd AS Integer

  iInd = Int(Rnd($aPict.Count))

  PRINT $aPict[iInd]
  $hApp.Background.SetWallpaper($aPict[iInd], 1)

END
