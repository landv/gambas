' Gambas class file

Inherits SmtpSession

Property Read Connected As Boolean

Private $hProcess As Process
Private $bConnected As Boolean
Private $sError As String

Public Sub Connect(hClient As SmtpClient, sHost As String, iPort As Integer)

  Dim aExec As String[]
  Dim I As Integer
  
  If Not sHost Then sHost = "localhost"
  If iPort = 0 Then iPort = 587
  
  Super.Connect(hClient, sHost, iPort)

  aExec = [SmtpSession.GetOpenSSLPath(), "s_client", "-quiet", "-starttls", "smtp", "-connect", sHost & ":" & iPort]
  If Me._Debug Then Error "gb.net.smtp: running: "; aExec.Join(" ")

  $hProcess = Exec aExec For Read Write As "OpenSSL"
  $hProcess.Blocking = True
  $hProcess.EndOfLine = gb.Windows
  
  $bConnected = False
  For I = 1 To 10
    Wait 0.1
    If Not $hProcess Then
      If $sError Then 
        Error.Raise(Trim(Split($sError, "\n")[0]))
      Else
        Error.Raise("TLS session has stopped unexpectedly")
      Endif
    Endif
  Next
  
  $bConnected = True 
  
  Me.Stream = $hProcess

End

Public Sub Disconnect()

  $hProcess.Kill()

End

Public Sub OpenSSL_Error(Text As String)
  
  If Me._Debug Then Error "openssl: "; Quote(Text)
  $sError &= Text
  
End

Public Sub OpenSSL_Read()
  
  $bConnected = True
  
End


Public Sub OpenSSL_Kill()

  Super.Disconnect
  $hProcess = Null

End

Private Function Connected_Read() As Boolean

  If $hProcess And If $hProcess.State = Process.Running Then Return True

End
