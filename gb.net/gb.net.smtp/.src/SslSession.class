' Gambas class file

Inherits SmtpSession

Property Read Connected As Boolean

Private $hProcess As Process
Private $sError As String
Private $bConnected As Boolean
Private $bDisconnect As Boolean

Public Sub Connect(hClient As SmtpClient, sHost As String, iPort As Integer)

  Dim aExec As String[]
  
  If Not sHost Then sHost = "localhost"
  If iPort = 0 Then iPort = 465

  Super.Connect(hClient, sHost, iPort)

  aExec = [SmtpSession.GetOpenSSLPath(), "s_client", "-quiet", "-connect", sHost & ":" & iPort]
  If Me._Debug Then Error "gb.net.smtp: running: "; aExec.Join(" ")
  
  $hProcess = Exec aExec For Read Write As "OpenSSL"
  $hProcess.Blocking = True
  $hProcess.EndOfLine = gb.Windows
  
  $bConnected = False
  Repeat
    Wait 0.1
    If Not $hProcess Then
      If $sError Then 
        Error.Raise(Trim(Split($sError, "\n")[0]))
      Else
        Error.Raise("SSL session has stopped unexpectedly")
      Endif
    Endif
  Until $bConnected 
  
  Me.Stream = $hProcess

End

Public Sub Disconnect()

  $bDisconnect = True
  $hProcess.Kill()

End

Public Sub OpenSSL_Error(Text As String)
  
  If Me._Debug Then Error "openssl: "; Quote(Text)
  $sError &= Text
  
End

Public Sub OpenSSL_Read()
  
  $bConnected = True
  
End


Public Sub OpenSSL_Kill()

  Super.Disconnect
  $hProcess = Null

End

Private Function Connected_Read() As Boolean

  If $hProcess And If $hProcess.State = Process.Running Then Return True

End
