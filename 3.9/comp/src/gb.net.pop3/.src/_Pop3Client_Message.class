' Gambas class file

Export

''Returns the content of the message. It is cached after the first request.
Property Read Index As Integer
Property Read Text As String
Property Read Message As MimeMessage
Property Read Deleted As Boolean

''Returns the size of the message. It is cached after the first request.
Property Read Size As Integer

''Returns the unique id of the message. It is cached after the first request.
Property Read UniqueID As String

Private $iMsgId As Integer
Private $oPOPClient As Pop3Client
Private $iSize As Integer
Private $sUID As String
Private $sText As String
Private $hMessage As MimeMessage
Private $bDeleted As Boolean

Public Sub _new(msgid As Integer, $oClient As Pop3Client)

  If $oClient.Debug Then $oClient._PrintDebug("Creating new message instance for " & msgid)

  $iMsgId = msgid + 1

  $oPOPClient = $oClient

End

Private Function Text_Read() As String

  Dim sResponse As String

  If $bDeleted Then Return
  If $sText Then Return $sText

  sResponse = $oPOPClient.Exec("RETR " & $iMsgId, True)
  Pop3Client._CheckResponse(sResponse)

  sResponse = Pop3Client._StripOK(sResponse, True)
  $sText = sResponse
  Return sResponse

End

Private Function Size_Read() As Integer

  Dim sResponse As String

  If $iSize Then Return $iSize
  If $bDeleted Then Return 0

  sResponse = $oPOPClient.Exec("LIST " & $iMsgId, False)
  Pop3Client._CheckResponse(sResponse)

  sResponse = Pop3Client._StripOK(sResponse)
  $iSize = CInt(Split(sResponse, " ")[1])
  Return $iSize

End

''Deletes the message from the mailbox
Public Function Delete() 

  Dim sResponse As String

  sResponse = $oPOPClient.Exec("DELE " & $iMsgId)
  Pop3Client._CheckResponse(sResponse)
  $bDeleted = True

End

''Returns _Lines_ from the top of the message
Public Function Top(Lines As Integer) As String

  Dim sResponse As String

  sResponse = $oPOPClient.Exec(Subst("TOP &1 &2", $iMsgId, Lines), True)
  Pop3Client._CheckResponse(sResponse)

  Return Pop3Client._StripOK(sResponse, True)

End

Private Function UniqueID_Read() As String

  Dim Response As String

  If $sUID Then Return $sUID

  Response = $oPOPClient.Exec("UIDL " & $iMsgId, False)
  Pop3Client._CheckResponse(Response)

  Response = Pop3Client._StripOK(Response)
  $sUID = Split(Response, " ")[1]
  Return $sUID

End

''Clears the cached data
Public Sub Refresh()

  $iSize = 0
  $sText = ""
  $sUID = ""

End

Public Sub _free()

  $oPOPClient = Null

End

Private Function Message_Read() As MimeMessage

  If $bDeleted Then Return
  If Not $hMessage Then $hMessage = New MimeMessage(Me.Text)
  Return $hMessage

End

Private Function Index_Read() As Integer

  Return $iMsgId - 1

End

Private Function Deleted_Read() As Boolean

  Return $bDeleted

End

Public Sub _Reset()
  
  $bDeleted = False
  
End
