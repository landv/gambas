' Gambas class file

'Static Private $aSupport As String[]
'Static Private $iWait As Integer
'Static Private $iSupport As Integer

'Private $aUser As New CUser[]
'Private $hBack As Picture
'Private $iStep As Integer
'Private $iFirst As Integer
'Private $bDrawAbout As Boolean

Private Const NSTEP As Integer = 100

'Private $hWelcome As CWelcome

Private $aCache As Image[]
Private $Y As Integer
Private $YP As Integer

Static Public Sub Run()

  Dim hForm As FAbout

  hForm = New FAbout
  hForm.ShowModal

End

Public Sub _new()

  ' Dim hImage As Image
  ' Dim hBackground As Image
  
  Me.Background = Color.SetAlpha(Color.TextBackground, 90)

  'hImage = Image.Load("img/background/tawhid.png")
  'hBackground = New Image(hImage.W, hImage.H, Color.SetAlpha(Color.TextBackground, 160))
  'hBackground.PaintImage(hImage, 0, 0)
  'Me.Picture = hBackground.Picture
  
End


Public Sub Form_Open()

  '$hWelcome = New CWelcome(dwgWelcome)

End


Public Sub btnClose_Click()

  Me.Close

End


Private Sub RemoveLink(sStr As String) As String
  
  Dim iPos As Integer
  Dim iPos2 As Integer
  
  Do

    iPos = InStr(sStr, "<a href=")
    If iPos = 0 Then Break

    iPos2 = InStr(sStr, ">", iPos)
    If iPos2 = 0 Then iPos2 = Len(sStr) + 1
    sStr = Left(sStr, iPos - 1) & Mid(sStr, iPos2 + 1)
    
  Loop
  
  Return Replace(sStr, "</a>", "")
  
End

Private Function GetStars(iStar As Integer) As String

  Dim sStar As String
  Dim iInd As Integer
  
  For iInd = 1 To iStar
    If ((iInd - 1) Mod 5) = 0 Then
      sStar &= "★"
    Else
      sStar &= "☆"
    Endif
  Next

  Return sStar  

End

Private Sub FillAbout()

  Dim hFic As File
  Dim sText As String
  Dim sLig As String
  Dim sMail As String
  Dim sCountry As String
  Dim sName As String
  Dim aAuthor As New String[]
  Dim aSplit As String[]
  Dim iPos As Integer
  Dim I As Integer
  Dim N As Integer

  hFic = Open "~/gambas/3.0/trunk/app/src/gambas3/authors.txt"

  While Not Eof(hFic)

    Line Input #hFic, sLig
    If Not sLig Then Continue
    If Left(sLig) = "-" Then Continue

    sName = sLig
    Line Input #hFic, sMail
    Line Input #hFic, sCountry
    
    sText = ""
    While Not Eof(hFic)

      Line Input #hFic, sLig
      If Not sLig Then Continue
      If Left(sLig) = "-" Then Break
      
      sLig = RemoveLink(sLig)
      If Right(sLig) <> "." Then sLig &= "."
      sText &= sLig & "<br>"

    Wend
    
    If Right(sText, 4) = "<br>" Then sText = Left(sText, -4)

    aAuthor.Add(sName & "\n\n" & sMail & "\n" & sCountry & "\n" & sText)
    
  Wend

  Close #hFic
  
  hFic = Open "~/gambas/3.0/trunk/app/src/gambas3/support.txt"
  
  While Not Eof(hFic)
    
    Line Input #hFic, sLig
    iPos = InStr(sLig, " ")
    If iPos = 0 Then Break
    sName = Mid$(sLig, iPos + 1)
    sText = sName & "\n" & Left(sLig, iPos - 1)
    
    iPos = aAuthor.Find(sName & "\n*", gb.Like)
    If iPos < 0 Then
      aAuthor.Add(sText)
    Else
      aAuthor[iPos] = sText & Mid$(aAuthor[iPos], Len(sName) + 2)
    Endif
    
  Wend
  
  Close #hFic

  aAuthor.Sort(gb.Language + gb.IgnoreCase)
  
  gvwAbout.Columns.Count = 1
  N = 6
  
  gvwAbout.Rows.Count = aAuthor.Count + N + 1
  gvwAbout.Columns[0].Width = Me.ClientWidth - Desktop.Scale * 6
  gvwAbout.Padding = Desktop.Scale
  
  ' Space
  gvwAbout.Rows[0].H = dwgAbout.H
  ' Gambas
  gvwAbout[1, 0].Text = "Gambas\nAlmost\nMeans\nB A S I C !"
  gvwAbout[1, 0].Font = Font["Bold,+10"]
  gvwAbout[1, 0].Alignment = Align.Top
  ' Logo
  gvwAbout[2, 0].Picture = Picture["img/logo/logo.png"]
  gvwAbout[2, 0].Alignment = Align.Top
  ' Licence title
  'gvwAbout[3, 0].Text = ("License")
  'gvwAbout[3, 0].Font = Font["Bold,+2"]
  'gvwAbout[3, 0].Alignment = Align.Top
  ' Licence
  gvwAbout[4, 0].RichText = lblLicense.Text
  gvwAbout[4, 0].WordWrap = True
  gvwAbout[4, 0].Alignment = Align.Top
  ' Authors title
  gvwAbout[5, 0].Text = ("Authors")
  gvwAbout[5, 0].Font = Font["Bold,+2"]
  gvwAbout[5, 0].Alignment = Align.Top
  
  For I = 1 To 5
    gvwAbout.Rows[I].H = -1
  Next
  gvwAbout.Rows[4].H += Desktop.Scale * 2
  
  For I = 0 To aAuthor.Max
    
    sText = ""
    aSplit = Split(aAuthor[I], "\n")
    
    sName = aSplit[0]

    If aSplit.Count = 2 Then
      
      sText &= "<font size=+1>" & aSplit[0] & "</font>"
      sText &= "<blockquote>"
      sText &= ("Financial support") & "<br>" & GetStars(CInt(aSplit[1]))
      
    Else
      
      sText &= "<b><font size=+1>" & aSplit[0] & "</font>"
      If aSplit[2] Then sText &= " (" & aSplit[2] & ")"
      If aSplit[3] Then sText &= "<br><i>" & aSplit[3] & "</i>"
      sText &= "<blockquote>"
      sText &= aSplit[4]
      If aSplit[1] Then sText &= "<br>" & ("Financial support") & "<br>" & GetStars(CInt(aSplit[1]))
      sText &= "</b>"

    Endif
    
    sText &= "</blockquote>"
    
    gvwAbout[I + N, 0].WordWrap = True
    gvwAbout[I + N, 0].RichText = sText
    gvwAbout.Rows[I + N].Height = -1
    gvwAbout.Rows[I + N].Height += Desktop.Scale * 3
    
  Next

  ' Space
  gvwAbout.Rows[gvwAbout.Rows.Max].H = dwgAbout.H

End

Public Sub timAbout_Timer()

  Dim hImage As Image
  
  Inc $Y
  If $Y >= dwgAbout.H Then 
    $Y = 0
    hImage = $aCache[0]
    $aCache[0] = $aCache[1]
    $aCache[1] = $aCache[2]
    $aCache[2] = hImage
    
    Inc $YP
    If ($YP * dwgAbout.H) >= gvwAbout.ScrollH Then $YP = 0

    PaintAbout($aCache[2], $YP)
    
  Endif
  
  dwgAbout.Refresh

End

Public Sub dwgAbout_Draw()
  
  Paint.DrawImage($aCache[0], 0, - $Y)
  If $Y > 0 Then Paint.DrawImage($aCache[1], 0, dwgAbout.H - $Y)
  
End



Public Sub Form_KeyPress()

  If Key.Code = Key.Escape Then Me.Close

End


Public Sub dwgWelcome_MouseDown()

  Me.Close

End

Private Sub PaintAbout(hImage As Image, iPos As Integer)

  gvwAbout.Move(Me.ClientW, 0, dwgAbout.W, dwgAbout.H)
  gvwAbout.Scroll(0, iPos * dwgAbout.H)
  Paint.Begin(hImage)
  Paint.ClipRect = Rect(0, 0, dwgAbout.W, dwgAbout.H)
  gvwAbout.ScrollArea_Draw
  Paint.End

End

Public Sub Form_Arrange()
  
  Dim I As Integer
  
  FillAbout
  
  $aCache = New Image[3]
  
  For I = 0 To $aCache.Max
    $aCache[I] = New Image(dwgAbout.W, dwgAbout.H)
    PaintAbout($aCache[I], I)
    '$aCache[I].Save("~/cache-" & CStr(I) & ".png")
  Next
  $YP = 2
  
End
