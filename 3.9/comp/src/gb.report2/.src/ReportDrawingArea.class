' Gambas class file

Export
Inherits ReportFrame
Public Const _Properties As String = "*,Cached"
Public Const _Similar As String = "ReportImage"
Public Const _DefaultEvent As String = "Draw"
Public SizeHints As ReportSizeHints
Property Cached As Boolean

Private $bCached As Boolean
Private $iOldResolution As Integer
Event Layout(Width As Float, Height As Float, Index As Integer)
Event Draw(Width As Float, Height As Float, Index As Integer)


Public Sub _GetSizeHints((AvailableW) As Float, (AvailableH) As Float, (TotalWidth) As Float, (TotalHeight) As Float, DataIndex As Integer) As ReportSizeHints
  Dim hMySize As ReportSizeHints
  SizeHints = Null
  Raise Layout(AvailableW, AvailableH, DataIndex)

  hMySize = Super._GetSizeHints(AvailableW, AvailableH, TotalWidth, TotalHeight, DataIndex)
  
  If Me.Autoresize Or If (Me._Width = 0 And Me._Height = 0) Then
    If SizeHints <> Null Then
      hMySize.Width = SizeHints.Width
      hMySize.Height = SizeHints.Height
    Endif
  Endif
      Try hMySize.NotFinished = SizeHints.NotFinished
    Return hMySize

  
End



Public Sub _Paint(Page As Integer, X As Float, Y As Float, hControl As TControl, DataIndex As Integer)
  
  'Dim hRect As Rect
  Dim iX, iY, iW, iH As Integer
  Dim hImg As Image

  iX = (x + hControl.RealLeft + Me.Report._ToPixels(Me.Padding._Left + Me.Border._Left))
  iY = (y + hControl.RealTop + Me.Report._ToPixels(Me.Padding._Top + Me.Border._Top))
  iW = (hControl.RealWidth - Me.Report._ToPixels(Me.Padding._Left + Me.Padding._Right + Me.Border._Left + Me.Border._Right))
  iH = (hControl.RealHeight - Me.Report._ToPixels(Me.Padding._Top + Me.Padding._Bottom + Me.Border._Top + Me.Border._Bottom))
  Paint.Save

  
  If $bCached Then 
    If $iOldResolution <> Paint.ResolutionX Or If hControl.Cache = Null Then
      $iOldResolution = Paint.ResolutionX
      hImg = New Image(iW, iH, Color.Transparent)
      Paint.Begin(hImg)
      Raise Draw(iW, iH, DataIndex)
      Paint.End 
      hControl.Cache = himg
    Else
      'If hControl.cache = Null Then Stop
      Try hImg = hControl.Cache
    Endif
    Draw.Image(hImg, iX, iy)
  Else
    
    'Paint.Translate(iX, iY)
    'Print "Paint.Begin: "; iX;; iY;; iW;; iH;; "(";; Paint.W;; Paint.H; ")"
    Paint.Begin(Paint.Device, RectF(iX, iY, iW, iH))
      Raise Draw(iW, iH, DataIndex)
    Paint.End
    'Paint.Translate(- iX, - iY)
  Endif
  Paint.Restore
  
End

Private Function Cached_Read() As Boolean
  
  Return $bCached
  
End

Private Sub Cached_Write(Value As Boolean)
  
  $bCached = Value
  
End

' Public Sub SetHints(Width As Integer, Height As Integer, NotFinished As Boolean)
'   $hSizeHints = New ReportSizeHints
'   $hSizeHints.Width = Width
'   $hSizeHints.Height = Height
'   $hSizeHints.NotFinished = NotFinished
'   
' End

