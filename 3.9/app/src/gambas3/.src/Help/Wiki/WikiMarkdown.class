' Gambas class file

Inherits Markdown

Public Sub Link(hLink As MarkdownLink)
  
  Dim sPath As String
  Dim sLink As String = hLink.Link
  Dim iPos As Integer
  
  If sLink Begins "/" Then
    sPath = sLink
  Else If sLink Begins "./" Then
    sPath = Wiki.Path &/ Mid$(sLink, 3)
  Else If sLink Begins "../" Then
    sPath = Wiki.MakeAbsolute(sLink, Wiki.Path)
  Else If Not sLink Then
    
    sLink = hLink.Text
    iPos = InStr(sLink, ".")
    If iPos Then
      hLink.Text = ""
      sPath = "/comp/gb" &/ Trim(Left$(sLink, iPos - 1)) &/ Trim(Mid$(sLink, iPos + 1))
    Endif
    
  Else
    
    If sLink Like "*://*" Then hLink.Blank = True
    
  Endif
  
  If Not sPath Then Return
  
  hLink.Query = Mid$(Wiki.LinkSuffix, 2)
  
  If Not hLink.Text Or If hLink.Text = hLink.Link Then
    
    If Wiki.IsImage(sPath) Then
      hLink.Html = "<img src=\"" & Html(MHelp.GetOfflinePath(sPath)) & "\" />"
      Return
    Endif
  
    hLink.Text = Wiki.GetPageTitle(sPath)
    
  Endif
  
  hLink.Link = Wiki.WikiRoot &/ sPath 
  
End

Private Sub MakeError(sMsg As String) As String[]

  Return ["*<span style=\"color:red;\">" & Html(sMsg) & "</span>*"]

End

Public Sub Command(sCommand As String) As String[]

  Dim aArg As String[]
  Dim sDir As String
  Dim aResult As String[]
  Dim iPos As Integer
  Dim sPath As String
  Dim sLetter, sLast As String
  Dim aDir As String[]
  Dim hComp As CComponent
  Dim hClass As CClassInfo
  Dim hSym As CSymbolInfo
  Dim sTitle As String
  Dim sClass As String
  Dim iStart As Integer
  Dim I, J, K, N, NC As Integer
  
  aArg = Split(sCommand, " ", Chr$(34))
  sCommand = aArg[0]
  
  If sCommand = "index" Then
    
    If aArg.Count > 1 Then
      
      sDir = File.Dir(Wiki.GetPagePathFrom(aArg[1]))
      aResult = New String[]
      aDir = New String[]
      
      NC = 2
      For I = 1 To aArg.Max
        If aArg[I] Begins "col=" Then
          Try NC = CInt(Mid$(aArg[I], 5))
        Endif
      Next
      
      If IsDir(sDir) Then
        
        For Each sDir In Dir(sDir, "*", gb.Directory)
          sPath = "/" &/ aArg[1] &/ sDir
          If Not Wiki.ExistPage(sPath) Then Continue
          sTitle = Wiki.GetPageTitle(sPath)
          If aArg.Count > 2 And If Comp(Left(sTitle, Len(aArg[2])), aArg[2], gb.Language + gb.IgnoreCase) = 0 Then sTitle = LTrim(Mid$(sTitle, Len(aArg[2]) + 1))
          
          Do
            sLetter = String.Left(sTitle)
            If Len(sLetter) > 1 Then Break
            If Not IsPunct(sLetter) Then Break
            If Not sTitle Then Break
            sTitle = String.Mid$(sTitle, 2)
          Loop
          
          If Not sTitle Then sTitle = " "
          
          aDir.Add(sTitle & "\n[" & sPath & "]")
        Next
        
        If aDir.Count Then

          aResult.Add("<table class=\"no-border full\">")
            
          aDir.Sort(gb.Natural + gb.IgnoreCase)
          
          iStart = 0
          For I = 0 To aDir.Max
            sLetter = String.UCase(String.Left(aDir[I]))
            If sLetter <> sLast Then
              If I Then GoSub MAKE_INDEX_LETTER
              sLast = sLetter
              iStart = I
            Endif
          Next
          GoSub MAKE_INDEX_LETTER
          
          aResult.Add("</table>")
          
        Endif
        
      Endif
      
      Return aResult
      
    Endif
  
  Else If sCommand = "since" Or If sCommand = "only" Then
  
    Return ["<div class=\"since\">" & ("Since") & "&nbsp;" & Html(aArg[1]) & "</div>"]
  
  Else If sCommand = "classes" And If Wiki.Component Then
    
    hComp = Wiki.Documentation[Wiki.Component]
    If hComp Then Return hComp.WikiGetClasses()

    Return MakeError(("This component does not exist."))
  
  Else If sCommand = "description" And If Wiki.Component Then
    
    hComp = Wiki.Documentation[Wiki.Component]
    If hComp Then Return hComp.WikiGetDescription()

    Return MakeError(("This component does not exist."))
  
  Else If sCommand = "symbols" Then
    
    If aArg.Count >= 2 And If aArg[1] Then
      sClass = aArg[1]
    Else
      sClass = Wiki.Class
    Endif
    
    If sClass Then
      hComp = Wiki.Documentation.Get(Wiki.Component)
      If hComp Then
        hClass = hComp[sClass]
        If hClass Then Return hClass.WikiGetSymbols()
      Endif
    Endif
    
    Return MakeError(sClass & ": " & ("This class does not exist."))
  
  Else If sCommand = "syntax" And If Wiki.Symbol Then
  
    hComp = Wiki.Documentation.Get(Wiki.Component)
    If hComp Then
      hClass = hComp[Wiki.Class]
      If hClass Then 
        hSym = hClass.Symbols[Wiki.Symbol]
        If hSym Then Return hSym.WikiGetSyntax()
      Endif
    Endif
    
    Return MakeError(("This symbol does not exist."))

  Else If sCommand = "stat" Then
    
    Return Wiki.GetStat()

  Else If sCommand = "todo" Or If sCommand = "changes" Then
    
    Return ["Unavailable in offline help."]

  Else
    
    Return ["**COMMAND: `" & sCommand & "`**"]
    
  Endif
  
MAKE_INDEX_LETTER:
  
  aResult.Add("<tr><td><div class=\"letter\">" & sLast & "&nbsp;</div></td>")
  
  N = (I - iStart + NC - 1) \ NC 
  J = iStart
  
  For K = 1 To NC
    
    aResult.Add("<td width=\"" & CStr(100 \ NC) & "%\">")
  
    For J = J To J + N - 1
      If J >= I Then Break
      iPos = InStr(aDir[J], "\n")
      aResult.Add(Mid$(aDir[J], iPos + 1) & "\\")
    Next
  
    aResult.Add("</td>")
    
  Next
  
  aResult.Add("</tr>")
  
  Return
  
End

Public Sub Enter(sClass As String) As String[]
  
  If sClass = "syntax" Then Me.EnableCode(False)
  
End

Public Sub Leave(sClass As String) As String[]
  
  If sClass = "syntax" Then Me.EnableCode(True)
  
End

Public Sub CheckURL(sURL As String, sMarkup As String) As String
  
  Dim sPath As String
  
  If sURL Begins "/wiki/" Then
    If sMarkup = "img" Then
      sPath = Mid$(sURL, 7)
      If sPath Ends "?v" Then sPath = Left(sPath, -2)
      Return MHelp.GetOfflinePath(sPath)
    Endif
  Endif
  
  Return sURL
  
End
