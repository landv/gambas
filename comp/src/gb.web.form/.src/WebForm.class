' Gambas class file

Export
Create Static

Inherits WebContainer

Public Const _IsForm As Boolean = True
Public Const _HiddenControls As String = "WebControl,WebForm,WebWindow,Timer"
Public Const _Properties As String = "*,Title,Resizable"
Public Const _DefaultEvent As String = "Open"

Static Property Debug As Boolean

Static Public _InExec As Integer
Static Public _Current As WebForm

Static Private $bDebug As Boolean

Static Private $aJavascriptBefore As New String[]
Static Private $aJavascript As New String[]
Static Private $aRefresh As New String[]

Static Private $cLibrary As New Collection
'Static Private $aPreload As String[]

Event Open
Event Event
Event Close

Property Title As String
Property Resizable As Boolean
Property Width, W As String
Property Height, H As String
Property Read Popup As Boolean
Property Read PopupParent As WebControl

Static Public _DisableRefresh As Integer
Static Public _HasRefresh As Boolean

Public _Loaded As Boolean
Public _Window As Integer

Private $sTitle As String
Private $bResizable As Boolean
Private $sX As String
Private $sY As String
Private $sWidth As String
Private $sHeight As String

Private $hWindowContainer As WebContainer
'Private $bCentered As Boolean

Static Public Sub Main()

  Dim sPath As String
  Dim hClass As Class
  Dim hForm As WebForm
  Dim iPos As Integer
  Dim sClass As String
  Dim hStat As ClassStat
  Dim aLib As String[]
  Dim sFile As String
  
  System.Language = Request.Language
  'Debug "----";; Application.Request
  
  sPath = Mid$(Request.Path, 2)
  
  If Not sPath Then
    
    hForm = Application.Startup.AutoCreate()
    
  Else If sPath Begins "style:" And sPath Ends ".css" Then
    
    aLib = Split(Left(sPath, -4), ":")
    If aLib.Count >= 3 Then sFile = aLib[1] & ".css"
    RenderStyleSheet(sFile)
    Return
    
  Else If sPath Begins "lib:" And sPath Ends ".js" Then
    
    aLib = Split(Left(sPath, -3), ":")
    If aLib.Count >= 3 Then sFile = aLib[1] & ".js"
    RenderJavascript(sFile)
    Return
  
  Else If sPath Begins "img:" Then
    
    Try sPath = Session[Mid$(sPath, 5)]["Image"]
    If Not Error Then Response.SendFile(sPath)
    Return
  
  Else If Exist("../.public/" &/ sPath) Then
    
    Response.SendFile("../.public/" &/ sPath)
    Return
    
  Else If Exist(".public/" &/ sPath) Then
    
    Response.SendFile(".public/" &/ sPath)
    Return
    
  Else
    
    iPos = InStr(sPath, "/")
    If iPos = 0 Then
      sClass = sPath
      sPath = ""
    Else
      sClass = Left(sPath, iPos - 1)
      sPath = Mid$(sPath, iPos + 1)
    Endif
    
    Try hStat = Class.Stat(".." &/ sClass)
    If Not hStat Then 
      Try hStat = Class.Stat(sClass)
      If Not hStat Then Goto NOT_FOUND
    Endif
    
    If LCase(hStat.Parent) <> "webform" Then Goto NOT_FOUND
    
    Try hClass = Class.Load(sClass)
    If Not hClass Then 
      Debug "Class.Load: "; Error.Text
      Goto NOT_FOUND
    Endif
    
    hForm = hClass.AutoCreate()
    
  Endif
  
  _Current = hForm
  
  If sPath = "x" Then
    hForm._Exec(JSON.Decode(Request["c"]))
  Else If sPath = "u" And If Request.Method = "POST" Then
    hForm._Upload(Request["id"])
  Else
    hForm.Render
  Endif
  
  Return
  
NOT_FOUND:

  Response.Status = "404 NotFound"
  Response.Begin
  Print "<html><body><h1>404 NotFound</h1></body></html>"
  Response.End
  
End

Static Public Sub _AddRefresh(sStr As String)
  
  $aRefresh.Add(sStr)
  
End

Static Public Sub _AddReplyBefore(sStr As String)

  $aJavascriptBefore.Add(sStr)
  
End

Static Public Sub _AddReply(sStr As String)

  $aJavascript.Add(sStr)
  
End


Static Public Sub _AddJavascript(sStr As String)
  
  _AddReply(sStr)
  
End

Public Sub _new()
  
  $hWindowContainer = New WebContainer(Me)
  $hWindowContainer.Ignore = True
  $hWindowContainer._Naked = True
  
End

Public Sub _ready()
  
  Me._InitProperties
  _Loaded = True

End

Public Sub _BeforeRender()
  
  Print "<div class=\"gw-form";
  If Me.Class Then Print " "; Me.Class;
  Print "\" id=\""; Me.Name; "\"";
  Me._RenderStyleSheet()
  Print ">"
  
End

Static Private Sub PrintJavascript()
  
  If $aJavascriptBefore.Count Then 
    Print $aJavascriptBefore.Join("\n")
    $aJavascriptBefore.Clear
  Endif
  
  If _HasRefresh Then
    _HasRefresh = False
    Me._RefreshReply()
  Endif
  
  If $aJavascript.Count Then 
    Print $aJavascript.Join("\n")
    $aJavascript.Clear
  Endif
  
End

Public Sub Render()
  
  Response.Buffered = True
  Response.Begin
  
  Header.Form = Me
  Header.__Render()
  'Print Header.ToString();
  Header.Form = Null
  
  $aJavascript.Clear
  
  Print "<body"; Me._GetEventJS("onload", "open"); ">"
  
  Print "<div id=\"gw-preload\" style=\"display:none;\"></div>"
  Print "<div id=\"gw-modal\" onmousedown=\"gw.window.onDownModal();\"></div>"
  
  Me._BeforeRender()
  Me._Render()
  Me._AfterRender()
  
  Print "<script type=\"text/javascript\">"
  Print "gw.form = "; JS(Object.Type(Me)); ";"
  Print "gw.version = "; JS(Application.Version); ";"
  PrintJavascript
  Print "</script>"
  
  Print "</body>"
  Print "</html>"
  
  Response.End
  
End

Static Private Sub RenderStyleSheet(Optional sFile As String)
  
  If Not sFile Then sFile = "style.css"
  
  If Not Exist(sFile) Then 
    Response.Status = "404 NotFound"
    Response.Begin
    Response.End
    Return
  Endif
  
  Response.Buffered = True
  Response.ContentType = "text/css;charset=utf-8"
  Response.Begin
  
  Print Replace(File.Load(sFile), "//", Application.Root);
  
  If Exist("../.public" &/ sFile) Then Print File.Load("../.public/" &/ sFile);
  
  Response.End
  
End

Static Private Sub RenderJavascript(Optional sFile As String)
  
  If Not sFile Then sFile = "lib.js"
  
  Response.ContentType = "text/javascript;charset=utf-8"
  Response.Begin
  
  If sFile = "lib.js" Then Print "$root = "; JS(Application.Root); ";"
  Print File.Load(sFile)
  
  Response.End
  
End


Private Function Title_Read() As String

  Return $sTitle

End

Private Sub Title_Write(Value As String)

  $sTitle = Value
  Me._SetProperty("Title", Value)

End

Static Public Sub Print(Text As String)
  
  $aJavascript.Add("console.log(" & JS(Text) & ");")
  
End

Public Sub _Exec(aCmd As Variant[])
  
  Dim hCtrl As WebControl
  Dim iOldInExec As Integer = _InExec
  Dim sErr As String
  
  Inc _InExec
  
  Raise Event

  Select Case aCmd[0]
    
    Case "raise"
      
      If aCmd[1] Then
        hCtrl = WebControl.FromName(aCmd[1])
      Else
        hCtrl = Me
      Endif
      
      If Not hCtrl Then Return
      
      Object.Raise(hCtrl, aCmd[2], aCmd[3])
      
    Case "update"
      
      'Debug aCmd[1];; aCmd[2];; aCmd[3]
      
      If aCmd[1] Then
        hCtrl = WebControl.FromName(aCmd[1])
        If Not hCtrl Then Return 'Error.Raise("Unknown control: " & aCmd[1] & " " & aCmd[2] & "\n" & WebControl._GetNames())
      Else
        hCtrl = Me
      Endif
      hCtrl._UpdateProperty(aCmd[2], aCmd[3])
    
  End Select
  
  _InExec = iOldInExec
  
  Response.Begin
  PrintJavascript
  Response.End
  
Catch
  
  sErr = Error.Text & "\n" & Error.Backtrace.Join(" ")
  'Debug sErr
  _InExec = iOldInExec
  
  Response.Begin
  Print "console.log("; JS(aCmd); ");"
  Print "console.log("; JS(sErr); ");"
  Print "console.log("; JS($aJavascript.Join("\n")); ");"
  Response.End
  
End

Public Sub Close()
  
  ' Dim bCancel As Boolean
  Dim hWindow As WebWindow
  
  Raise Close
  
  If Me._Window Then
  
    hWindow = WebControl.FromId(Me._Window)
    With hWindow
      .Close
      .Delete
    End With
    
    _AddReply("gw.window.refresh();")
  
  Else
    
    Me.Delete
    _AddReply("window.location.reload(true);")
    
  Endif
    
End

' Public Sub Open(Child As WebForm)
'   
'   Dim hWindow As WebWindow
'   
'   If $bOpened Then Return
'   
'   hWindow = New WebWindow(Me)
'   hWindow.Child = Child
'   Child._Window = hWindow.Id
'   
'   $bOpened = True
'   
' End

Public Sub Show()
  
  Dim hWindow As WebWindow
  
  If _Window Then Return
  
  hWindow = New WebWindow(WebForm._Current._GetWindowContainer())
  hWindow.Child = Me
  hWindow.Resizable = $bResizable
  hWindow.Move($sX, $sY, $sWidth, $sHeight)
  
  _Window = hWindow.Id
  
  Raise Open
  
End

Public Sub ShowModal()
  
  Dim hWindow As WebWindow
  
  If _Window = 0 Then Show()
  
  hWindow = WebControl.FromId(_Window)
  If hWindow Then hWindow.Modal = True
  
  Raise Open
  
End

Public Sub ShowPopup(Control As WebControl, Optional Alignment As Integer = Align.Left)
  
  Dim hWindow As WebWindow
  
  If _Window Then Return
  
  hWindow = New WebWindow(WebForm._Current._GetWindowContainer())
  hWindow.Child = Me
  hWindow.Resizable = $bResizable
  hWindow.PopupAlignment = Alignment
  hWindow.PopupControl = Control
  hWindow.Modal = True  
  
  _Window = hWindow.Id
  
  Raise Open
  
End



Public Sub Move(X As String, Y As String, Optional Width As String, Optional Height As String)
  
  Dim hWindow As WebWindow = WebControl.FromId(_Window)
  
  $sX = X
  $sY = Y
  If Not IsMissing(Width) Then $sWidth = Width
  If Not IsMissing(Height) Then $sHeight = Height
  '$bCentered = False
  
  If hWindow Then hWindow.Move(X, Y, Width, Height)
  
End

Public Sub Resize(Width As String, Height As String)
  
  Dim hWindow As WebWindow = WebControl.FromId(_Window)
  
  $sWidth = Width
  $sHeight = Height
  '$bCentered = False
  
  If hWindow Then hWindow.Resize(Width, Height)
  
End

Public Sub _UpdateProperty(sProp As String, vValue As Variant)
  
  Dim aWindows As String[]
  Dim sName As String
  
  Select Case sProp
  
    Case "#windows" 
      aWindows = vValue
      Inc _DisableRefresh
      For Each sName In aWindows
        WebControl.FromName(sName).Raise()
      Next
      Dec _DisableRefresh
      
  End Select
  
End

Private Function Resizable_Read() As Boolean

  Return $bResizable

End

Private Sub Resizable_Write(Value As Boolean)

  Dim hWindow As WebWindow = WebControl.FromId(_Window)
  $bResizable = Value
  If hWindow Then hWindow.Resizable = Value

End

' Public Sub Center()
'   
'   Dim hWindow As WebWindow = WebControl.FromId(_Window)
'   
'   $bCentered = True
'   If hWindow Then hWindow.Centered = True
'   
' End

Static Private Function Debug_Read() As Boolean

  Return $bDebug

End

Static Private Sub Debug_Write(Value As Boolean)

  $bDebug = Value
  _AddJavascript("gw.debug = " & JS(Value))

End

Private Function Width_Read() As String

  Return Me.Style["min-width"]

End

Private Sub Width_Write(Value As String)

  Me.Style["min-width"] = Value

End

Private Function Height_Read() As String

  Return Me.Style["min-height"]

End

Private Sub Height_Write(Value As String)

  Me.Style["min-height"] = Value

End

Public Sub _GetWindowContainer() As WebContainer
  
  Return $hWindowContainer
  
End

' Public Sub _Preload(sUrl As String)
'   
'   If Not $aPreload Then $aPreload = New String[]
'   If $aPreload.Exist(sUrl) Then Return
'   $aPreload.Add(sUrl)
'   
' End
' 
Static Public Sub _AddLibrary(sLib As String)
  
  $cLibrary[sLib] = True
  
End

Private Function Popup_Read() As Boolean

  Dim hWindow As WebWindow = WebForm.FromId(Me._Window)
  If hWindow Then Return hWindow.IsPopup()
  
End

Private Function PopupParent_Read() As WebControl

  Dim hWindow As WebWindow = WebForm.FromId(Me._Window)
  If hWindow Then Return hWindow.PopupControl

End

Public Sub _Upload(Id As String)

  Dim hCtrl As Object
  Dim iOldInExec As Integer = _InExec
  Dim sErr As String
  
  Inc _InExec
  
  hCtrl = WebControl.FromName(Id)
  If hCtrl Then hCtrl._UploadFinish
      
  _InExec = iOldInExec
  
  Response.Begin
  PrintJavascript
  Response.End
  
Catch
  
  sErr = Error.Text & "\n" & Error.Backtrace.Join(" ")
  'Debug sErr
  _InExec = iOldInExec
  
  Response.Begin
  Print "console.log("; JS(sErr); ");"
  Print "console.log("; JS($aJavascript.Join("\n")); ");"
  Response.End

End
