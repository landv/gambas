' Gambas class file

Public Name As String
Public Type As Integer
Public ReadOnly As Boolean
Public Length As Integer
Public Key As Boolean
Public {Default} As Variant
Public Serial As Boolean

Public Function GetWidth(hCtrl As Control, Optional bEdit As Boolean) As Integer

  Dim iWidth As Integer
  Dim iLen As Integer

  Select Case Type

    Case db.Boolean
      iWidth = hCtrl.Font.TextWidth(("False"))
      If bEdit Then iWidth += 32

    Case db.Integer, db.Serial
      iWidth = hCtrl.Font.TextWidth("9") * 8

    Case db.Long
      iWidth = hCtrl.Font.TextWidth("9") * 16

    Case db.Float
      iWidth = hCtrl.Font.TextWidth("9") * 16

    Case db.Date
      iWidth = hCtrl.Font.TextWidth("9") * 20 + 12

    Case db.String
      iLen = Length
      If iLen = 0 Then iLen = 255
      iLen = Min(32, iLen)
      iWidth = hCtrl.Font.TextWidth("M") * iLen
      
    Case db.Blob
      iWidth = hCtrl.Font.TextWidth("Blob") + 24 * 3

  End Select

  iWidth = Max(iWidth, hCtrl.Font.TextWidth(Name) + 16)

  Return iWidth

End

Public Sub FromField(hField As Object, Optional hTable As Table)
  
  Dim iType As Integer
  Dim bReadOnly As Boolean
  'Dim bResultField As Boolean = hField Is ResultField
  
  iType = hField.Type
  Name = hField.Name
  Serial = False
  If hTable Then 
    Key = hTable.PrimaryKey.Exist(Name)
    {Default} = hField.Default

    If iType = db.Serial Then
      iType = db.Long
      bReadOnly = True
      Serial = True
    Else
      bReadOnly = Key
    Endif
  Else
    bReadOnly = True
  Endif

  Type = iType
  ReadOnly = bReadOnly
  
  If iType = db.String Then
    Length = hField.Length
  Endif
  
End
