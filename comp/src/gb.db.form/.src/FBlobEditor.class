' Gambas class file

Private Const BY_LINE As Integer = 16

Static Public Data As String

Private $W As Integer
Private $sData As String
Private $sField As String
Private $bCanEdit As Boolean

Private $iRow As Integer
Private $iPos As Integer
  
Public Sub Run(sData As String, sField As String, bCanEdit As Boolean) As Boolean

  $sData = sData
  $sField = sField
  $bCanEdit = bCanEdit
  Me.ShowDialog
  If sData = $sData Then Return True
  Data = $sData
  
End


Private Sub RefreshView()

  lblSize.Text = Subst("&1 byte(s)", Format(Len($sData), ",0")) & " "
  gvwPreview.Rows.Count = (Len($sData) + BY_LINE - 1) \ BY_LINE
  $iRow = -1

End

Public Sub Form_Open()

  $W = gvwPreview.Font.TextWidth("0")

  gvwPreview.Columns.Count = 4
  gvwPreview.Columns[0].W = $W * 10
  gvwPreview.Columns[0].Alignment = Align.Right
  gvwPreview.Columns[1].W = Desktop.Scale * 2
  gvwPreview.Columns[2].W = $W * BY_LINE * 3 + Desktop.Scale * 2

  Me.W = $W * (BY_LINE * 4 + 10) + Desktop.Scale * 9

  If Not $bCanEdit Then
    btnLoad.Hide
    btnClear.Hide
  Endif

  RefreshView

End

Public Sub gvwPreview_MouseMove()

  Dim iRow As Integer
  Dim iCol As Integer
  Dim iPos As Integer
  Dim X, Y As Integer
  
  Try X = Mouse.X
  If Error Then
    X = Mouse.ScreenX - gvwPreview.ScreenX
    Y = Mouse.ScreenY - gvwPreview.ScreenY
  Else
    Y = Mouse.Y
  Endif
  
  iRow = gvwPreview.RowAt(Y)
  iCol = gvwPreview.ColumnAt(X)
  
  Select Case iCol
    Case 2
      iRow = iRow
      iPos = (X - gvwPreview.Columns[iCol].X) \ ($W * 3)
    Case 3
      iRow = iRow
      iPos = (X - gvwPreview.Columns[iCol].X) \ $W
    Case Else
      iRow = -1
  End Select
  
  iPos = Max(0, Min(BY_LINE - 1, iPos))
  If iRow >= 0 And If (iRow * BY_LINE + iPos) > Len($sData) Then iRow = -1
  
  If iRow <> $iRow Or If iPos <> $iPos Then
    $iRow = iRow
    $iPos = iPos
    gvwPreview.Rows[$iRow].Refresh
  Endif
  
End

Public Sub gvwPreview_Leave()
  
  If $iRow >= 0 Then
    $iRow = -1
    gvwPreview.Rows[$iRow].Refresh
  Endif
  
End


Private Sub Warning(sTitle As String, Optional sMsg As String, Optional bError As Boolean)
  
  If Right(sTitle) <> "." Then sTitle &= "."
  If sMsg Then sMsg = "<p>" & sMsg
  sMsg = "<b>" & sTitle & "</b>" & sMsg
  
  If bError Then
    Message.Error(sMsg) 
  Else
    Message.Warning(sMsg)
  Endif

End

Public Sub btnSave_Click()
  
  Dialog.Title = ("Save blob to file")
  Dialog.Path = File.SetBaseName(Dialog.Path, $sField)
  If Not File.Ext(Dialog.Path) Then Dialog.Path = File.SetExt(Dialog.Path, "bin")
  If Dialog.SaveFile() Then Return
  
  File.Save(Dialog.Path, $sData)
  
Catch
  
  Warning(("Unable to save file"), Error.Text)
  
End

Public Sub btnLoad_Click()
  
  Dialog.Title = ("Load blob from file")
  If Dialog.OpenFile() Then Return
  
  $sData = File.Load(Dialog.Path)
  RefreshView
  'GetDataView().TableView_Save($iRow, $iCol, File.Load(Dialog.Path))
  
Catch
  
  Warning(("Unable to load file"), Error.Text)
  
End

Public Sub btnClear_Click()
  
  If Message.Warning(("Do you really want to clear blob?"), ("Clear"), ("Cancel")) <> 1 Then Return
  
  $sData = ""
  RefreshView
  'GetDataView().TableView_Save($iRow, $iCol, "")
  
Catch
  
  Warning(("Unable to clear blob"), Error.Text)
  
End

Public Sub gvwPreview_Draw(X As Integer, Y As Integer, Width As Integer, Height As Integer, Row As Integer, Column As Integer)

  Dim iPos As Integer
  Dim sText As String
  Dim I As Integer
  Dim sCar As String
  Dim SX As Integer

  Paint.Background = Color.TextForeground

  iPos = Row * BY_LINE
  
  Select Case Column
    
    Case 0
      sText = CStr(iPos)
      Paint.DrawText(sText, X, Y, Width, Height, Align.Right)
      
    Case 1
      Return
    
    Case 2

      SX = X
      For I = iPos + 1 To Min(Len($sData), iPos + BY_LINE)
        
        sCar = Hex$(Asc($sData, I), 2)
        Paint.DrawText(sCar, SX, Y, Width, Height, Align.Left)
        SX += $W * 3
        
      Next
    
    Case 3

      SX = X
      For I = iPos + 1 To Min(Len($sData), iPos + BY_LINE)
        
        sCar = Mid$($sData, I, 1)
        If Asc(sCar) < 32 Or If Asc(sCar) >= 127 Then sCar = "."
        Paint.DrawText(sCar, SX, Y, Width, Height, Align.Left)
        SX += $W
        
      Next
    
  End Select
  
  If $iRow = Row Then
    If Column = 2 Then
      Paint.Background = Color.SetAlpha(Color.SelectedBackground, 128)
      Paint.Rectangle(X + $iPos * $W * 3, Y, $W * 2, Height)
      Paint.Fill
    Else If Column = 3 Then
      Paint.Background = Color.SetAlpha(Color.SelectedBackground, 128)
      Paint.Rectangle(X + $iPos * $W, Y, $W, Height)
      Paint.Fill
    Endif
  Endif

End

Public Sub gvwPreview_Scroll()

  gvwPreview_MouseMove

End
