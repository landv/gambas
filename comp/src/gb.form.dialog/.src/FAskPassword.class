' Gambas class file

Class Desktop

Static Public NoUser As Boolean
Static Public (User) As String
Static Public Password As String

Private $bHasDesktop As Boolean
Private $bHasSettings As Boolean
Private $sKey As String

Public Sub Run() As Boolean

  ' Dim hWindow As Window
  Dim bRet As Integer
  Dim iBusy As Integer
  
  ' hWindow = New Window
  ' hWindow.Move(0, 0, Screen.Width, Screen.Height)
  ' hWindow.Background = Color.Black
  ' hWindow.Opacity = 40
  ' hWindow.Border = False
  ' hWindow.SkipTaskbar = True
  ' hWindow.Show
  
  iBusy = Application.Busy
  Application.Busy = 0
  
  bRet = Not Me.ShowModal()
  
  Application.Busy = iBusy
  
  ' hWindow.Close
  
  Return bRet

End

Public Sub btnOK_Click()

  {User} = txtUser.Text
  Password = txtPassword.Text

  If $sKey Then 
    Settings[$sKey &/ "User"] = txtUser.Text 
    Settings[$sKey &/ "Remember"] = chkRemember.Value
    If $bHasDesktop And If chkRemember.Value Then
      Desktop.Passwords[$sKey] = txtPassword.Text
    Endif
  Endif

  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End


Public Sub Form_Open()

  $bHasSettings = Component.IsLoaded("gb.settings")
  $bHasDesktop = Component.IsLoaded("gb.desktop")
  
  If Dialog.Title Then Me.Title = Dialog.Title
  
  If $bHasSettings And If Dialog.Key Then $sKey = "gb.form.dialog/FAskPassword/" &/ Dialog.Key
  
  If NoUser Then
    txtUser.Enabled = False
  Else If $sKey Then
    txtUser.Text = Settings[$sKey &/ "User"]
  Else
    txtUser.Text = {User}
  Endif
  
  If $bHasDesktop And If $sKey Then
    chkRemember.Show
    chkRemember.Value = Settings[$sKey &/ "Remember", False]
    If chkRemember.Value Then
      txtPassword.Text = Desktop.Passwords[$sKey]
    Endif
  Else
    panPassword.H -= chkRemember.H
  Endif
  
  If txtUser.Length Or If NoUser Then
    txtPassword.SetFocus
  Else
    txtUser.SetFocus
  Endif

End

Public Sub Form_Close()

  NoUser = False

End
