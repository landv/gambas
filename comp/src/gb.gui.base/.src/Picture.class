' Gambas class file

Export

Class Stock

Static $cCache As New Collection
Static Private $bInit As Boolean
Static Private $bDarkTheme As Boolean

Static Private Sub GetCurrentComponent() As String

  Dim iPos As Integer
  Dim sFunc As String
  Dim sComp As String
  
  sFunc = System.Backtrace[2]
  iPos = InStr(sFunc, ".")
  Try sComp = Classes[Left(sFunc, iPos - 1)].Component.Name
  
  'Debug sComp
  Return sComp

Catch
  
End


Static Public Sub _get(Path As String) As Picture
  
  Dim sKey As String
  Dim hPict As Picture
  Dim sPath As String
  Dim sBaseName As String
  Dim sDarkPath As String
  
  If Not Path Then Return
  
  If Path Begins "icon:/" Then
    sKey = Path
  Else If File.IsRelative(Path) Then
    sKey = GetCurrentComponent() &/ Path
  Else
    sKey = Path
  Endif
  
  hPict = $cCache[sKey]
  If hPict Then Return hPict
  
  If Path Begins "icon:/" Then
    
    'Debug Path
    Try hPict = Stock[Mid$(Path, 7)]
    
  Else
    
    sPath = Path
    
    While sPath Begins "./"
      sPath = Mid$(sPath, 3)
    Wend
    If File.IsRelative(sPath) Then sPath = ".." &/ sPath
    'Debug Path;; System.Backtrace.Join(" ")
    
    ' Support for rtl/ltr icons
    
    sBaseName = File.BaseName(sPath)
    If sBaseName Ends "-ltr" And If System.RightToLeft Then
      sBaseName = Left(sBaseName, -4) & "-rtl"
      sPath = File.SetBaseName(sPath, sBaseName)
    Else If sBaseName Ends "-rtl" And If Not System.RightToLeft Then
      sBaseName = Left(sBaseName, -4) & "-ltr"
      sPath = File.SetBaseName(sPath, sBaseName)
    Endif
    
    ' Support for dark themes
    
    If Not $bInit Then
      If Color[Color.Desaturate(Color.Background)].Blue < 128 Then $bDarkTheme = True
      $bInit = True
    Endif
    
    If $bDarkTheme Then
      sDarkPath = File.SetBaseName(sPath, sBaseName & "-dark")
      If Exist(sDarkPath) Then
        Try hPict = Image.Load(sDarkPath).Picture
        If hPict Then Goto __LOAD_OK
      Endif
    Endif
    
    Try hPict = Image.Load(sPath).Picture
    If Error Then Return Null
    ' Error "gb.gui: warning: unable to load icon "; Path; ": "; System.Backtrace.Join(" ")
  Endif
  
__LOAD_OK:
  
  $cCache[sKey] = hPict
  Return hPict
  
End

Static Public Sub _put(Value As Picture, Path As String)
  
  Dim sKey As String
  
  If Not Path Then Return
  
  If Path Begins "icon:/" Then
    sKey = Path
  Else If File.IsRelative(Path) Then
    sKey = GetCurrentComponent() &/ Path
  Else
    sKey = Path
  Endif
  
  $cCache[sKey] = Value
  
End

Static Public Sub Flush()
  
  $cCache.Clear
  
End



Static Public Sub _exit()
  
  $cCache.Clear
  
End
