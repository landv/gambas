' Gambas class file

Export

Event _Refresh

Property Read Y, {Top} As Integer
Property Height, H As Integer
Property Selected As Boolean
Property Text, Title As String
Property Border As Border

Public _Row As Integer

Private Sub GetRows() As _GridView_Rows
  
  Return Object.Parent(Me)
  
End

Private Sub GetView() As GridView
  
  Return Object.Parent(Object.Parent(Me))
  
End

Private Function Height_Read() As Integer

  Return GetRows()._GetRowHeight(_Row)

End

Private Sub Height_Write(Value As Integer)

  GetRows()._SetRowHeight(_Row, Value)

End

Private Function Y_Read() As Integer

  Return GetRows()._GetRowY(_Row)

End

Private Function Selected_Read() As Boolean

  Return GetView()._IsRowSelected(_Row)

End

Private Sub Selected_Write(Value As Boolean)

  Dim hView As GridView = GetView()
  Dim hRows As _GridView_Rows = GetRows()
  
  If hView.Mode = Select.Single Then
    hView._SelectRow(_Row, Value)
  Else If hView.Mode = Select.Multiple Then
    If Value Then
      If Not hRows._IsSelected(_Row) Then hRows.Select(_Row)
    Else
      If hRows._IsSelected(_Row) Then hRows.UnSelect(_Row)
    Endif
  Endif

End

Private Function Text_Read() As String

  Return GetRows()._GetRowText(_Row)

End

Private Sub Text_Write(Value As String)

  GetRows()._SetRowText(_Row, Value)

End

Public Sub Refresh()
  
  GetView().Refresh
  
End

Private Function Border_Read() As Border

  Return GetRows()._GetRowBorder(_Row)

End

Private Sub Border_Write(Value As Border)

  GetRows()._SetRowBorder(_Row, Value)

End
