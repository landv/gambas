' Gambas class file

Inherits Panel

Property Text As String
Property Alignment As Integer

Private $hTextArea As TextArea
Private $iPadding As Integer

Public Sub _new(iPadding As Integer, bWrap As Boolean)

  $iPadding = Max(0, iPadding - 1)
  Me.Ignore = True
  Me.Border = Border.Plain
  Me.Background = Color.TextBackground
  
  $hTextArea = New TextArea(Me) As "TextArea"
  $hTextArea.ScrollBar = Scroll.None
  $hTextArea.Wrap = bWrap
  $hTextArea.Border = False
  $hTextArea.Background = Color.TextBackground
  
  Me.Proxy = $hTextArea
  
End


Private Function Text_Read() As String

  Return $hTextArea.Text

End

Private Sub Text_Write(Value As String)

  $hTextArea.Font = Me.Parent.Font
  $hTextArea.Text = Value
  $hTextArea.SelectAll

End

Private Function Alignment_Read() As Integer

  Return $hTextArea.Alignment

End

Private Sub Alignment_Write(Value As Integer)

  $hTextArea.Alignment = Value

End

Public Sub TextArea_Change()
  
  ResizeTextArea
  
End

Public Sub TextArea_GotFocus()
  
  ResizeTextArea
  
End


Private Sub ResizeTextArea()

  Dim Y As Integer
  Dim H As Integer

  If $hTextArea.Wrap Then
    Y = 1 'Max(1, (Me.H - $hTextArea.Font.TextHeight($hTextArea.Text)) \ 2)
    H = Me.H - Y * 2
  Else
    Y = Max(1, (Me.H - $hTextArea.Font.TextHeight($hTextArea.Text)) \ 2)
    H = Me.H - Y * 2
  Endif

  $hTextArea.Move(2, Y, Me.W - 4, H)

End

