' Gambas class file

PUBLIC Name AS String
PUBLIC Type AS String
PUBLIC Kind AS String
PUBLIC Signature AS String
PUBLIC Value AS String
PUBLIC Class AS String
PUBLIC Component AS String
'PUBLIC NotPublic AS Boolean
PUBLIC {Private} AS Boolean
PUBLIC LineNumber AS Integer
PUBLIC Hidden AS Boolean
PUBLIC {Static} AS Boolean

CLASS Picture


PUBLIC FUNCTION GetIcon() AS String

  DIM bStatic AS Boolean
  DIM sIcon AS String

  bStatic = UCase(Kind) = Kind

  SELECT CASE LCase(Kind)

    CASE "r"
      sIcon = If(bStatic, "s-property-read.png", "property-read.png")
    CASE "p"
      sIcon = If(bStatic, "s-property.png", "property.png")
    CASE "v"
      sIcon = If(bStatic, "s-variable.png", "variable.png")
    CASE "c"
      sIcon = "constant.png"
    CASE ":"
      sIcon = "event.png"
    CASE "t"
      sIcon = "control.png"
    CASE "m"
      sIcon = If(bStatic, "s-method.png", "method.png")

  END SELECT

  RETURN sIcon

END


PRIVATE FUNCTION GetType(sType AS String) AS String

  IF Len(sType) = 1 THEN
    sType = Info.GetLongType(sType)
  ELSE IF Left$(sType) = "." THEN
    sType = "Object"
  ENDIF

  RETURN sType

END


PUBLIC FUNCTION GetSignature() AS String

  DIM iInd AS Integer
  DIM sRes AS String
  DIM sCar AS String
  DIM iPos AS Integer
  DIM bName AS Boolean
  DIM bFirst AS Boolean
  DIM sSign AS String

  DIM sBold AS String
  DIM sNoBold AS String

  sSign = Signature
  IF Left$(sSign) = "#" THEN
    RETURN Mid$(sSign, 2)
  ENDIF

  bFirst = TRUE

  DO

    iInd = iInd + 1

    IF iInd > Len(sSign) THEN BREAK

    sCar = Mid$(sSign, iInd, 1)

    IF sCar = "'" OR sCar = "(" THEN

      IF sCar = "'" THEN
        iPos = InStr(sSign, "'", iInd + 1)
      ELSE
        iPos = InStr(sSign, ")", iInd + 1)
      ENDIF

      IF iPos = 0 THEN BREAK

      IF bFirst THEN
        bFirst = FALSE
      ELSE
        sRes &= sBold & "," & sNoBold & " "
      ENDIF

      sRes &= Mid$(sSign, iInd + 1, iPos - iInd - 1) & " " & sBold & "AS" & sNoBold & " "

      iInd = iPos
      bName = TRUE

      CONTINUE

    ENDIF

    IF sCar = "<" THEN

      iPos = InStr(sSign, ">", iInd + 1)
      IF iPos = 0 THEN BREAK
      iInd = iPos
      CONTINUE

    ENDIF

    IF sCar = "[" THEN

      sRes &= " " & sBold & "[" & sNoBold
      CONTINUE

    ENDIF

    IF sCar = "]" THEN

      sRes &= " " & sBold & "] " & sNoBold
      CONTINUE

    ENDIF

    IF NOT bName THEN

      IF bFirst THEN
        bFirst = FALSE
      ELSE
        sRes &= sBold & "," & sNoBold & " "
      ENDIF

      sRes &= sBold & "? AS " & sNoBold
      bName = FALSE

    ENDIF
    IF iInd = Len(sSign) THEN
      IF sCar = "." THEN
        sRes &= ", ..."
        CONTINUE
      ENDIF
    ENDIF

    IF (sCar >= "A" AND sCar <= "Z") OR sCar = "." THEN

      iPos = InStr(sSign, ";", iInd)
      IF iPos = 0 THEN iPos = Len(sSign) + 1

      sRes &= Mid$(sSign, iInd, iPos - iInd)

      iInd = iPos
      CONTINUE

    ENDIF

    sRes &= GetType(sCar)

  LOOP

  IF Name = "_get" OR Name = "_put" THEN
    sRes = "[ " & sRes & " ]"
  ELSE
    sRes = "( " & sRes & " )"
  ENDIF

  IF Type THEN sRes &= " AS " & GetType(Type)

  RETURN sRes

END
