' Gambas class file

Export
Inherits UserControl

Public Const _Properties As String = "*,MinValue=0,MaxValue=100,Step=1,DefaultValue,Value"
Public Const _DefaultEvent As String = "Change"
Public Const _DefaultSize As String = "32,4"
Public Const _Similar As String = "Slider"

Event Change

Property Value As Integer
Property MinValue As Integer
Property MaxValue As Integer
Property Step As Integer
Property DefaultValue As Integer
Property Font As Font

Private $hSlider As Slider
Private $hSpinBox As SpinBox
Private $iStep As Integer = 1
Private $iDefault As Integer

Public Sub _new()
  
  Me._Arrangement = Arrange.Horizontal
  Me._Spacing = True
  
  $hSlider = New Slider(Me) As "Slider"
  $hSlider.Expand = True
  $hSlider.PageStep = 1
  
  $hSpinBox = New SpinBox(Me) As "SpinBox"
  UpdateSpinBoxWidth
  
End


Private Function Value_Read() As Integer

  Return $hSlider.Value

End

Private Sub Value_Write(Value As Integer)

  $hSlider.Value = Value

End

Private Function MinValue_Read() As Integer

  Return $hSlider.MinValue

End

Private Sub UpdateSpinBoxWidth()

  Dim N As Integer

  N = Max(Abs($hSpinBox.MinValue), Abs($hSpinBox.MaxValue))
  If $hSpinBox.MinValue < 0 Then N *= 10

  $hSpinBox.W = Max(Desktop.Scale * 8, (2 + Len(CStr(N))) * $hSpinBox.Font.TextWidth("0") + Desktop.Scale * 2)

End

Private Sub MinValue_Write(Value As Integer)

  $hSlider.MinValue = Value
  $hSpinBox.MinValue = Value
  UpdateSpinBoxWidth

End

Private Function MaxValue_Read() As Integer

  Return $hSlider.MaxValue

End

Private Sub MaxValue_Write(Value As Integer)

  $hSlider.MaxValue = Value
  $hSpinBox.MaxValue = Value
  UpdateSpinBoxWidth

End

Public Sub Slider_Change()
  
  $hSpinBox.Value = $hSlider.Value
  Raise Change
  
End

Public Sub SpinBox_Change()
  
  $hSlider.Value = $hSpinBox.Value
  
End


Private Function Step_Read() As Integer

  Return $iStep

End

Private Sub Step_Write(Value As Integer)

  $iStep = Value
  $hSlider.PageStep = Value

End

Public Sub SpinBox_MouseWheel()
  
  $hSpinBox.Value += If(Mouse.Forward, $iStep, - $iStep)
  Stop Event
  
End

Private Function DefaultValue_Read() As Integer

  Return $iDefault

End

Private Sub DefaultValue_Write(Value As Integer)

  $iDefault = Value

End

Public Sub Slider_DblClick()
  
  $hSlider.Value = $iDefault
  Stop Event
  
End


Private Function Font_Read() As Font

  Return Super.Font

End

Private Sub Font_Write(Value As Font)

  Super.Font = Value
  UpdateSpinBoxWidth

End
