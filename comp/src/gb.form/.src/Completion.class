' Gambas class file

Export

Event Activate

Public List As String[]
Public Icons As String[]
Public Start As Integer

Property Delay As Integer
Property Read Editor As TextBox

Private $hObserver As Observer
Private $hTimer As Timer
Private $hPopup As Panel
Private $hList As GridView
Private $hWatcher As Watcher
Private $bExplicit As Boolean

Private Sub FindTextBox(hCtrl As Control) As Control
  
  If hCtrl Is Container Then
    For Each hCtrl In Container(hCtrl).Children
      If hCtrl Is TextBox Then Return hCtrl
    Next
  Else If hCtrl Is TextBox Then
    Return hCtrl
  Endif
  
End


Private Sub GetTextBox() As TextBox

  Return $hObserver.Object

End

Public Sub _new(hCtrl As Control)
  
  hCtrl = FindTextBox(hCtrl)
  
  $hObserver = New Observer(hCtrl) As "TextBox"
  $hWatcher = New Watcher(hCtrl) As "TextBox"
  
  $hTimer = New Timer As "Timer"
  $hTimer.Delay = 350
  
End

Public Sub Open()
  
  HideList
  $bExplicit = True
  Timer_Timer
  
End


Public Sub TextBox_Change()
  
  If Last.Length = 0 Then HideList
  $hTimer.Start
  
End

Private Sub HideList()

  If $hPopup Then 
    $hPopup.Hide
  Endif
  $hTimer.Stop

End

Public Sub TextBox_KeyPress()
  
  Dim iRow As Integer
  
  If $hPopup And If $hPopup.Visible Then
    
    Select Case Key.Code
      
      Case Key.Down, Key.PageDown
        iRow = $hList.Row + If(Key.Code = Key.Down, 1, $hList.ClientH \ $hList.Rows.H)
        If iRow < $hList.Rows.Count Then $hList.Row = iRow
        Stop Event
        Return
        
      Case Key.Up, Key.PageUp
        iRow = $hList.Row - If(Key.Code = Key.Up, 1, $hList.ClientH \ $hList.Rows.H)
        If iRow >= 0 Then $hList.Row = iRow
        Stop Event
        Return
        
      Case Key.Home
        $hList.Row = 0
        Stop Event
        Return
        
      Case Key.End
        $hList.Row = $hList.Rows.Count - 1
        Stop Event
        Return
        
      Case Key.Enter, Key.Return
        GridView_Click
        Stop Event
        Return
        
      Case Key.Escape
        HideList
        Stop Event
        Return
        
    End Select
    
  Else
    
    If Key.Code = Key.Space And If Key.Control Then
      Me.Open
      Stop Event
    Endif
    
  Endif
  
End


Public Sub Timer_Timer()

  Dim aList As String[]
  Dim hCtrl As TextBox
  Dim H, Y As Integer
  Dim X As Integer
  
  $hTimer.Stop
  
  List = Null
  Me.Fill(GetTextBox().Text, $bExplicit)
  aList = List
  
  $bExplicit = False
  If Not aList Or If aList.Count = 0 Then Return
  
  hCtrl = GetTextBox()
  Object.Lock(hCtrl)
  
  If Not $hPopup Then
    
    '$hPopup = New Window As "Popup"
    '$hPopup.Arrangement = Arrange.Fill
    '$hPopup.Border = False
    '$hPopup.SkipTaskbar = True
    '$hPopup.TopOnly = True
    '$hPopup.Persistent = True
    
    $hPopup = New Panel(GetTextBox().Window)
    $hPopup.Ignore = True
    $hPopup.Border = Border.Plain
    $hPopup.Background = Color.TextBackground
    $hPopup.Foreground = Color.TextForeground
    $hPopup.Arrangement = Arrange.Fill
    
    $hList = New GridView($hPopup) As "GridView"
    $hList.Border = False
    $hList.Grid = False
    $hList.Mode = Select.Single
    $hList.ScrollBar = Scroll.Vertical
    $hList.Columns.Count = 1
    $hList.Padding = 0
    
  Endif
  
  $hList.Rows.Count = aList.Count
  $hList.Row = 0 'Me.GetIndex()
  
  X = hCtrl.Font.TextWidth(String.Left(hCtrl.Text, Start))
  Y = hCtrl.ScreenY - hCtrl.Window.ScreenY + hCtrl.H
  H = $hList.Rows.Height * Min(8, aList.Count) + 2
  H = Min(H, hCtrl.Window.H - Y)
  
  $hPopup.Move(hCtrl.ScreenX - hCtrl.Window.ScreenX + X, Y, Max(hCtrl.W, Desktop.Scale * 24) - X, H)
  $hPopup.Raise
  $hPopup.Show
  
  Object.Unlock(hCtrl)
  
End

Public Sub Popup_Activate()
  
  With GetTextBox()
    .Window.Show
    .SetFocus
  End With
  
End

Public Sub TextBox_Hide()
  
  HideList
  
End

Public Sub TextBox_Move()
  
  HideList
  
End

Public Sub TextBox_Resize()
  
  HideList
  
End

Public Sub TextBox_LostFocus()
  
  HideList
  
End

Private Function Delay_Read() As Integer

  Return $hTimer.Delay

End

Private Sub Delay_Write(Value As Integer)

  $hTimer.Delay = Value

End

Public Sub GridView_Click()

  Dim hCtrl As TextBox
  
  hCtrl = GetTextBox()
  
  hCtrl.Text = String.Left(hCtrl.Text, Start) & List[$hList.Row]
  HideList
  hCtrl.SetFocus
  Raise Activate
  
End

Public Sub GridView_Data(Row As Integer, (Column) As Integer)
  
  Try $hList.Data.Text = List[Row]
  If Icons Then Try $hList.Data.Picture = Picture[Icons[Row]]
  
End


Public Sub Fill(Text As String, Explicit As Boolean)
  
  If Len(Text) < 3 Or If Explicit Then Return
  List = ["Gambas", "Almost", "Means", "Basic!", Text]
  
End

Private Function Editor_Read() As TextBox

  Return $hObserver.Object

End
