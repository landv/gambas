' Gambas class file

Class DesktopMime
Class MediaView

Static Private $aImgExt As String[] = ["png", "jpg", "jpeg", "gif", "bmp", "xpm"]
Static Private $aAudioExt As String[] = ["wav", "mp3", "ogg", "flac"]
Static Private $aVideoExt As String[] = ["mp4", "mov", "avi", "mpg", "mpeg", "flv", "wmv", "mkv"]

Private $sPath As String

Private $iSize As Long
Private $hTask As Task
Private $sError As String
Private $bPreview As Boolean
Private $hPreview As Image

Private $hViewer As MediaView

Private TAB_IMAGE_PREVIEW As Integer = 1
Private TAB_MEDIA_PREVIEW As Integer = 2
Private TAB_ERROR As Integer = 3

Private $bMedia As Boolean
Private $bIgnoreNextTaskError As Boolean

Public Sub Run(sPath As String) As Boolean
  
  SetPath(sPath)
  btnClose.Show
  panMediaClose.Show
  'tabInfo.Border = False
  Me.Margin = True
  Return Not FFileProperties.ShowModal()
  
End

Private Sub FormatPath(sPath As String) As String
  
  If sPath Begins System.User.Home & "/" Then   
    sPath = "~" &/ Mid$(sPath, Len(System.User.Home) + 2)
  Endif 
  
  Return sPath
  
End

Public Sub GetPath() As String
  
  Return $sPath
  
End

Public Sub SetPath(sPath As String)
  
  $bIgnoreNextTaskError = True
  Try $hTask.Stop
  $sPath = sPath
  $bPreview = False
  RefreshPath
  tabInfo_Click
  
End

Private Sub RefreshPath()
  
  Dim hParent As FileProperties = Me.Parent
  Dim hData As _FilePropertiesData
  
  Dim hMime As DesktopMime
  Dim hLabel As Label
  Dim W As Integer
  Dim sAuth As String
  Dim bShowImagePreview As Boolean
  Dim bShowMediaPreview As Boolean

  If hParent Then hData = hParent._RaiseData()

  lblDir.Text = FormatPath(File.Dir($sPath))
  If String.Len(lblDir.Text) >= 32 Then lblDir.Tooltip = lblDir.Text
  
  If Exist($sPath) Then
  
    With DirCache[File.Dir($sPath)].GetInfo(File.Name($sPath))
      
      If hData Then
        
        lblType.Text = hData.Type
        picIcon.Picture = hData.Icon
        
      Else
      
        If .Link Then
          lblType.Text = ("Link")
          'lblType.Font.Italic = True
          hMime = DesktopMime.FromFile($sPath)
          Try picIcon.Picture = hMime.GetIcon(64).Picture
        Else If IsDir($sPath) Then
          picIcon.Picture = Picture["icon:/64/directory"]
          lblType.Text = ("Directory")
        Else If Component.IsLoaded("gb.desktop") Then
          hMime = DesktopMime.FromFile($sPath)
          If hMime Then
            lblType.Text = String.UCaseFirst(hMime.GetComment())
            Try picIcon.Picture = hMime.GetIcon(64).Picture
            If Error Then Error "gb.form: unable to find icon for mimetype: "; hMime.GenericIcon
            If hMime.Type Like "image/*" Then
              bShowImagePreview = True
            Else If $bMedia And If hMime.Type Like "{video,audio}/*" Then
              bShowMediaPreview = True
            Else If $bMedia And If hMime.Type Like "audio/*" Then
              bShowMediaPreview = True
            Endif
          Endif
        Else If $aImgExt.Exist(File.Ext($sPath), gb.IgnoreCase) Then
          picIcon.Picture = Picture["icon:/64/image"]
          lblType.Text = ("Image")
          bShowImagePreview = True
        Else If $bMedia Then
          If $aAudioExt.Exist(File.Ext($sPath), gb.IgnoreCase) Then
            picIcon.Picture = Picture["icon:/64/audio"]
            lblType.Text = ("Audio")
            bShowMediaPreview = True
          Else If $aVideoExt.Exist(File.Ext($sPath), gb.IgnoreCase) Then
            picIcon.Picture = Picture["icon:/64/video"]
            lblType.Text = ("Video")
            bShowMediaPreview = True
          Endif
        Endif
        
      Endif
      
      
      If .Link Then
        lblLabelLink.Show
        lblLink.Show
        lblLink.Text = .Link
      Else
        lblLabelLink.Hide
        lblLink.Hide
      Endif
        
      If IsDir($sPath) Then $hTask = New CTaskDirSize($sPath) As "Task"
      
      $iSize = .Size
      UpdateSize()
      
      lblLastModified.Text = Format(.LastModified, gb.GeneralDate)
      
      sAuth = .Auth
      'sAuth = Left$(sAuth, 3) & " " & Mid$(sAuth, 4, 3) & " " & Right$(sAuth, 3)
      lblAuth.Text = sAuth
      lblUser.Text = .User
      lblGroup.Text = .Group
      
    End With
    
  Else
    
    lblLabelType.Text = ("Type")
    lblType.Text = ""
    lblLastModified.Text = ""
    lblAuth.Text = ""
    lblUser.Text = ""
    lblGroup.Text = ""
    lblSize.Text = ""

  Endif
  
  tabInfo[TAB_IMAGE_PREVIEW].Visible = bShowImagePreview
  tabInfo[TAB_MEDIA_PREVIEW].Visible = bShowMediaPreview
  
  For Each hLabel In panProperty.Children
    If hLabel.Expand Then Continue
    hLabel.Foreground = Color.Merge(Color.Foreground, Color.LightForeground)
    W = Max(W, hLabel.Font.TextWidth(hLabel.Text))
  Next

  For Each hLabel In panProperty.Children
    If hLabel.Expand Then Continue
    hLabel.W = W + Desktop.Scale * 3
  Next
  
  lblName.Text = File.Name($sPath)
  If Me Is Window Then Me.Title = Subst(("&1 properties"), lblName.Text)

End

' Public Sub Form_Open()
' 
'   Me.Title = Subst(("&1 properties"), File.Name($sPath))
' 
' End

Public Sub btnOK_Click()

  Me.Close

End

Private Sub UpdateSize(Optional sDirSize As String)

  Dim sSize As String
  Dim aDirSize As String[]
  Dim nDir, nFile As Integer

  If sDirSize Then
    aDirSize = Split(sDirSize, " ")
    Try $iSize = CLong(aDirSize[0])
    If Error Then Return
    Try nFile = CInt(aDirSize[1])
    Try nDir = CInt(aDirSize[2])
  Endif
  
  sSize = Main.GetFileSize($iSize)
  If $iSize >= 1000 Then sSize &= " (" & Subst(("&1 B"), Format($iSize, "#,###")) & ")"
  
  If sDirSize Then
    sSize &= "\n"
    If nFile = 0 Then
      sSize &= ("no file")
    Else If nFile = 1
      sSize &= ("one file")
    Else
      sSize &= Format(nFile, ",#") & " " & ("files")
    Endif
    sSize &= ", "
    If nDir = 0 Then
      sSize &= ("no directory")
    Else If nDir = 1
      sSize &= ("one directory")
    Else
      sSize &= Format(nDir, ",#") & " " & ("directories")
    Endif
    lblSize.H = Desktop.Scale * 6
  Endif
  
  lblSize.Text = sSize

End

Public Sub Form_Close()

  $bIgnoreNextTaskError = True
  Try $hTask.Stop

End

Public Sub Task_Read(sData As String)

  UpdateSize(Trim(sData))
  lblSize.Foreground = Color.LightForeground
  
End

Private Sub ShowError(Optional sErr As String)

  If $bIgnoreNextTaskError Then
    $bIgnoreNextTaskError = False
    Return
  Endif
  If sErr Then $sError &= sErr
  tabInfo[TAB_ERROR].Visible = True
  txtError.Text = $sError

End

Public Sub Task_Error(Data As String)
  
  ShowError(Data)
  
End


Public Sub Task_Kill()
  
  Try UpdateSize(Last.Value)
  If Error Then ShowError(Error.Text & "\n")
  lblSize.Foreground = Color.Default
  
End

Public Sub Add(hPanel As Control, sTitle As String)

  Dim I As Integer = tabInfo.Index
  Dim N As Integer = tabInfo.Count
  
  Inc tabInfo.Count
  tabInfo[N].Text = sTitle
  tabInfo.Index = N
  hPanel.Reparent(tabInfo)
  HPanel.Expand = True
  hPanel.Show
  tabInfo.Index = I

End

Public Sub Form_Arrange()

  Dim D As Integer
  
  If btnClose.Visible Then 
    D = Desktop.Scale
    If Me.Margin Then D += Desktop.Scale
    btnClose.Move(Me.ClientW - btnClose.W - D, Me.ClientH - btnClose.H - D)
  Endif

End

Public Sub btnClose_Click()

  Me.Close

End

Public Sub GetBorder() As Boolean
  
  Return tabInfo.Border
  
End

Public Sub SetBorder(bBorder As Boolean)
  
  tabInfo.Border = bBorder
  
End


Public Sub _new()

  panIcon.H = 64 + Desktop.Scale * 2
  picIcon.Resize(64, 64)
  tabInfo[TAB_IMAGE_PREVIEW].Visible = False
  tabInfo[TAB_MEDIA_PREVIEW].Visible = False
  tabInfo[TAB_ERROR].Visible = False
  $bMedia = Component.IsLoaded("gb.media")
  
End

Public Sub tabInfo_Click()

  If tabInfo.Index = TAB_IMAGE_PREVIEW Then
    
    If Not $bPreview Then
      
      $bPreview = True
      
      $hPreview = Image.Load($sPath)
      imvPreview.Image = $hPreview
      If Not btnZoomFit.Value Then
        btnZoomFit.Value = True
      Else
        btnZoomFit_Click
      Endif
      
    Endif
    
  Else If tabInfo.Index = TAB_MEDIA_PREVIEW Then

    If Not $bPreview Then
      
      $bPreview = True
      
      If Not $hViewer Then
        Component.Load("gb.media.form")
        $hViewer = New MediaView(tabInfo)
        $hViewer.Expand = True
        $hViewer.Border = False
        $hViewer.Lower
      Endif
      
      $hViewer.URL = $sPath
      $hViewer.Play
      
      panMediaClose.Raise
      
    Endif
    
  Endif
  
Catch

  ShowError(Error.Text & "\n")

End

Private Sub UpdateLabel()

  lblSizePreview.Text = CStr($hPreview.W) & " Ã— " & CStr($hPreview.H) & " (" & Format(imvPreview.Zoom, "0%") & ")"

End

Public Sub sldZoom_Change()

  imvPreview.Zoom = 2 ^ (sldZoom.Value / 4)
  UpdateLabel

End

Public Sub btnZoomNormal_Click()

  btnZoomFit.Value = False
  sldZoom.Value = 0
  imvPreview.Zoom = 1
  UpdateLabel

End

Public Sub btnZoomIn_Click()

  If Not sldZoom.Enabled Then
    btnZoomNormal_Click
  Else
    sldZoom.Value += 4
  Endif

End

Public Sub btnZoomOut_Click()

  If Not sldZoom.Enabled Then
    btnZoomNormal_Click
  Else
    sldZoom.Value -= 4
  Endif

End

Public Sub btnZoomFit_Click()

  If btnZoomFit.Value Then
    imvPreview.ZoomFit
    UpdateLabel
    sldZoom.Enabled = False
  Else
    sldZoom.Enabled = True
    sldZoom_Change
  Endif

End

Public Sub imvPreview_Arrange()

  If tabInfo.Index = TAB_IMAGE_PREVIEW Then 
    If btnZoomFit.Value Then btnZoomFit_Click
  Endif

End
