' Gambas class file

EXPORT
INHERITS UserControl

PUBLIC CONST _Properties AS String = "*,Border=True,Root,Picture,ShowHidden"
PUBLIC CONST _DefaultEvent AS String = "Click"

EVENT {Select}
EVENT Click
EVENT Activate
EVENT Menu
EVENT Icon(Path AS String)

PROPERTY Root AS String
PROPERTY Current AS String
PROPERTY ShowHidden AS Boolean
PROPERTY Picture AS Picture
PROPERTY Border AS Boolean
PROPERTY Icon AS Picture

PRIVATE $sRoot AS String
PRIVATE $hTreeView AS TreeView
PRIVATE $bShowHidden AS Boolean
PRIVATE $hRootPict AS Picture
PRIVATE $hPict AS Picture
PRIVATE $bDeleteOnCancel AS Boolean
PRIVATE $bNoExpand AS Boolean

PUBLIC SUB _new()

  $hTreeView = NEW TreeView(ME) AS "TreeView"
  $hTreeView.Editable = TRUE
  ClearView

END


PRIVATE FUNCTION GetRoot() AS String

  IF Trim($sRoot) THEN RETURN $sRoot
  RETURN User.Home

END



PRIVATE SUB ClearView()

  DIM sRoot AS String = GetRoot()

  $hTreeView.Clear
  AddDir(File.Dir(sRoot), File.Name(sRoot))
  '$hTreeView.Add(sRoot, sRoot, $hRootPict)
  '$hTreeView.Add(sRoot & "/", "",, sRoot)

  'IF NOT $hRootPict THEN Picture_Write(Picture["icon:/16/directory"]) 'open

  $hTreeView.MoveFirst
  $hTreeView.Item.Expanded = TRUE
  TreeView_Expand

END


PRIVATE FUNCTION Root_Read() AS String

  RETURN $sRoot

END

PRIVATE SUB Root_Write(sRoot AS String)

  IF sRoot = $sRoot THEN RETURN
  $sRoot = sRoot
  ClearView

END

PRIVATE SUB AddDir(sDir AS String, sFile AS String)
  
  DIM sParent AS String
  DIM hPict AS Picture
  DIM bNoEdit AS Boolean
  DIM sAfter AS String
  
  IF NOT $bShowHidden AND IF Left(sFile) = "." THEN RETURN
  
  sParent = sDir
  IF GetRoot() = (sDir &/ sFile) THEN 
    sParent = ""
    hPict = $hRootPict
    IF NOT hPict THEN hPict = Picture["icon:/16/directory"]
    bNoEdit = TRUE
  ELSE 
    hPict = GetIcon(sDir &/ sFile)
  ENDIF
  
  sAfter = FindAfter($hTreeView, sFile, sParent)
  
  WITH $hTreeView.Add(sDir &/ sFile, sFile, hPict, sParent)
    .Editable = NOT bNoEdit
    'DEBUG sFile; " -> "; sAfter
    .MoveAfter(sAfter)
  END WITH
  
  $hTreeView.Add(sDir &/ sFile & "/", "",, sDir &/ sFile)
  
END

PUBLIC SUB TreeView_Expand()

  DIM sFile AS String
  DIM sDir AS String
  DIM sDelete AS String

  'DEBUG $hTreeView.Item.Key

  $hTreeView.Item.Selected = TRUE
  sDir = $hTreeView.Item.Key
  IF sDir = GetRoot() THEN
    $hTreeView.Item.Picture = $hRootPict
  ELSE
    $hTreeView.Item.Picture = GetIcon($hTreeView.Item.key) 'open
  ENDIF

  IF $hTreeView.MoveChild() THEN RETURN
  IF $hTreeView.Item.Text THEN RETURN
  sDelete = $hTreeView.Item.Key
  
  INC Application.Busy

  $hTreeView[sDelete].Delete

  FOR EACH sFile IN Dir(sDir, "*", gb.Directory).Sort()
    AddDir(sDir, sFile)
  NEXT

  IF $hTreeView[sDir].Children = 0 THEN
    $hTreeView[sDir].Picture = GetIcon(sDir)
  ELSE
    '$hTreeView[sDir].Expanded = TRUE
  ENDIF

FINALLY

  IF ERROR THEN DEBUG Error.Where; ": "; Error.Text 
  DEC Application.Busy

CATCH

END

PUBLIC SUB TreeView_Collapse()

  'DEBUG $hTreeView.Item.Key
  '$hTreeView.Item.Selected = TRUE
  'IF $hTreeView.Item.Key = GetRoot() THEN RETURN
 ' $hTreeView.Item.Picture = GetIcon(

  $bNoExpand = TRUE
  LAST.Item.Selected = TRUE
  $bNoExpand = FALSE

END

PUBLIC SUB TreeView_Select()

  IF NOT $bNoExpand THEN
    LAST.Item.Expanded = TRUE
  ENDIF
  RAISE {Select}

END

PUBLIC SUB TreeView_Activate()

  RAISE Activate

END

PUBLIC SUB TreeView_Click()

  RAISE Click

END


PUBLIC SUB TreeView_Menu()

  RAISE Menu

END


PRIVATE FUNCTION Current_Read() AS String

  TRY RETURN $hTreeView.Current.Key

END

PRIVATE SUB MoveToLastChild()

  $hTreeView.MoveChild
  REPEAT
  UNTIL $hTreeView.MoveNext()
  $hTreeView.MoveBack
  $hTreeView.Item.EnsureVisible

CATCH

END


PRIVATE SUB Current_Write(sDir AS String)

  DIM sElt AS String
  DIM sSubDir AS String

  sDir = Trim(sDir)
  'IF Right(sDir) = "/" THEN sDir = Left(sDir, - 1)
  IF sDir = Current_Read() THEN RETURN

  'ClearView

  sSubDir = "/"
  FOR EACH sElt IN Split(File.Dir(sDir), "/", "", TRUE)
    sSubDir &/= sElt
    TRY $hTreeView[sSubDir].Expanded = TRUE
  NEXT

  TRY $hTreeView[sDir].Selected = TRUE
  IF NOT ERROR THEN
    $hTreeView.MoveTo(sDir)
    $hTreeView.Item.Expanded = TRUE
    MoveToLastChild
    $hTreeView[sDir].EnsureVisible
    'TRY $hTreeView.MoveNext
    'TRY $hTreeView.Item.EnsureVisible
    RAISE Click
  ENDIF

END


PRIVATE SUB RefreshView()

  DIM sDir AS String

  sDir = Current_Read()
  ClearView
  Current_Write(sDir)

END


PRIVATE FUNCTION ShowHidden_Read() AS Boolean

  RETURN $bShowHidden

END

PRIVATE SUB ShowHidden_Write(bShowHidden AS Boolean)

  IF bShowHidden <> $bShowHidden THEN
    $bShowHidden = bShowHidden
    RefreshView
  ENDIF

END

PRIVATE FUNCTION Picture_Read() AS Picture

  DIM hPict AS Picture

  hPict = $hTreeView[GetRoot()].Picture
  IF hPict <> Picture["icon:/16/directory"] THEN RETURN hPict 'open

END

PRIVATE SUB Picture_Write(hPict AS Picture)

  IF hPict THEN
    $hRootPict = hPict
  ELSE
    $hRootPict = Picture["icon:/16/directory"] 'open
  ENDIF

  $hTreeView[GetRoot()].Picture = $hRootPict

END


PRIVATE SUB Border_Write(bBorder AS Boolean)

  $hTreeView.Border = bBorder

END

PRIVATE FUNCTION Border_Read() AS Boolean

  RETURN $hTreeView.Border

END

PRIVATE FUNCTION GetIcon(sPath AS String) AS picture

  IF sPath = GetRoot() THEN RETURN $hRootPict  
  $hPict = NULL
  RAISE Icon(sPath)
  IF $hPict THEN RETURN $hPict
  RETURN Picture["icon:/16/directory"]
  
END


PRIVATE FUNCTION Icon_Read() AS Picture

  RETURN $hPict

END

PRIVATE SUB Icon_Write(Value AS Picture)

  IF Value.Height <> 16 THEN 
    Value = Value.Image.Stretch(Value.Width * 16 / Value.Height, 16).Picture
  ENDIF
  $hPict = Value

END

PUBLIC SUB TreeView_Rename()
  
  DIM sKey AS String = $hTreeView.Item.Key
  DIM sParent AS String
  DIM sNewName AS String = $hTreeView.Item.Text
  
  IF $hTreeView.MoveParent() THEN RETURN
  IF sNewName = File.Name(sKey) THEN RETURN 
  
  MOVE sKey TO File.SetName(sKey, sNewName)
  
  sParent = File.Dir(sKey)
  
  ' TODO: Optimization
  
  Object.Lock($hTreeView)
  $hTreeView.Remove(sKey)
  sKey = File.SetName(sKey, sNewName)
  AddDir(File.Dir(sKey), File.Name(sKey))
  Object.Unlock($hTreeView)
  
  $hTreeView[sKey].Selected = TRUE
  
  $bDeleteOnCancel = FALSE
  
CATCH 

  Message.Error(("Cannot rename directory.") & "\n\n" & Error.Text)  
  
END

PUBLIC SUB TreeView_Cancel()
  
  IF NOT $bDeleteOnCancel THEN RETURN
  
  TRY RMDIR $hTreeView.Item.Key
  $hTreeView.Item.Delete

  $bDeleteOnCancel = FALSE
  
END


PUBLIC SUB NewFolder()
  
  DIM sDir AS String
  DIM iInd AS Integer
  
  sDir = ("New folder")
  iInd = 1
  
  DO
    IF NOT Exist(Current_Read() &/ sDir) THEN BREAK
    INC iInd
    sDir = ("New folder") & " (" & CStr(iInd) & ")"
  LOOP  
  
  MKDIR Current_Read() &/ sDir
  AddDir(Current_Read(), sDir)
  $bDeleteOnCancel = TRUE
  $hTreeView[Current_Read() &/ sDir].Rename
  
CATCH

  Message.Error(("Cannot create directory.") & "\n\n" & Error.Text)
  
END

PRIVATE SUB FindAfter(hView AS TreeView, sText AS String, OPTIONAL sParent AS String) AS String
  
  DIM sAfter AS String
  DIM iConv AS Integer
  
  WITH hView
    IF sParent THEN
      .MoveTo(sParent)
      .MoveChild
    ELSE 
      .MoveFirst
    ENDIF
    WHILE .Available
      IF Comp(.Item.Text, sText, gb.Text + gb.Language) > 0 THEN BREAK
      IF iConv > 0 THEN BREAK
      sAfter = .Item.Key
      .MoveNext
    WEND
  END WITH

  RETURN sAfter
  
END

PUBLIC SUB Reload()
  
  RefreshView
  
END
