' Gambas class file

Inherits TextHighlighter

Public Const Name As String = "gambas"

Class Highlight

Static Public Sub _init()
  
  Component.Load("gb.eval")
  
End

Public Sub GetKeywords() As String[]
  
  Return System.Keywords
  
End

Private Sub IsProc() As Boolean

  Dim aSym As String[]
  Dim I As Integer
  Dim sSym As String

  aSym = Highlight.Symbols
  
  For I = 0 To aSym.Max
    sSym = aSym[I]
    If sSym == "PRIVATE" Or If sSym == "PUBLIC" Or If sSym == "STATIC" Or If sSym == "FAST" Then Continue
    If sSym == "SUB" Or If sSym == "PROCEDURE" Or If sSym == "FUNCTION" Then
      Return True
    Else
      Return False
    Endif
  Next

End

Public Sub Run(Text As String)
  
  Dim iState As Integer
  Dim iTag As Integer
  Dim iPos As Integer
  Dim I As Integer
  Dim iLen As Integer
  
  iState = TextHighlighter.State
  iTag = TextHighlighter.Tag
  
  Highlight.Analyze(Text, True)
  TextHighlighter.Limit = IsProc()
  
  iPos = 0
  For I = 0 To Highlight.Symbols.Max
    
    If iPos < Highlight.Positions[I] Then 
      TextHighlighter.Add(Highlight.Normal, Highlight.Positions[I] - iPos)
      iPos = Highlight.Positions[I]
    Endif
    
    iLen = String.Len(Highlight.Symbols[I])
    TextHighlighter.Add(Highlight.Types[I], iLen)
    iPos += iLen
    
  Next

  If iPos < String.Len(Highlight.TextAfter) Then 
    TextHighlighter.Add(Highlight.Normal, String.Len(Highlight.TextAfter) - iPos)
  Endif
  
  TextHighlighter.State = iState
  TextHighlighter.Tag = iTag
  TextHighlighter.TextAfter = Highlight.TextAfter

End
