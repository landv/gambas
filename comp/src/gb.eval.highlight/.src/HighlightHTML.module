' Gambas module file

' Gambas module file

Public Const STYLE_HTML As Integer = 1
Public Const STYLE_WEB As Integer = 2
Public Const STYLE_CSS As Integer = 3

Private Const TAG_HTML As Integer = 0
Private Const TAG_CODE As Integer = 1
Private Const TAG_SCRIPT As Integer = 2
Private Const TAG_STYLE As Integer = 3

Private Sub AnalyzeCode(sText As String) As String
  
  Dim iInd As Integer
  Dim iPos As Integer
  Dim iLen As Integer
  
  If Not sText Then Return
  
  Highlight.Analyze(sText, True)
  
  iPos = 0
  For iInd = 0 To Highlight.Symbols.Max
    
    If iPos < Highlight.Positions[iInd] Then 
      Highlight.Add(Highlight.Normal, Highlight.Positions[iInd] - iPos)
      iPos = Highlight.Positions[iInd]
    Endif
    
    iLen = String.Len(Highlight.Symbols[iInd])
    Highlight.Add(Highlight.Types[iInd], iLen)
    iPos += iLen
    
  Next

  If iPos < String.Len(Highlight.TextAfter) Then 
    Highlight.Add(Highlight.Normal, String.Len(Highlight.TextAfter) - iPos)
  Endif
  
  Return Highlight.TextAfter

End

Public Sub Run(iStyle As Integer)

  Dim iState As Integer
  Dim iNextState As Integer
  Dim iSaveState As Integer
  Dim iSaveNextState As Integer
  Dim iInd As Integer
  Dim J As Integer
  Dim sText As String
  Dim sOrgText As String
  Dim sTextAfter As String
  Dim sCar As String
  Dim iPos As Integer
  Dim bMarkup As Boolean
  Dim bQuoteMarkup As Boolean
  Dim bQuote As Boolean
  Dim bLimit As Boolean
  Dim bCode As Boolean
  Dim iTag As Integer
  Dim iCount As Integer
  Dim iCodeStart As Integer
  Dim bScript, bStyle As Boolean
  Dim sMarkup As String

  iState = Highlight.State
  iTag = Highlight.Tag
  sText = Highlight.Text
  sOrgText = sText

  'PRINT "Highlight:";; iState;; iTag;; sText

  Select Case iStyle
  
    Case STYLE_HTML, STYLE_WEB

      Select Case iTag
        Case TAG_HTML
          bMarkup = iState = Highlight.Keyword
        Case TAG_CODE
          bCode = True
          iCodeStart = 1
        Case TAG_SCRIPT
          bScript = True
        Case TAG_STYLE
          bStyle = True
      End Select
    
      For iInd = 1 To String.Len(sText)
    
        iNextState = iState
        sCar = String.Mid$(sText, iInd, 1)
        'sCar = String.Left(sText)
        'sText = String.Mid$(sText, 2)
    
        If bCode Then
        
          If bQuote Then
            If sCar = Chr$(34) Then 
              bQuote = False
            Endif
          Else If sCar = Chr$(34) Then
            bQuote = True
          Else If sCar = "'" Then
            Break
          Else If String.Mid$(sText, iInd, 2) = "%>" Then
            bCode = False
            sTextAfter &= AnalyzeCode(String.Mid$(sText, iCodeStart, iInd - iCodeStart))
            Highlight.Add(Highlight.Keyword, 2)
            sTextAfter &= "%>"
            Inc iInd
            Highlight.AlternateState = False
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
            'iNextState = iSaveNextState 'Highlight.Normal
          Endif
          Continue
          
        Else If bScript Then
          
          If String.Mid$(sText, iInd, 9) = "</script>" Then
            bScript = False
            Highlight.AlternateState = False
            Highlight.Add(Highlight.Keyword, 9)
            iInd += 8
            sTextAfter &= "</script>"
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
          Else
            sTextAfter &= sCar
            Highlight.Add(iState)
            iState = iNextState
          Endif
          Continue

        Else If bStyle Then
          
          If String.Mid$(sText, iInd, 8) = "</style>" Then
            bStyle = False
            Highlight.AlternateState = False
            Highlight.Add(Highlight.Keyword, 8)
            iInd += 7
            sTextAfter &= "</style>"
            iState = iSaveState 'Highlight.Normal
            iNextState = iState
          Else
            sTextAfter &= sCar
            Highlight.Add(iState)
            iState = iNextState
          Endif
          Continue

        Else If iStyle = STYLE_WEB And If String.Mid$(sText, iInd, 2) = "<%" Then
        
          If String.Mid$(sText, iInd + 2, 1) = "=" Then
            iCount = 3
          Else 
            iCount = 2
          Endif
          Highlight.AlternateState = True
          Highlight.Add(Highlight.Keyword, iCount)
          sTextAfter &= String.Mid$(sText, iInd, iCount)
          iInd += iCount - 1
          
          iSaveState = iState
          'iSaveNextState = iNextState
          iState = Highlight.Normal
          iNextState = Highlight.Normal
          
          bCode = True
          iCodeStart = iInd + 1
          Continue
          
        Else If bMarkup Then
    
          If bQuoteMarkup Then
            If sCar = Chr$(34) Then 
              bQuoteMarkup = False
              iNextState = Highlight.Operator
            Endif
          Else If sCar = ">" Then
            bMarkup = False
            If sMarkup Then
              Highlight.Add(Highlight.Keyword, 1)
              sTextAfter &= sCar
              iSaveState = iState
              iState = Highlight.Normal
              iNextState = Highlight.Normal
              If sMarkup = "script" Then
                bScript = True
              Else If sMarkup = "style" Then
                bStyle = True
              Endif
              Highlight.AlternateState = True
              Continue
            Else
              iState = Highlight.Keyword
              iNextState = Highlight.Normal
            Endif
          Else If sCar = " " Then
            iNextState = Highlight.Operator
          Else If sCar = "=" Then
            iNextState = Highlight.String
          Else If sCar = Chr$(34) Then 
            bQuoteMarkup = True
          Endif
    
        Else
    
          Select Case iState
            Case Highlight.Normal
              If sCar = "<" Then
                If String.Mid$(sText, iInd, 4) = "<!--" Then
                  iState = Highlight.Comment
                  iNextState = Highlight.Comment
                Else If String.Mid$(sText, iInd, 9) = "<![CDATA[" Then
                  iState = Highlight.Symbol
                  iNextState = Highlight.Symbol
                Else
                  iState = Highlight.Keyword
                  iNextState = Highlight.Keyword
                  bMarkup = True
                  sMarkup = ""
                  If String.Mid$(sText, iInd, 5) = "<body" Or If String.Mid$(sText, iInd, 5) = "<head" Then 
                    bLimit = True
                  Else If String.Mid$(sText, iInd, 7) = "<script" Then
                    sMarkup = "script"
                  Else If String.Mid$(sText, iInd, 6) = "<style" Then
                    sMarkup = "style"
                  Endif
                Endif
              Else If sCar = "&" Then
                iPos = String.InStr(sText, ";", iInd)
                If iPos = 0 Or iPos = iInd + 1 Then
                  iState = Highlight.Error
                Else
                  For J = iInd + 1 To iPos - 1
                    sCar = String.Mid$(sText, J, 1)
                    If IsLetter(sCar) Then Continue
                    If IsDigit(sCar) Then Continue
                    If InStr("_#", sCar) Then Continue
                    Break
                  Next
                  If J = iPos Then
                    Highlight.Add(Highlight.Number, iPos - iInd + 1)
                    sTextAfter &= String.Mid$(sText, iInd, iPos - iInd + 1)
                    iInd = iPos
                    Continue
                  Else
                    iState = Highlight.Error
                  Endif
                Endif
              Endif
            Case Highlight.Comment
              If sCar = ">" And If iInd > 2 And If String.Mid$(sText, iInd - 2, 2) = "--" Then
                iNextState = Highlight.Normal
              Endif
            Case Highlight.Symbol
              If sCar = ">" And If iInd > 2 And If String.Mid$(sText, iInd - 2, 2) = "]]" Then
                iNextState = Highlight.Normal
              Endif
          End Select
    
        Endif
    
        sTextAfter &= sCar
        Highlight.Add(iState)
        iState = iNextState
    
      Next
    
      If bCode Then
        sTextAfter &= AnalyzeCode(String.Mid$(sText, iCodeStart))
        iTag = TAG_CODE
      Else If bScript Then
        iTag = TAG_SCRIPT
      Else If bStyle Then
        iTag = TAG_STYLE
      Else
        iTag = TAG_HTML
        If iNextState <> Highlight.Comment And If iNextState <> Highlight.Symbol Then
          If bMarkup Then 
            iNextState = Highlight.Keyword
          Else 
            iNextState = Highlight.Normal
          Endif
        Endif
      Endif
      
      Highlight.Text = sTextAfter
      
    Case STYLE_CSS
    
      For iInd = 1 To String.Len(sText)
    
        iNextState = iState
        sCar = String.Mid$(sText, iInd, 1)

        Select Case iState
        
          Case Highlight.Normal, Highlight.String
            If sCar = "." Or If sCar = "#" Then 
              iState = Highlight.String
              iNextState = iState
            Else If sCar = " " Then 
              iState = Highlight.Normal
              iNextState = iState
            Else If sCar = "{" Then 
              iState = Highlight.Keyword
              iNextState = iState
              iTag = Highlight.Keyword
              bLimit = True
            Else If sCar = "/" Then 
              If Mid$(sText, iInd, 2) = "/*" Then 
                iState = Highlight.Comment
                iNextState = iState
              Endif
            Endif
            
          Case Highlight.Keyword, Highlight.Number
            If sCar = ":" Then 
              iNextState = Highlight.Number
            Else If sCar = ";" Then 
              iState = Highlight.Keyword
              iNextState = iState
            Else If sCar = "}" Then 
              iNextState = Highlight.Normal
              iTag = 0
            Else If sCar = "/" Then 
              If Mid$(sText, iInd, 2) = "/*" Then 
                iState = Highlight.Comment
                iNextState = iState
              Endif
            Endif
                        
          Case Highlight.Comment
            If sCar = "/" And If iInd > 1 And If String.Mid$(sText, iInd - 1, 1) = "*" Then
              iNextState = IIf(iTag, iTag, Highlight.Normal)
            Endif
            
        End Select
        
        Highlight.Add(iState)
        iState = iNextState
    
      Next
    
  End Select 

  Highlight.State = iNextState
  Highlight.Tag = iTag
  Highlight.ShowLimit = bLimit

End
