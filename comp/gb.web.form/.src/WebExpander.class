' Gambas class file

Export
Inherits WebContainer

Public Const _Properties As String = "*,Text,Hidden"
Public Const _DefaultEvent As String = "Show"
Public Const _Similar As String = "WebContainer"
Public Const _DrawWith As String = "Expander"

Event Show
Event Hide

Property Hidden As Boolean
Property Text As String
Property Border As Boolean

Private $hCont As WebContainer
Private $bHidden As Boolean
Private $sText As String
Private $bBorder As Boolean

Public Sub _new()
  
  $hCont = New WebContainer(Me)
  Me._Container = $hCont
  
End


Private Function Hidden_Read() As Boolean

  Return $bHidden

End

Private Sub Hidden_Write(Value As Boolean)

  $bHidden = Value
  Me._SetProperty("Hidden", Value)
  If $bHidden Then
    Raise Hide
  Else
    Raise Show
  Endif

End

Private Function Text_Read() As String

  Return $sText

End

Private Sub Text_Write(Value As String)

  $sText = Value
  Me._SetProperty("Text", Value)

End


Public Sub _Render()
  
  If $bBorder Then Print "<div class=\"gw-expander-border\">"
  
  Print "<div class=\"gw-expander-header\">";
  Print "<img src=\""; Application.Root &/ If($bHidden, "gw-arrow-right.png", "gw-arrow-down.png"); "\""; Me._GetUpdateJS("onclick", "hidden", JS(Not $bHidden)); ">";
  Print "<div>"; $sText; "</div>";
  Print "</div>"
  
  If Not $bHidden Then
    With $hCont
      ._BeforeRender()
      ._Render()
      ._AfterRender()
    End With
  Endif
  
  If $bBorder Then Print "</div>"
  
End

Public Sub _UpdateProperty(sProp As String, vValue As Variant)
  
  If sProp = "hidden" Then Try Me.Hidden = vValue
  
End


Private Function Border_Read() As Boolean

  Return $bBorder

End

Private Sub Border_Write(Value As Boolean)

  $bBorder = Value
  Me._SetProperty("Border", Value)

End
