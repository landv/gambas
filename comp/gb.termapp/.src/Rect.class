' Gambas class file

Class Rect
Public Left As Integer
Public Top As Integer
Public Width As Integer
Public Height As Integer
Property Right As Integer
Property Bottom As Integer

Private Function Right_Read() As Integer

  Return Left + Width

End

Private Sub Right_Write(Value As Integer)

  Width = Value - Left

End

Private Function Bottom_Read() As Integer

  Return Top + Height

End

Private Sub Bottom_Write(Value As Integer)

  Height = Value - Top

End

Public Function Union(hRect As Rect) As Rect

  Dim rRect As New Rect

  rRect.Left = Min(Me.Left, hRect.Left)
  rRect.Top = Min(Me.Top, hRect.Top)
  rRect.Bottom = Max(Me.Bottom, hRect.Bottom)
  rRect.Right = Max(Me.Right, hRect.Right)

  Return rRect

End

Public Function Intersection(hRect As Rect) As Rect

  Dim hRet As Rect
  Dim x, y, x2, y2 As Integer
  If Not hRect Then Return

  x = Max(Me.Left, hRect.Left)
  y = Max(Me.Top, hRect.Top)
  x2 = Min(Me.Left + Me.Width, hRect.Left + hRect.Width)
  y2 = Min(Me.Top + Me.Height, hRect.Top + hRect.Height)

  If x2 > x And y2 > y Then

    hRet = New Rect
    hRet.Left = x
    hRet.Top = y
    hRet.Width = x2 - x
    hRet.Height = y2 - y
    Return hRet
    
  Endif

  Return

End
